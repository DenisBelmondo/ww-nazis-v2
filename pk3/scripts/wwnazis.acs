#library "WWNAZIS"
#include "zcommon.acs"

SCRIPT "WWNaziTaunt" (int tauntType)
{
	int tauntChance,tauntDelay;

	delay(20); // Appropriate wait time until player is done with the taunt-worthy action.
	tauntChance = random(1,5);
	tauntDelay = 5 * 35;
	//Print(i:tauntChance);

	if(tauntType == 1 && tauntChance == 5) {
		PlaySound(0, "player/taunt", CHAN_VOICE, 1.0);
		Delay(tauntDelay); } // argument 1: taunt from firing
	if(tauntType == 2 && tauntChance >= 2) {
		PlaySound(0, "player/reload", CHAN_VOICE, 1.0);
		Delay(tauntDelay); } // 2: taunt from reloading
	if(tauntType == 3 && tauntChance >= 3) {
		PlaySound(0, "player/grenadecall", CHAN_VOICE, 1.0);
		Delay(tauntDelay); } // 3: throwing a grenade
	//Log(s:"Taunt ready!");
}
str weapons[21]=
{
	"Melee","Firebrand","Luger9mm","Walther9mm","WeaselsRevolver",
	"M1911","TrenchShotgun","Browning5","MP40","PPSH41",
	"Thompson","Kar98k","G43","Enfield","Garand",
	"STG44","FG42","Pyrolight","Nebelwerfer","Panzerschreck",
	"UMG43"
};
str weaponsfancy[21]=
{
	"Shovel","Firebrand","Luger P-08","Walther P-38",".357 Revolver",
	"Colt M1911","M1897 Trenchgun","Browning Auto-5","MP-40","PPSh-41",
	"Thompson M1A1","Karabiner 98k","Gewehr 43","Lee-Enfield SMLE","M1 Garand",
	"Sturmgewehr 44","FG 42","PyroLight","Klein Nebelwerfer","Panzerschreck",
	"UMG-43"
};
script "WWNaziStartingInventoryCheck" ENTER
{
	for(int i=0;i<=20;i++)
	{
		if(CheckInventory(weapons[i]))
			GiveInventory(StrParam(s:weapons[i],s:"StartCheck"),1);
	}
}
script "WWNaziWeaponCollect"(int weapon)
{
	int load,i,currentweapon,min,max;
	if(weapon<=2)//Melee
	{
		min=0;
		max=1;
	}
	else if(weapon>=3&&weapon<=6)//Pistol
	{
		min=2;
		max=5;
	}
	else if(weapon>=7&&weapon<=11)//Shotgun/SMG
	{
		min=6;
		max=10;
	}
	else if(weapon>=12&&weapon<=17)//Rifle/Machinegun
	{
		min=11;
		max=16;
	}
	else if(weapon>=18)//Heavy slot
	{
		min=17;
		max=20;
	}
	for(i=min;i<=max;i++)
	{
		if(CheckInventory(weapons[i]))
			currentweapon=i+1;
	}
	if(currentweapon)	//You have a weapon for this slot
	{
		if(CheckInventory("IJustTraded"))
			SetResultValue(0);
		else
		{
			hudmessage(s:"Tap ",k:"+use",s:" to trade the ",s:weaponsfancy[currentweapon-1],s:" for the ",s:weaponsfancy[weapon-1],s:".";HUDMSG_PLAIN,887,CR_GOLD,0.5,0.375,3.0);
			int buttons = GetPlayerInput(-1, INPUT_BUTTONS);
			if(buttons & BT_USE)
			{
				GiveInventory("IJustTraded",1);
				TakeInventory(weapons[currentweapon-1],1);
				DropItem(0,StrParam(s:weapons[currentweapon-1],s:"Pickup"));
				GiveInventory(weapons[weapon-1],1);
				if(!GetCVAR("neverswitchonpickup"))
					SetWeapon(weapons[weapon-1]);
				if(!CheckInventory(StrParam(s:weapons[weapon-1],s:"StartCheck")))
				{
					GiveInventory(StrParam(s:weapons[weapon-1],s:"StartCheck"),1);
					if(weapon!=0&&weapon!=1&&weapon!=19&&weapon!=21)
						GiveInventory(StrParam(s:weapons[weapon-1],s:"Loaded"),999);
				}
				if(weapon==21)
					PlaySound(0,"chaingun/get",CHAN_WEAPON);
				else
					PlaySound(0,"*weaponlaugh",CHAN_WEAPON);
				hudmessage(s:"Traded the ",s:weaponsfancy[currentweapon-1],s:" for the ",s:weaponsfancy[weapon-1],s:"!";HUDMSG_PLAIN,887,CR_GOLD,0.5,0.375,3.0);
				SetResultValue(1);
			}
			else
				SetResultValue(0);
		}
	}
	else
	{
		GiveInventory(weapons[weapon-1],1);
		if(!GetCVAR("neverswitchonpickup"))
			SetWeapon(weapons[weapon-1]);
		if(!CheckInventory(StrParam(s:weapons[weapon-1],s:"StartCheck")))
		{
			GiveInventory(StrParam(s:weapons[weapon-1],s:"StartCheck"),1);
			if(weapon!=0&&weapon!=1&&weapon!=19&&weapon!=21)
				GiveInventory(StrParam(s:weapons[weapon-1],s:"Loaded"),999);
		}
		if(weapon==21)
			PlaySound(0,"chaingun/get",CHAN_WEAPON);
		else
			PlaySound(0,"*weaponlaugh",CHAN_WEAPON);
		hudmessage(s:"Picked up the ",s:weaponsfancy[weapon-1],s:"!";HUDMSG_PLAIN,887,CR_GOLD,0.5,0.375,3.0);
		SetResultValue(1);
	}
}