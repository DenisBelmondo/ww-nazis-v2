#library "WWNAZIS"
#include "zcommon.acs"

Script "AutoReload"(void)
{
	SetResultValue(GetCVar("autoreload"));
}
Script "ShellLifetime"(void)
{
	SetResultValue(GetCVar("casinglifetime"));
}
Script "RecoilAmount"(void)
{
	SetResultValue(GetCVar("recoilamount"));
}
Script "MonsterType"(void)
{
	SetResultValue(GetCVar("monstertype"));
}
str taunts[18]=
{
	"allcaps/taunt","allcaps/reload","allcaps/grenadecall",
	"caleb/taunt","caleb/reload","caleb/grenadecall",
	"kingpin/taunt","kingpin/reload","kingpin/grenadecall",
	"soulman/taunt","soulman/reload","soulman/grenadecall",
	"spy/taunt","spy/reload","spy/grenadecall",
	"custom/taunt","custom/reload","custom/grenadecall"
};
str pain[24]=
{
	"allcapslowpain","allcapsmedpain","allcapshipain","allcapscritpain",
	"caleblowpain","calebmedpain","calebhipain","calebcritpain",
	"kingpinlowpain","kingpinmedpain","kingpinhipain","kingpincritpain",
	"soulmanlowpain","soulmanmedpain","soulmanhipain","soulmancritpain",
	"spylowpain","spymedpain","spyhipain","spycritpain",
	"weasellowpain","weaselmedpain","weaselhipain","weaselcritpain"
};
str scream[12]=
{
	"allcapsdeath","allcapscrazydeath",
	"calebdeath","calebcrazydeath",
	"kingpindeath","kingpincrazydeath",
	"soulmandeath","soulmancrazydeath",
	"spydeath","spycrazydeath",
	"weasledeath","weaselcrazydeath"
};
SCRIPT "WWNaziTaunt" (int tauntType)
{
	int pack=GetCVar("tauntpack");
	if(!pack)
		terminate;
	delay(20); // Appropriate wait time until player is done with the taunt-worthy action.
	//Print(i:tauntChance);
	if(tauntType==1&&random(1,5)==5)		// argument 1: taunt from firing
		PlaySound(0,taunts[0+((pack*3)-3)],CHAN_VOICE,1.0);
	else if(tauntType==2&&random(1,5)>=2)	// 2: taunt from reloading
		PlaySound(0,taunts[1+((pack*3)-3)],CHAN_VOICE,1.0);
	else if(tauntType==3&&random(1,5)>=2)	// 3: throwing a grenade
		PlaySound(0,taunts[2+((pack*3)-3)],CHAN_VOICE,1.0);
	Delay(5*35);
	//Log(s:"Taunt ready!");
}
script "PlayerPain" (void)
{
	int pack=GetCVar("tauntpack"),health=GetActorProperty(0,APROP_Health),level;
	if(!pack)
		pack=6;
	if(health<=25)
		level=3;
	else if(health<=50)
		level=2;
	else if(health<=75)
		level=1;
	PlaySound(0,pain[level+((pack*4)-4)],CHAN_VOICE,1.0);
}
script "PlayerScream" (void)
{
	int pack=GetCVar("tauntpack"),health=GetActorProperty(0,APROP_Health),level;
	if(!pack)
		pack=6;
	if(health<=(-50))
		level=1;
	PlaySound(0,scream[level+((pack*2)-2)],CHAN_VOICE,1.0);
}
script "WWNaziSkillCheck" (void)
{
	SetResultValue(GameSkill());
}
str weapons[22]=
{
	"Shovel","KnuckleDusters","Firebrand","Luger9mm","Walther9mm","WeaselsRevolver",
	"M1911","TrenchShotgun","Browning5","MP40","PPSH41",
	"Thompson","Kar98k","G43","Enfield","Garand",
	"STG44","FG42","Pyrolight","Nebelwerfer","Panzerschreck",
	"UMG43"
};
str weaponsfancy[22]=
{
	"Shovel","Knuckle Dusters","Firebrand","Luger P08","Walther P38",".357 Revolver",
	"Colt M1911","M1897 Trenchgun","Browning Auto-5","MP 40","PPSh-41",
	"Thompson M1A1","Karabiner 98k","Gewehr 43","Lee-Enfield SMLE","M1 Garand",
	"Sturmgewehr 44","FG 42","PyroLight","Klein Nebelwerfer","Panzerschreck",
	"UMG 43"
};
script "WWNaziStartingInventoryCheck" ENTER
{
	for(int i=3;i<=20;i++)
	{
		if(i!=19&&CheckInventory(weapons[i]))
			GiveInventory(StrParam(s:weapons[i],s:"StartCheck"),1);
	}
}
script "WWNaziCheatCheck" ENTER
{
	if(!CheckActorClass(0,"WWNazisPlayer"))
		terminate;
	if(CheckInventory("ICheated"))
	{
		TakeInventory("ICheated",1);
		GiveInventory("IJustTraded",1);
		for(int i=3;i<=21;i++)
			DropItem(0,StrParam(s:weapons[i],s:"Pickup"));
		hudmessage(s:"Weapons! Take your pick!!";HUDMSG_PLAIN,887,CR_GOLD,0.5,0.3,2.0);
	}
	Delay(1);
	restart;
}
script "WWNaziWeaponCollect"(int weapon)
{
	if(!CheckActorClass(0,"WWNazisPlayer"))
	{
		SetResultValue(0);
		terminate;
	}
	int load,i,currentweapon,min,max;
	if(weapon<=2)//Melee
	{
		min=0;
		max=2;
	}
	else if(weapon>=3&&weapon<=6)//Pistol
	{
		min=3;
		max=6;
	}
	else if(weapon>=7&&weapon<=11)//Shotgun/SMG
	{
		min=7;
		max=11;
	}
	else if(weapon>=12&&weapon<=17)//Rifle/Machinegun
	{
		min=12;
		max=17;
	}
	else if(weapon>=18)//Heavy
	{
		min=18;
		max=21;
	}
	for(i=min;i<=max;i++)
	{
		if(CheckInventory(weapons[i]))
			currentweapon=i;
	}
	if(currentweapon)	//You have a weapon for this slot
	{
		if(CheckInventory("IJustTraded")||currentweapon==weapon)
			SetResultValue(0);
		else
		{
			hudmessage(s:"Tap <",k:"+zoom",s:"> to trade the ",s:weaponsfancy[currentweapon],s:" for the ",s:weaponsfancy[weapon],s:".";HUDMSG_PLAIN,887,CR_GOLD,0.5,0.3,2.0);
			int buttons = GetPlayerInput(-1,INPUT_BUTTONS);
			if(buttons & BT_ZOOM)
			{
				GiveInventory("IJustTraded",1);
				giveweapon(weapon);
				ACS_NamedExecuteAlways("WWNaziWeaponDrop",0,currentweapon,weapon);
				hudmessage(s:"Traded the ",s:weaponsfancy[currentweapon],s:" for the ",s:weaponsfancy[weapon],s:"!";HUDMSG_PLAIN,887,CR_GOLD,0.5,0.3,2.0);
				SetResultValue(1);
			}
			else
				SetResultValue(0);
		}
	}
	else
	{
		giveweapon(weapon);
		hudmessage(s:"Picked up the ",s:weaponsfancy[weapon],s:"!";HUDMSG_PLAIN,887,CR_GOLD,0.5,0.3,2.0);
		SetResultValue(1);
	}
}
script "WWNaziWeaponDrop"(int currentweapon,int weapon)
{
	int changing=SetWeapon(weapons[weapon]);
	while(changing&&!StrCmp(GetWeapon(),weapons[currentweapon]))
		delay(1);
	TakeInventory(weapons[currentweapon],1);
	DropItem(0,StrParam(s:weapons[currentweapon],s:"Pickup"));
}

function void giveweapon(int weapon)
{
	GiveInventory(weapons[weapon],1);
	if(!GetCVAR("neverswitchonpickup"))
		SetWeapon(weapons[weapon]);
	if(weapon>=3&&weapon!=19&&weapon!=21)
	{
		if(!CheckInventory(StrParam(s:weapons[weapon],s:"StartCheck")))
		{
			GiveInventory(StrParam(s:weapons[weapon],s:"StartCheck"),1);
			GiveInventory(StrParam(s:weapons[weapon],s:"Loaded"),999);
		}
	}
}