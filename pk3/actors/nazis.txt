ACTOR Nazi
{
	Scale 1.1
	Height 56
	Mass 100
	PainChance 200
	Speed 2
	Monster
	+FLOORCLIP
	+DONTHARMSPECIES
	+BOSSDEATH
	+DONTGIB
	States
	{
	SkillCheck:
		"####" "#" 0 A_JumpIf(CallACS("WWNaziSkillCheck")>=2,3)
		"####" "#" 0 A_ChangeFlag("DOHARMSPECIES",1)
		"####" "#" 0 A_ChangeFlag("DONTHARMSPECIES",0)
		"####" "#" 0 A_JumpIf(CallACS("WWNaziSkillCheck")<=2,2)
		"####" "#" 0 A_ChangeFlag("GHOST",1)
		"####" "#" 0 A_Jump(256,"Look")
		Stop
	Look:
		"####" "#" 10 A_Look
		Loop
	See:
		"####" A 1 A_Chase
		"####" AAA 1 A_Chase("","")
		"####" A 1 A_Chase
		"####" AAA 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBB 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BBB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCC 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CCC 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DDD 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DDD 1 A_Chase("","")
		Loop
	Idle:
		"####" AAAAAAAA 1 A_Wander
		"####" A 0 A_Look
		"####" BBBBBBBB 1 A_Wander
		"####" A 0 A_Look
		"####" CCCCCCCC 1 A_Wander
		"####" A 0 A_Look
		"####" DDDDDDDD 1 A_Wander
		"####" A 0 A_Look
		Loop
	Pain:
		"####" H 3
		"####" H 3 A_Pain
		"####" H 0 A_Jump(256,"See")
		Stop
	}
}

ACTOR GuardSpawner : RandomSpawner
{
	DropItem Guard
	DropItem SSGuard
	DropItem WGuard
}

ACTOR Guard : Nazi
{
	Health 20
	SeeSound "Nazi1/Sighted"
	PainSound "Nazi1/Pain"
	DeathSound "Nazi1/Death"
	Obituary "%o was shot by a pistol soldier."
	DropItem Walther9mmSpawner
	States
	{
	Spawn:
		GARD N 0
		Goto SkillCheck
	Missile:
		"####" EF 5 A_FaceTarget
		"####" G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		"####" G 8 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-8,8))
		"####" F 8
		"####" F 0 A_Jump(256,"See")
		Stop
	Death:
		"####" I 5
		"####" J 5 A_Scream
		"####" K 5 A_NoBlocking
		"####" L 5
		"####" M -1
		Stop
	Death.Fire:
		BURN A 5 BRIGHT A_SetScale(1.0)
		BURN BC 5 BRIGHT
		BURN D 5 BRIGHT A_PlaySound("death/burning")
		BURN EFGHIJKLMN 5 BRIGHT
		BURN O 5 BRIGHT A_NoBlocking
		BURN PQRSTU 5 BRIGHT
		BURN V -1
		Stop
	Raise:
		"####" MLKJI 5
		"####" I 0 A_Jump(256,"See")
		Stop
	}
}

ACTOR SSGuard : Guard
{
	Speed 2.66666667
	Obituary "%o was killed by an SS guard."
	Health 40
	States
	{
	Spawn:
		SSPG N 0
		Goto SkillCheck
	See:
		"####" A 1 A_Chase
		"####" AA 1 A_Chase("","")
		"####" A 1 A_Chase
		"####" AA 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BB 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" BB 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CC 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" CC 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DD 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" DD 1 A_Chase("","")
		Loop
	Idle:
		"####" AAAAAA 1 A_Wander
		"####" A 0 A_Look
		"####" BBBBBB 1 A_Wander
		"####" A 0 A_Look
		"####" CCCCCC 1 A_Wander
		"####" A 0 A_Look
		"####" DDDDDD 1 A_Wander
		"####" A 0 A_Look
		Loop
	}
}

ACTOR WGuard : Guard
{
	Speed 2.5
	Obituary "%o was shot by a Wehrmacht soldier."
	Health 30
	PainChance 185
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn:
		GRD2 N 0
		Goto SkillCheck
	Missile:
		GRD2 EF 2 A_FaceTarget
		GRD2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		GRD2 G 4 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-6,6))
		GRD2 F 4 A_FaceTarget
		GRD2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		GRD2 G 8 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-10,10))
		GRD2 F 8
		Goto See
	}
}

ACTOR MP40GuardSpawner : RandomSpawner
{
	DropItem MP40Guard
	DropItem SSMP40Guard
	DropItem WMP40Guard
}

ACTOR MP40Guard : Guard
{
	Obituary "%o was plugged by a soldier with an MP40."
	SeeSound "Nazi2/Sighted"
	PainSound "Nazi2/Pain"
	DeathSound "Nazi2/Death"
	DropItem MP40Spawner
	DropItem GrenadePickup,16
	States
	{
	Spawn:
		MGRD N 0
		Goto SkillCheck
	Missile:
		MGRD EF 5 A_FaceTarget
		MGRD G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		MGRD G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		MGRD G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		MGRD G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		MGRD G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		MGRD G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGRD F 2
		Goto See
	}
}

ACTOR SSMP40Guard : SSGuard
{
	SeeSound "Nazi2/Sighted"
	PainSound "Nazi2/Pain"
	DeathSound "Nazi2/Death"
	DropItem MP40Spawner
	DropItem GrenadePickup,16
	Obituary "%o was plugged by an SS with an MP40."
	States
	{
	Spawn:
		SSMG N 0
		Goto SkillCheck
	Missile:
		SSMG EF 5 A_FaceTarget
		SSMG G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		SSMG G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		SSMG F 2 A_MonsterRefire(30,"See")
		SSMG F 0 A_FaceTarget
		Goto Missile+2
	}
}

ACTOR WMP40Guard : MP40Guard
{
	Obituary "%o was plugged by a trigger-happy Wehrmacht."
	Health 30
	Speed 2.5
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn:
		MGR2 N 0
		Goto SkillCheck
	Missile:
		MGR2 EF 2 A_FaceTarget
		MGR2 G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		MGR2 G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGR2 F 1 A_FaceTarget
		MGR2 G 0 A_PlaySound("nazi/mp40",CHAN_WEAPON)
		MGR2 G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-12,12))
		MGR2 F 2 A_MonsterRefire(30,"See")
		MGR2 F 0 A_FaceTarget
		Goto Missile+2
	}
}

ACTOR OfficerSpawner : RandomSpawner
{
	DropItem Officer
	DropItem WOfficer
	DropItem SSOfficer
}

ACTOR Officer : Guard
{
	Speed 4
	Obituary "%o was executed by an officer."
	SeeSound "Nazi4/Sighted"
	PainSound "Nazi4/Pain"
	DeathSound "Nazi4/Death"
	DropItem Luger9mmSpawner
	States
	{
	Spawn:
		OFFI N 0
		Goto SkillCheck
	See:
		"####" A 1 A_Chase
		"####" A 1 A_Chase("","")
		"####" A 1 A_Chase
		"####" A 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","")
		"####" B 1 A_Chase
		"####" B 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" C 1 A_Chase("","")
		"####" C 1 A_Chase
		"####" C 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" D 1 A_Chase("","")
		"####" D 1 A_Chase
		"####" D 1 A_Chase("","")
		Loop
	Missile:
		"####" EF 2 A_FaceTarget
		"####" G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		"####" G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-4,4))
		"####" F 4
		"####" F 0 A_Jump(256,"See")
		Stop
	Idle:
		"####" AAAA 1 A_Wander
		"####" A 0 A_Look
		"####" BBBB 1 A_Wander
		"####" A 0 A_Look
		"####" CCCC 1 A_Wander
		"####" A 0 A_Look
		"####" DDDD 1 A_Wander
		"####" A 0 A_Look
		Loop
	}
}

ACTOR SSOfficer : Officer
{
	Speed 5.33333333
	Obituary "%o was viciously executed by an SS officer."
	Health 40
	States
	{
	Spawn:
		SSOF N 0
		Goto SkillCheck
	}
}

ACTOR WOfficer : Officer
{
	Obituary "%o was quickly executed by an officer."
	Health 30
	Speed 5
	PainChance 180
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn:
		OFR2 N 0
		Goto SkillCheck
	Missile:
		OFR2 EF 2 A_FaceTarget
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 4 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-4,4))
		OFR2 F 3 A_FaceTarget
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-6,6))
		OFR2 F 3 A_FaceTarget
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-8,8))
		OFR2 F 3 A_FaceTarget
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-8,8))
		OFR2 F 4
		Goto See
	}
}

ACTOR RiflemanSpawner : RandomSpawner
{
	DropItem RifleGuard
	DropItem WRifleGuard
	DropItem WaffenSS
}

ACTOR RifleGuard : MP40Guard
{
	Obituary "%o was plinked by a rifle soldier."
	SeeSound "Nazi3/Sighted"
	DropItem Kar98kSpawner
	States
	{
	Spawn:
		RGRD N 0
		Goto SkillCheck
	Missile:
		RGRD EF 10 A_FaceTarget
		RGRD G 0 A_PlaySound("garand/fire",CHAN_WEAPON)
		RGRD G 8 BRIGHT A_CustomMissile("EnemyRifleTracer",32,0,random(-1,1))
		RGRD F 8
		Goto See
	}
}

ACTOR WRifleGuard : RifleGuard
{
	Obituary "%o was snap-shot by a rifle soldier."
	Health 30
	Speed 2.5
	+MISSILEMORE
	+MISSILEEVENMORE
	DropItem G43Spawner
	States
	{
	Spawn:
		RGR2 N 0
		Goto SkillCheck
	Missile:
		RGR2 EF 10 A_FaceTarget
		RGR2 G 0 A_PlaySound("garand/fire",CHAN_WEAPON)
		RGR2 G 8 BRIGHT A_CustomMissile("EnemyRifleTracer",32,0,random(-1,1))
		RGR2 F 4 A_FaceTarget
		RGR2 G 0 A_PlaySound("garand/fire",CHAN_WEAPON)
		RGR2 G 8 BRIGHT A_CustomMissile("EnemyRifleTracer",32,0,random(-5,5))
		RGR2 F 8
		Goto See
	}
}

ACTOR WaffenSS : SSMP40Guard
{
	SeeSound "Nazi3/Sighted"
	Obituary "%o was shot to pieces by a Waffen-SS."
	Health 80
	DropItem STG44Spawner
	DropItem GrenadePickup,32
	States
	{
	Spawn:
		WAFF N 0
		Goto SkillCheck
	Missile:
		"####" EF 5 A_FaceTarget
		"####" G 0 A_PlaySound("nazi/stg44",CHAN_WEAPON)
		"####" G 2 BRIGHT A_CustomMissile("EnemyStGTracer",32,0,random(-4,4))
		"####" F 4 A_MonsterRefire(30,"See")
		"####" F 0 A_FaceTarget
		Goto Missile+2
	}
}

ACTOR ParatrooperSpawner : RandomSpawner
{
	DropItem Paratrooper
	DropItem Mutant
}

ACTOR Paratrooper : WaffenSS
{
	Obituary "%o was slaughtered by a Fallschirmjaeger."
	Health 90
	DropItem FG42Spawner,128
	States
	{
	Spawn:
		PARA N 0
		Goto SkillCheck
	}
}

ACTOR Mutant : Nazi
{
	Obituary "%o did the dance of doom with a Mutant."
	Health 90
	Speed 3
	DeathSound "mutant/Death"
	BloodColor "Green"
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn:
		MTNT A 0
		Goto SkillCheck
	See:
		MTNT A 0 A_SetSpeed(3)
		MTNT B 1 A_Chase
		MTNT B 1 A_Chase("","")
		MTNT B 1 A_Chase
		MTNT B 1 A_Chase("","")
		MTNT C 1 A_Chase
		MTNT C 1 A_Chase("","")
		MTNT C 1 A_Chase
		MTNT C 1 A_Chase("","")
		MTNT D 1 A_Chase
		MTNT D 1 A_Chase("","")
		MTNT D 1 A_Chase
		MTNT D 1 A_Chase("","")
		MTNT E 1 A_Chase
		MTNT E 1 A_Chase("","")
		MTNT E 1 A_Chase
		MTNT E 1 A_Chase("","")
		Loop
	Missile:
		MTNT F 6 A_FaceTarget
		MTNT G 0 A_PlaySound("walther/fire",CHAN_WEAPON)
		MTNT G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-1,1))
		MTNT F 6 A_FaceTarget
		MTNT H 0 A_PlaySound("walther/fire",CHAN_WEAPON)
		MTNT H 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-1,1))
		MTNT F 6 A_MonsterRefire(30,"See")
		MTNT F 0 A_FaceTarget
		Goto Missile+1
	Pain:
		MTNT I 6
		Goto See
	Death:
		MTNT J 3
		MTNT K 3 A_Scream
		MTNT L 3 A_NoBlocking
		MTNT MN 3
		MTNT O -1
		Stop
	Idle:
		MTNT A 0 A_SetSpeed(1.5)
		MTNT BBBBBBBB 1 A_Wander
		MTNT A 0 A_Look
		MTNT CCCCCCCC 1 A_Wander
		MTNT A 0 A_Look
		MTNT DDDDDDDD 1 A_Wander
		MTNT A 0 A_Look
		MTNT EEEEEEEE 1 A_Wander
		MTNT A 0 A_Look
		Loop
	Raise:
		MTNT ONMLKJ 4
		Goto See
	}
}

ACTOR DogSpawner : RandomSpawner
{
	DropItem Rottweiler
	DropItem GermanShepherd
	DropItem Doberman
}

ACTOR Rottweiler : Nazi
{
	Scale 1
	HitObituary "%o was bitten by a guard dog."
	Health 25
	PainChance 180
	Speed 4
	Height 48
	Mass 70
	SeeSound "dog/sight"
	PainSound "dog/pain"
	DeathSound "dog/death"
	ActiveSound "dog/active"
	-CANUSEWALLS
	States
	{
	Spawn:
		DOGY A 0
		Goto SkillCheck
	Look:
	Idle:
		"####" A 0 A_SetSpeed(4)
		"####" AAAAAAAA 1 A_Wander
		"####" A 0 A_Look
		"####" BBBBBBBB 1 A_Wander
		"####" A 0 A_Look
		"####" CCCCCCCC 1 A_Wander
		"####" A 0 A_Look
		"####" DDDDDDDD 1 A_Wander
		"####" A 0 A_Look
		Loop
	See:
		"####" A 0 A_SetSpeed(8)
		"####" A 1 A_Chase
		"####" AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_Chase
		"####" BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_Chase
		"####" CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_Chase
		"####" DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Melee:
		"####" EF 2 Fast A_FaceTarget
		"####" G 8 Fast A_CustomMeleeAttack(random(1,8)*5,"dog/attack","dog/attack")
		"####" FE 4 Fast
		Goto See
	Pain:
		"####" M 2
		"####" M 2 A_Pain
		Goto See
	Death:
		"####" H 8
		"####" I 8 A_Scream
		"####" J 6
		"####" K -1 A_NoBlocking
		Stop
	Raise:
		"####" KJIH 6
		Goto See
	}
}

ACTOR GermanShepherd : Rottweiler
{
	States
	{
	Spawn:
		DOG2 A 0
		Goto SkillCheck
	}
}

ACTOR Doberman : Rottweiler
{
	States
	{
	Spawn:
		DOG3 A 0
		Goto SkillCheck
	}
}

ACTOR DoctorSpawner : RandomSpawner
{
	DropItem RocketMan
	DropItem RocketMan
	DropItem MadDoctor
}

ACTOR RocketMan : Nazi
{
	Health 300
	PainChance 110
	Mass 200
	Obituary "%o was splattered by Otto Giftmacher."
	SeeSound "Boss2/Sighted"
	PainSound "boss/pain"
	DeathSound "Boss2/Death"
	States
	{
	Spawn:
		OTTO A 0
		Goto SkillCheck
	Missile:
		OTTO E 10 A_FaceTarget
		OTTO F 8 BRIGHT A_CustomMissile("MiniRocket",32,12)
		OTTO E 8
		Goto See
	Pain:
		OTTO K 3
		OTTO K 3 A_Pain
		Goto See
	Death:
		"####" G 5
		"####" H 5 A_Scream
		"####" I 5
		"####" J -1 A_NoBlocking
		Stop
	Raise:
		"####" JIHG 5
		Goto See
	}
}

ACTOR MiniRocket
{
	Radius 11
	Height 8
	Speed 15
	Damage 5
	Projectile
	+RANDOMIZE
	+THRUGHOST
	SeeSound "nazi/missilef"
	DeathSound "world/barrelboom"
	States
	{
	Spawn:
		NMIS A 1 BRIGHT A_SpawnItemEx("RocketFlame",random(-1,1),0,random(-1,1))
		Loop
	Death:
		FRME A 1 BRIGHT A_Explode(20,40)
		FRME BCDEFGHIHJKLMNOPQRS 1 BRIGHT
		Stop
	}
}

ACTOR MadDoctor : RocketMan
{
	+LOOKALLAROUND
	Obituary "%o was used as an unwitting test subject by Dr. Schabbs."
	States
	{
	Spawn:
		SCHB A 0
		Goto SkillCheck
	Missile:
		SCHB E 10 A_FaceTarget
		SCHB F 8 A_CustomMissile("FlyingNeedle",32,12)
		SCHB A 8
		Goto See
	Pain:
		SCHB G 3
		SCHB A 3 A_Pain
		Goto See
	}
}

ACTOR FlyingNeedle : MiniRocket
{
	Speed 20
	Damage (random(1,4)*8)
	SeeSound ""
	DeathSound ""
	States
	{
	Spawn:
		NEDL ABCD 3
		Loop
	Death:
		TNT1 A 1
		Stop
	}
}

ACTOR SuperSoldierSpawner : RandomSpawner
{
	DropItem SuperSoldier
	DropItem SuperSoldier
	DropItem SuperSoldier
	DropItem SuperSoldier
	DropItem SuperSoldier
	DropItem SuperSoldier
	DropItem SuperSoldier
	DropItem SuperSoldierGirl
}

ACTOR SuperSoldier : Nazi
{
	Obituary "%o was terminated by an ubersoldat."
	Health 500
	Radius 24
	Height 64
	Mass 500
	PainChance 150
	SeeSound "Boss1/Sighted"
	PainSound "boss/pain"
	DeathSound "Boss1/Death"
	DropItem UMG43Spawner,8
	+NOBLOOD
	States
	{
	Spawn:
		HANS A 0
		Goto SkillCheck
	Missile:
		HANS E 6 A_FaceTarget
		HANS E 4 A_PlaySound("chaingun/start",CHAN_AUTO)
		HANS E 0 A_PlaySound("chaingun/loop",CHAN_AUTO)
		HANS E 10 A_FaceTarget
		HANS F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		HANS G 2 BRIGHT A_FaceTarget
		HANS F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		HANS G 2 BRIGHT A_FaceTarget
		HANS F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		HANS G 2 BRIGHT A_MonsterRefire(30,"See")
		HANS G 0 A_FaceTarget
 		Goto Missile+4
	Death:
		"####" I 5
		"####" J 5 A_Scream
		"####" K 5
		"####" L -1 A_NoBlocking
		Stop
	Raise:
		"####" LKJI 5
		Goto See
	}
}

ACTOR SuperSoldierGirl : SuperSoldier
{
	Obituary "%o was terminated by a female ubersoldat."
	SeeSound "BossGirl/Sighted"
	PainSound "bossGirl/pain"
	DeathSound "BossGirl/Death"
	DropItem ""
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn:
		WBO4 A 0
		Goto SkillCheck
	Missile:
		WBO4 E 20 A_FaceTarget
		WBO4 F 0 A_PlaySound("browning/fire",CHAN_WEAPON)
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",32,16,frandom(-6,6))
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",32,-16,frandom(-6,6))
		WBO4 FG 2 BRIGHT A_FaceTarget
		WBO4 F 0 A_PlaySound("browning/fire",CHAN_WEAPON)
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",32,16,frandom(-6,6))
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",32,-16,frandom(-6,6))
		WBO4 FG 2 BRIGHT A_FaceTarget
		WBO4 F 0 A_PlaySound("browning/fire",CHAN_WEAPON)
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",32,16,frandom(-6,6))
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",32,-16,frandom(-6,6))
		WBO4 FG 2 BRIGHT
		Goto See
	}
}

ACTOR SuperSoldierElite : SuperSoldier
{
	Obituary "%o was pulverized by an elite ubersoldat."
	Health 850
	States
	{
	Spawn:
		TRAN A 0
		Goto SkillCheck
	Missile:
		TRAN E 6 A_FaceTarget
		TRAN E 4 A_PlaySound("chaingun/start",CHAN_AUTO)
		TRAN E 0 A_PlaySound("chaingun/loop",CHAN_AUTO)
		TRAN E 10 A_FaceTarget
		TRAN F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		TRAN G 2 A_FaceTarget
		TRAN F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		TRAN G 2 A_FaceTarget
		TRAN F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		TRAN G 2 A_FaceTarget
		TRAN F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		TRAN G 2 BRIGHT A_MonsterRefire(30,"See")
		TRAN G 0 A_FaceTarget
		Goto Missile+4
	}
}

ACTOR General : RocketMan
{
	Obituary "%o was eliminated by a Nazi general."
	Health 800
	Mass 1000
	SeeSound "Boss3/Sighted"
	DeathSound "Boss3/Death"
	DropItem NebelwerferSpawner,64
	DropItem UMG43Spawner,8
	+MISSILEEVENMORE
	States
	{
	Spawn:
		FETT A 0
		Goto SkillCheck
	Missile:
		FETT E 10 A_FaceTarget
		FETT E 0 A_Jump(128,"Chaingun")
		FETT F 8 BRIGHT A_CustomMissile("MiniRocket",32,15)
		FETT E 8 A_FaceTarget
		FETT F 8 BRIGHT A_CustomMissile("MiniRocket",32,15)
		FETT E 8 A_FaceTarget
		FETT F 8 BRIGHT A_CustomMissile("MiniRocket",32,15)
		FETT E 8
		Goto See
	Chaingun:
		FETT H 0 A_FaceTarget
		FETT H 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		FETT H 2 BRIGHT A_CustomMissile("EnemyChaingunTracer",40,-12,random(-11,11))
		FETT G 2 BRIGHT A_MonsterRefire(30,"See")
		Loop
	Pain:
		FETT I 3
		FETT I 3 A_Pain
		Goto See
	Death:
		FETT J 5
		FETT K 5 A_Scream
		FETT L 5
		FETT M -1 A_NoBlocking
		Stop
	Raise:
		FETT MLKJ 5
		Goto See
	}
}

ACTOR FlamerMan : RocketMan
{
	Speed 2.66666667
	+LOOKALLAROUND
	Obituary "%o was roasted by a Nazi with a flamethrower."
	Health 500
	SeeSound "Boss1/Sighted"
	DeathSound "Boss1/Death"
	DropItem PyrolightSpawner
	States
	{
	Spawn:
		FLAM A 0
		Goto SkillCheck
	See:
		FLAM A 1 A_Chase
		FLAM AA 1 A_Chase("","")
		FLAM A 1 A_Chase
		FLAM AA 1 A_Chase("","")
		FLAM B 1 A_Chase
		FLAM BB 1 A_Chase("","")
		FLAM B 1 A_Chase
		FLAM BB 1 A_Chase("","")
		FLAM C 1 A_Chase
		FLAM CC 1 A_Chase("","")
		FLAM C 1 A_Chase
		FLAM CC 1 A_Chase("","")
		FLAM D 1 A_Chase
		FLAM DD 1 A_Chase("","")
		FLAM D 1 A_Chase
		FLAM DD 1 A_Chase("","")
		Loop
	Missile:
		FLAM E 10 A_FaceTarget
		FLAM E 0 A_JumpIfCloser(384,"ProperMissile")
		FLAM F 4 BRIGHT A_CustomMissile("EnemyFlamebolt",32,12)
		FLAM E 6
		Goto See
	ProperMissile:
		FLAM FFFFFFFF 1 BRIGHT A_CustomMissile("EnemyFlamerShot",32,12)
		FLAM E 6 A_MonsterRefire(10,"See")
		Goto Missile
	Pain:
		FLAM A 3
		FLAM A 3 A_Pain
		Goto See
	Death:
		FLAM G 5
		FLAM H 5 A_Scream
		FLAM I 5
		FLAM J -1 A_NoBlocking
		Stop
	Idle:
		FLAM AAAAAA 1 A_Wander
		FLAM A 0 A_Look
		FLAM BBBBBB 1 A_Wander
		FLAM A 0 A_Look
		FLAM CCCCCC 1 A_Wander
		FLAM A 0 A_Look
		FLAM DDDDDD 1 A_Wander
		FLAM A 0 A_Look
		Loop
	}
}

ACTOR EnemyFlamerShot : Flamebolt
{
	+THRUGHOST
	-RIPPER
}

ACTOR EnemyFlamebolt : Flameball
{
	+THRUGHOST
	Damage 8
	Scale .5
	States
	{
	Death:
		FBLX A 2 BRIGHT A_Explode(8,64)
		FBLX BCDEFGHIJK 2 BRIGHT
		Stop
	}
}

ACTOR HitlerGhost : Nazi
{
	Scale 1.2
	Obituary "%o thought %g saw Hitler."
	Health 250
	Mass 1000
	Speed 2.16666667
	Floatspeed .67
	PainChance 24
	DeathSound "hitlerghost/die"
	SeeSound "hitlerghost/see"
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	-FLOORCLIP
	States
	{
	Spawn:
		HGST A 0
		Goto SkillCheck
	See:
		HGST A 1 A_Chase("Missile","Missile",CHF_FASTCHASE)
		HGST AAAAA 1 A_Chase("","")
		HGST B 1 A_Chase
		HGST BBBBB 1 A_Chase("","")
		HGST C 1 A_Chase
		HGST CCCCC 1 A_Chase("","")
		HGST B 1 A_Chase
		HGST BBBBB 1 A_Chase("","")
		Loop
	Missile:
		HGST A 4 A_FaceTarget
		HGST D 4 BRIGHT A_CustomMissile("EnemyFlamebolt2",30)
		HGST A 4
		Goto See
	Pain:
		HGST A 4
		Goto See
	Death:
		HGST E 6 A_Scream
		HGST FGHI 6
		HGST J -1 A_NoBlocking
		Stop
	Idle:
		Stop
	}
}

ACTOR EnemyFlamebolt2 : EnemyFlamebolt
{
	Damage (random(1,4)*5)
	Scale .35
	States
	{
	Death:
		FBLX ABCDEFGHIJK 2 BRIGHT
		Stop
	}
}

ACTOR Geist : Nazi
{
	+LOOKALLAROUND
	Scale .8
	Obituary "%o had %p soul removed by a Geist."
	Health 100
	Radius 16
	Mass 1000
	Speed 5
	PainChance 255
	RenderStyle Translucent
	Alpha .5
	DeathSound "geist/die"
	PainSound "geist/pain"
	Floatspeed 1.33
	-FLOORCLIP
	+FLOAT
	+NOGRAVITY
	+DONTFALL
	+NOBLOOD
	States
	{
	Spawn:
		GEST A 0
		Goto SkillCheck
	Look:
		GEST ABCD 4 A_Look
		Loop
	See:
		GEST A 0 A_PlaySound("geist/active",CHAN_VOICE,1,1)
		GEST A 0 A_SetTranslucent(0.1)
		GEST AAABBBCCCDDD 1 A_Chase
		GEST A 0 A_SetTranslucent(0.2)
		GEST AAABBBCCCDDD 1 A_Chase
		GEST A 0 A_SetTranslucent(0.3)
		GEST AAABBBCCCDDD 1 A_Chase
		GEST A 0 A_SetTranslucent(0.4)
		GEST AAABBBCCCDDD 1 A_Chase
		GEST A 0 A_SetTranslucent(0.5)
		GEST AAABBBCCCDDD 1 A_Chase
		GEST A 0 A_SetTranslucent(0.4)
		GEST AAABBBCCCDDD 1 A_Chase
		GEST A 0 A_SetTranslucent(0.3)
		GEST AAABBBCCCDDD 1 A_Chase
		GEST A 0 A_SetTranslucent(0.2)
		GEST AAABBBCCCDDD 1 A_Chase
		Loop
	Melee:
		GEST A 1 A_SetTranslucent(0.1)
		GEST AA 1 A_FadeIn(0.2)
		GEST B 3 BRIGHT A_FaceTarget
		GEST CDABCD 3 BRIGHT A_CustomMeleeAttack(4)
		GEST AAAA 1 A_Fadeout(0.1)
		Goto See
	Pain:
		"####" E 1 A_Pain
		"####" E 1 A_SetTranslucent(0.4)
		"####" EEE 1 A_Fadeout(0.1)
		TNT1 A 0 A_Jump(256,"See")
		Stop
	Death:
		"####" E 5 A_SetTranslucent(0.5)
		"####" E 6 A_Scream
		"####" FG 6
		"####" H 6 A_NoBlocking
		Stop
	Idle:
		Stop
	}
}

ACTOR NaziGeist : Geist
{
	Scale 1.2
	Health 400
	Radius 31
	Speed 3.75
	PainChance 128
	States
	{
	Spawn:
		GES2 A 0
		Goto SkillCheck
	Look:
		GES2 ABCD 6 A_Look
		Loop
	See:
		GES2 A 0 A_PlaySound("geist/active",CHAN_VOICE,1,1)
		GES2 A 0 A_SetTranslucent(0.1)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		GES2 A 0 A_SetTranslucent(0.2)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		GES2 A 0 A_SetTranslucent(0.3)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		GES2 A 0 A_SetTranslucent(0.4)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		GES2 A 0 A_SetTranslucent(0.5)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		GES2 A 0 A_SetTranslucent(0.4)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		GES2 A 0 A_SetTranslucent(0.3)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		GES2 A 0 A_SetTranslucent(0.2)
		GES2 A 1 A_Chase
		GES2 AAAAA 1 A_Chase("","")
		GES2 B 1 A_Chase
		GES2 BBBBB 1 A_Chase("","")
		GES2 C 1 A_Chase
		GES2 CCCCC 1 A_Chase("","")
		GES2 D 1 A_Chase
		GES2 DDDDD 1 A_Chase("","")
		Loop
	Missile:
		GES2 A 1 BRIGHT A_SetTranslucent(0.1)
		GES2 AAABBB 1 BRIGHT A_FadeIn(0.1)
		GES2 B 1 BRIGHT A_SetTranslucent(1.0)
		GES2 CD 4 BRIGHT A_FaceTarget
		GES2 A 0 A_Jump(128,3)
		GES2 A 4 BRIGHT A_PainAttack("Geist",0,PAF_NOSKULLATTACK,0)
		GES2 A 0 A_Jump(256,3)
		GES2 A 0 A_PainAttack("Geist",-45,PAF_NOSKULLATTACK,0)
		GES2 A 4 BRIGHT A_PainAttack("Geist",45,PAF_NOSKULLATTACK,0)
		GES2 B 3 BRIGHT
		GES2 BCCCC 1 BRIGHT A_Fadeout(0.1)
		GES2 D 4 BRIGHT
		Goto See
	Melee:
		Stop
	}
}

ACTOR MiniShip : Nazi
{
	+LOOKALLAROUND
	Obituary "%o received death from above."
	Health 600
	Mass 2000
	PainSound "metal/pain"
	DeathSound "world/barrelboom"
	Floatspeed .67
	-FLOORCLIP
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	States
	{
	Spawn:
		SHIP A 0
		Goto SkillCheck
	See:
		"####" A 1 A_Chase
		"####" AAA 1 A_Chase("","")
		Loop
	Missile:
		SHIP A 10 A_FaceTarget
		SHIP B 8 BRIGHT A_CustomMissile("MiniRocket",20,4)
		SHIP A 8 A_FaceTarget
		SHIP C 8 BRIGHT A_CustomMissile("MiniRocket",20,-4)
		SHIP A 8
		Goto See
	Pain:
		"####" A 3
		"####" A 3 A_Pain
		Goto See
	Death:
		SHIP A -1
		Wait
	Crash:
		BARX A 0 A_SetFloorClip
		BARX A 0 A_Scream
		BARX A 2 BRIGHT A_Explode(40)
		BARX BCDEFGHIJKLM 2 BRIGHT
		Stop
	Idle:
		SHIP AAAAAAAA 1 A_Wander
		SHIP A 0 A_Look
		Loop
	}
}

ACTOR MGTurret : MiniShip
{
	Scale 1
	Speed 0
	Obituary "%o was shot up by a gun turret."
	Health 100
	Radius 24
	Height 24
	Mass 10000000000000000
	PainChance 100
	+MISSILEEVENMORE
	-FLOAT
	-NOGRAVITY
	States
	{
	Spawn:
		GUTR A 0
		Goto SkillCheck
	Missile:
		GTUR A 10 A_FaceTarget
		GTUR B 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		GTUR C 3 BRIGHT A_CustomMissile("EnemyChaingunTracer",20,0,random(-11,11))
		GTUR B 3 BRIGHT A_MonsterRefire(40,"See")
		GTUR B 0 A_FaceTarget
		Goto Missile+1
	Death:
		Goto Crash
	Idle:
		Goto Look
	}
}

ACTOR DeathKnight : Nazi
{
	+LOOKALLAROUND
	Scale 1.5
	Obituary "%o knelt before the Death Knight."
	Health 3000
	Radius 64
	Height 96
	Mass 1000
	Speed 3.5
	PainChance 20
	SeeSound "deathknight/sight"
	PainSound "boss/pain"
	DeathSound "deathknight/death"
	DropItem NebelwerferSpawner
	+MISSILEEVENMORE
	+BOSS
	+NORADIUSDMG
	States
	{
	Spawn:
		DKGT A 0
		Goto SkillCheck
	See:
		"####" A 1 A_Chase
		"####" AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 1 A_Chase
		"####" AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_Chase
		"####" BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" B 1 A_Chase
		"####" BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 0 A_PlaySound("hitler/walk")
		"####" C 1 A_Chase
		"####" CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" C 1 A_Chase
		"####" CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_Chase
		"####" DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" D 1 A_Chase
		"####" DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		"####" A 0 A_PlaySound("hitler/walk")
		Loop
	Missile:
		DKGT E 10 A_FaceTarget
		DKGT E 0 A_Jump(40,"AlphaStrike")
		DKGT E 0 A_Jump(128,"Chaingun")
		DKGT F 3 BRIGHT A_CustomMissile("NebRocketEnemy",80,24)
		DKGT E 3 A_MonsterRefire(40,"See")
		DKGT G 3 BRIGHT A_CustomMissile("NebRocketEnemy",80,-24)
		DKGT E 3 A_MonsterRefire(40,"See")
		DKGT F 3 BRIGHT A_CustomMissile("NebRocketEnemy",80,24)
		DKGT E 3 A_MonsterRefire(40,"See")
		DKGT G 3 BRIGHT A_CustomMissile("NebRocketEnemy",80,-24)
		DKGT E 3
		Goto See
	Chaingun:
		DKGT I 0 A_FaceTarget
		DKGT I 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		DKGT II 0 A_CustomMissile("EnemyChaingunTracer",40,24,random(-11,11))
		DKGT II 0 A_CustomMissile("EnemyChaingunTracer",40,-24,random(-11,11))
		DKGT I 2 BRIGHT
		DKGT H 2 BRIGHT A_MonsterRefire(30,"See")
		Loop
	AlphaStrike:
		DKGT K 0 A_FaceTarget
		DKGT K 0 A_CustomMissile("NebRocketEnemy",80,24)
		DKGT K 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		DKGT KK 0 A_CustomMissile("EnemyChaingunTracer",40,24,random(-11,11))
		DKGT KK 0 A_CustomMissile("EnemyChaingunTracer",40,-24,random(-11,11))
		DKGT K 2 BRIGHT
		DKGT L 0 A_CustomMissile("NebRocketEnemy",80,-24)
		DKGT L 2 BRIGHT A_MonsterRefire(30,"See")
		Loop
	Pain:
		DKGT E 3
		DKGT E 3 A_Pain
		Goto See
	Death:
		DKGT M 5 A_Scream
		DKGT NOP 5
		DKGT Q 5 A_NoBlocking
		DKGT R 5
		DKGT S -1
		Stop
	Idle:
		Stop
	}
}

ACTOR NebRocketEnemy : NebRocket
{
	Speed 15
	+THRUGHOST
	-FORCERADIUSDMG
	SeeSound "nebelwerfer/fire"
	States
	{
	Death:
		FRME A 1 BRIGHT A_Explode(40)
		FRME BCDEFGHIHJKLMNOPQRS 1 BRIGHT
		Stop
	}
}

ACTOR Hitler : DeathKnight
{
	-LOOKALLAROUND
	Scale 1.1
	Obituary "%o heiled Hitler."
	Health 2500
	Radius 32
	Height 64
	Speed 2.5
	SeeSound "hitler/sight"
	PainSound "hitler/pain"
	DeathSound "hitler/death"
	ActiveSound "hitler/active"
	DropItem UMG43Spawner,64
	States
	{
	Spawn:
		HITL A 0
		Goto SkillCheck
	Missile:
		HITL E 10 A_FaceTarget
		HITL F 0 A_FaceTarget
		HITL F 0 A_PlaySound("chaingun/fire",CHAN_WEAPON)
		HITL F 0 A_CustomMissile("EnemyChaingunTracer",32,24,random(-3,3))
		HITL F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-24,random(-3,3))
		HITL F 0 A_CustomMissile("EnemyChaingunTracer",48,24,random(-3,3))
		HITL F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",48,-24,random(-3,3))
		HITL G 2 BRIGHT A_MonsterRefire(30,"See")
		Goto Missile+1
	Pain:
		HITL Q 3
		HITL Q 3 A_Pain
		Goto See
	Death:
		HITL H 10 A_Scream
		HITL I 10 A_SpawnItemEx("HitlerSuitScrap",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		HITL JK 10
		HITL L 10 A_PlaySound("hitler/gib")
		HITL M 10 A_NoBlocking
		HITL NO 10
		HITL P -1
		Stop
	}
}

ACTOR HitlerSuitScrap
{
	Scale 1.1
	+FLOORCLIP
	States
	{
	Spawn:
		HIT2 AB 20
		HIT2 C -1
		Stop
	}
}

ACTOR ChairGuy : Nazi
{
	Obituary "%o was given the chair."
	Health 600
	Mass 1000
	PainChance 110
	SeeSound "krist/sight"
	PainSound "boss/pain"
	DeathSound "krist/death"
	DropItem NebelwerferSpawner,64
	DropItem GrenadePickup,128
	+MISSILEEVENMORE
	States
	{
	Spawn:
		KRIS A 0
		Goto SkillCheck
	See:
		KRIS A 1 A_Chase
		KRIS AAA 1 A_Chase("","")
		KRIS A 1 A_Chase
		KRIS AAA 1 A_Chase("","")
		KRIS B 1 A_Chase
		KRIS BBB 1 A_Chase("","")
		KRIS B 1 A_Chase
		KRIS BBB 1 A_Chase("","")
		Loop
	Missile:
		KRIS E 10 A_FaceTarget
		KRIS F 6 BRIGHT A_CustomMissile("MiniMiniRocket",48,15)
		KRIS E 6 A_FaceTarget
		KRIS G 6 BRIGHT A_CustomMissile("MiniMiniRocket",48,-15)
		KRIS E 6 A_FaceTarget
		KRIS F 6 BRIGHT A_CustomMissile("MiniMiniRocket",48,15)
		KRIS E 6 A_FaceTarget
		KRIS G 6 BRIGHT A_CustomMissile("MiniMiniRocket",48,-15)
		Goto See
	Death:
		KRIS H 8 A_Scream
		KRIS HHH 8 A_SetAngle(angle+45)
		KRIS HHHHHH 4 A_SetAngle(angle+45)
		KRIS HHHHHHHHHHHH 2 A_SetAngle(angle+45)
		KRIS HHHHHHHHHHHH 1 A_SetAngle(angle+45)
		BARX A 0 A_NoBlocking
		BARX A 0 A_PlaySound("world/barrelboom")
		BARX A 2 BRIGHT A_Explode(40)
		BARX BCDEFGHIJKLM 2 BRIGHT
		Stop
	Idle:
		KRIS AAAAAAAA 1 A_Wander
		KRIS A 0 A_Look
		KRIS BBBBBBBB 1 A_Wander
		KRIS A 0 A_Look
		Loop
	}
}

ACTOR MiniMiniRocket : MiniRocket
{
	Scale .8
	Damage (random(1,4)*4)
	DeathSound "nebelwerfer/xplode"
}

ACTOR DeadGuard
{
	Scale 1.1
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3)
		GARD M -1
		SSPG M -1
		GRD2 M -1
		Stop
	}
}

ACTOR DeadMP40Guard : DeadGuard
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3)
		MGRD M -1
		MGR2 M -1
		SSMG M -1
		Stop
	}
}

ACTOR DeadOfficer : DeadGuard
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3)
		OFFI M -1
		OFR2 M -1
		SSOF M -1
		Stop
	}
}

ACTOR DeadDog
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3)
		DOGY K -1
		DOG2 K -1
		DOG3 K -1
		Stop
	}
}

ACTOR DeadHitlerGhost
{
	Scale 1.2
	States
	{
	Spawn:
		HGST J -1
		Stop
	}
}