ACTOR BigNazi
{
	Scale 1.1
	Health 300
	PainChance 110
	Height 56
	Mass 200
	Speed 8
	DamageFactor "Enemy",0.0
	MONSTER
	+FLOORCLIP
	+LOOKALLAROUND
	+DOHARMSPECIES
	PainSound "boss/pain"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(CallACS("WWNaziSkillCheck")<=2,2)
		TNT1 A 0 A_ChangeFlag("GHOST",1)
		TNT1 A 0 A_Jump(256,"Spawn2")
	Idle:
		"####" AA 4 A_Wander
		"####" A 0 A_Look
		"####" BB 4 A_Wander
		"####" A 0 A_Look
		"####" CC 4 A_Wander
		"####" A 0 A_Look
		"####" DD 4 A_Wander
		"####" A 0 A_Look
		Loop
	}
}
ACTOR Nazi : BigNazi
{
	-LOOKALLAROUND
	Health 20
	Mass 100
	PainChance 200
	SeeSound "Nazi1/Sighted"
	PainSound "Nazi1/Pain"
	DeathSound "Nazi1/Death"
	States
	{
	Death:
		"####" I 5
		"####" J 5 A_Scream
		"####" K 5 A_NoBlocking
		"####" L 5
		"####" M -1
		Stop
	Death.Fire:
		BURN A 5 BRIGHT A_SetScale(1.0)
		BURN BC 5 BRIGHT
		BURN D 5 BRIGHT A_PlaySound("death/burning")
		BURN EFGHIJKLMN 5 BRIGHT
		BURN O 5 BRIGHT A_NoBlocking
		BURN PQRSTU 5 BRIGHT
		BURN V -1
		Stop
	}
}

ACTOR GuardSpawner : RandomSpawner replaces Zombieman
{
	DropItem Guard
	DropItem SSGuard
	DropItem WGuard
}

ACTOR Guard : Nazi
{
	Obituary "%o was shot by a pistol soldier."
	DropItem Walther9mmPickup
	DropItem MPMagazine
	States
	{
	Spawn2:
		GARD N 10 A_Look
		Loop
	See:
		GARD AABBCCDD 4 A_Chase
		Loop
	Missile:
		GARD EF 5 A_FaceTarget
		GARD G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		GARD G 8 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-8,8))
		GARD F 8
		Goto See
	Pain:
		GARD H 3
		GARD H 3 A_Pain
		Goto See
	Raise:
		GARD MLKJI 5
		Goto See
	}
}

ACTOR SSGuard : Guard
{
	Obituary "%o was killed by an SS guard."
	Health 40
	States
	{
	Spawn2:
		SSPG N 10 A_Look
		Loop
	See:
		SSPG AABBCCDD 3 A_Chase
		Loop
	Missile:
		SSPG EF 5 A_FaceTarget
		SSPG G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		SSPG G 8 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-8,8))
		SSPG F 8
		Goto See
	Pain:
		SSPG H 3
		SSPG H 3 A_Pain
		Goto See
	Raise:
		SSPG MLKJI 5
		Goto See
	}
}

ACTOR WGuard : Guard
{
	Obituary "%o was shot by a Wehrmacht soldier."
	Health 30
	Speed 10
	PainChance 185
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn2:
		GRD2 N 10 A_Look
		Loop
	See:
		GRD2 AABBCCDD 4 A_Chase
		Loop
	Missile:
		GRD2 EF 2 A_FaceTarget
		GRD2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		GRD2 G 4 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-6,6))
		GRD2 F 4 A_FaceTarget
		GRD2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		GRD2 G 8 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-10,10))
		GRD2 F 8
		Goto See
	Pain:
		GRD2 H 3
		GRD2 H 3 A_Pain
		Goto See
	Raise:
		GRD2 MLKJI 5
		Goto See
	}
}

ACTOR MP40GuardSpawner : RandomSpawner replaces ShotgunGuy
{
	DropItem MP40Guard
	DropItem SSMP40Guard
	DropItem WMP40Guard
}

ACTOR MP40GuardSpawner2 : MP40GuardSpawner replaces WolfensteinSS{}

ACTOR MP40Guard : Nazi
{
	Obituary "%o was plugged by a soldier with an MP40."
	SeeSound "Nazi2/Sighted"
	PainSound "Nazi2/Pain"
	DeathSound "Nazi2/Death"
	DropItem MP40Pickup
	DropItem MPMagazine
	DropItem MPMagazine
	DropItem GrenadePickup,16
	States
	{
	Spawn2:
		MGRD N 10 A_Look
		Loop
	See:
		MGRD AABBCCDD 4 A_Chase
		Loop
	Missile:
		MGRD EF 5 A_FaceTarget
		MGRD G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		MGRD G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		MGRD G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		MGRD G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		MGRD G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGRD F 2 A_FaceTarget
		MGRD G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		MGRD G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGRD F 2
		Goto See
	Pain:
		MGRD H 3
		MGRD H 3 A_Pain
		Goto See
	Raise:
		MGRD MLKJI 5
		Goto See
	}
}

ACTOR WMP40Guard : MP40Guard
{
	Obituary "%o was plugged by a trigger-happy Wehrmacht."
	Health 30
	Speed 10
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn2:
		MGR2 N 10 A_Look
		Loop
	See:
		MGR2 AABBCCDD 4 A_Chase
		Loop
	Missile:
		MGR2 EF 2 A_FaceTarget
		MGR2 G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		MGR2 G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		MGR2 F 1 A_FaceTarget
		MGR2 G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		MGR2 G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-12,12))
		MGR2 F 2 A_MonsterRefire(30,"See")
		MGR2 F 0 A_FaceTarget
		Goto Missile+2
	Pain:
		MGR2 H 3
		MGR2 H 3 A_Pain
		Goto See
	Raise:
		MGR2 MLKJI 5
		Goto See
	}
}

ACTOR SSMP40Guard : MP40Guard
{
	Obituary "%o was plugged by an SS with an MP40."
	Health 40
	States
	{
	Spawn2:
		SSMG N 10 A_Look
		Loop
	See:
		SSMG AABBCCDD 3 A_Chase
		Loop
	Missile:
		SSMG EF 5 A_FaceTarget
		SSMG G 0 A_PlaySound("weapons/assaultgun",CHAN_WEAPON)
		SSMG G 2 BRIGHT A_CustomMissile("EnemySMGTracer",32,0,random(-8,8))
		SSMG F 2 A_MonsterRefire(30,"See")
		SSMG F 0 A_FaceTarget
		Goto Missile+2
	Pain:
		SSMG H 3
		SSMG H 3 A_Pain
		Goto See
	Raise:
		SSMG MLKJI 5
		Goto See
	}
}

ACTOR RiflemanSpawner : RandomSpawner replaces ChaingunGuy
{
	DropItem RifleGuard
	DropItem WRifleGuard
	DropItem WaffenSS
}

ACTOR RifleGuard : Nazi
{
	Obituary "%o was plinked by a rifle soldier."
	SeeSound "Nazi3/Sighted"
	PainSound "Nazi2/Pain"
	DeathSound "Nazi2/Death"
	DropItem RifleSpawner
	States
	{
	Spawn2:
		RGRD N 10 A_Look
		Loop
	See:
		RGRD AABBCCDD 4 A_Chase
		Loop
	Missile:
		RGRD EF 10 A_FaceTarget
		RGRD G 0 A_PlaySound("garand/fire",CHAN_WEAPON)
		RGRD G 8 BRIGHT A_CustomMissile("EnemyRifleTracer",32,0,random(-1,1))
		RGRD F 8
		Goto See
	Pain:
		RGRD H 3
		RGRD H 3 A_Pain
		Goto See
	Raise:
		RGRD MLKJI 5
		Goto See
	}
}

ACTOR WRifleGuard : RifleGuard
{
	Obituary "%o was snap-shot by a rifle soldier."
	Health 30
	Speed 10
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn2:
		RGR2 N 10 A_Look
		Loop
	See:
		RGR2 AABBCCDD 4 A_Chase
		Loop
	Missile:
		RGR2 EF 10 A_FaceTarget
		RGR2 G 0 A_PlaySound("garand/fire",CHAN_WEAPON)
		RGR2 G 8 BRIGHT A_CustomMissile("EnemyRifleTracer",32,0,random(-1,1))
		RGR2 F 4 A_FaceTarget
		RGR2 G 0 A_PlaySound("garand/fire",CHAN_WEAPON)
		RGR2 G 8 BRIGHT A_CustomMissile("EnemyRifleTracer",32,0,random(-5,5))
		RGR2 F 8
		Goto See
	Pain:
		RGR2 H 3
		RGR2 H 3 A_Pain
		Goto See
	Raise:
		RGR2 MLKJI 5
		Goto See
	}
}

ACTOR WaffenSS : RifleGuard
{
	Obituary "%o was shot to pieces by a Waffen-SS."
	Health 80
	DropItem STG44Spawner
	DropItem GrenadePickup,32
	States
	{
	Spawn2:
		WAFF N 10 A_Look
		Loop
	See:
		WAFF AABBCCDD 3 A_Chase
		Loop
	Missile:
		WAFF EF 5 A_FaceTarget
		WAFF G 0 A_PlaySound("weapons/stg44",CHAN_WEAPON)
		WAFF G 2 BRIGHT A_CustomMissile("EnemyStGTracer",32,0,random(-4,4))
		WAFF F 4 A_MonsterRefire(30,"See")
		WAFF F 0 A_FaceTarget
		Goto Missile+2
	Pain:
		WAFF H 3
		WAFF H 3 A_Pain
		Goto See
	Raise:
		WAFF MLKJI 5
		Goto See
	}
}

ACTOR SpectreSpawner : RandomSpawner replaces Spectre
{
	DropItem Paratrooper
	DropItem Mutant
}

ACTOR Paratrooper : RifleGuard
{
	Obituary "%o was slaughtered by a Fallschirmjaeger."
	Health 90
	DropItem FG42Spawner,128
	DropItem MauserAmmoSmall,128
	States
	{
	Spawn2:
		PARA N 10 A_Look
		Loop
	See:
		PARA AABBCCDD 3 A_Chase
		Loop
	Missile:
		PARA EF 5 A_FaceTarget
		PARA G 0 A_PlaySound("weapons/stg44",CHAN_WEAPON)
		PARA G 2 BRIGHT A_CustomMissile("EnemyStGTracer",32,0,random(-4,4))
		PARA F 4 A_MonsterRefire(30,"See")
		PARA F 0 A_FaceTarget
		Goto Missile+2
	Pain:
		PARA H 3
		PARA H 3 A_Pain
		Goto See
	Raise:
		PARA MLKJI 5
		Goto See
	}
}

ACTOR Mutant : Nazi
{
	Obituary "%o did the dance of doom with a Mutant."
	Health 90
	Speed 6
	SeeSound ""
	DeathSound "mutant/Death"
	BloodColor "Green"
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn2:
		MTNT A 10 A_Look
		Loop
	See:
		MTNT BBCCDDEE 2 A_Chase
		Loop
	Missile:
		MTNT F 6 A_FaceTarget
		MTNT G 0 A_PlaySound("weapons/Walther",CHAN_WEAPON)
		MTNT G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-1,1))
		MTNT F 6 A_FaceTarget
		MTNT H 0 A_PlaySound("weapons/Walther",CHAN_WEAPON)
		MTNT H 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-1,1))
		MTNT F 6 A_MonsterRefire(30,"See")
		MTNT F 0 A_FaceTarget
		Goto Missile+1
	Pain:
		MTNT I 6
		Goto See
	Death:
	Death.Fire:
		MTNT I 3
		MTNT J 3 A_Scream
		MTNT K 3 A_NoBlocking
		MTNT LM 3
		MTNT N -1
		Stop
	Raise:
		MTNT MLKJI 5
		Goto See
	}
}

ACTOR OfficerSpawner : RandomSpawner replaces DoomImp
{
	DropItem Officer
	DropItem WOfficer
	DropItem SSOfficer
}

ACTOR Officer : Nazi
{
	Obituary "%o was executed by an officer."
	SeeSound "Nazi4/Sighted"
	PainSound "Nazi4/Pain"
	DeathSound "Nazi4/Death"
	DropItem Luger9mmPickup
	DropItem MPMagazine
	States
	{
	Spawn2:
		OFFI N 10 A_Look
		Loop
	See:
		OFFI AABBCCDD 2 A_Chase
		Loop
	Missile:
		OFFI EF 2 A_FaceTarget
		OFFI G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFFI G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-4,4))
		OFFI F 4
		Goto See
	Pain:
		OFFI H 3
		OFFI H 3 A_Pain
		Goto See
	Raise:
		OFFI MLKJI 5
		Goto See
	}
}

ACTOR WOfficer : Officer
{
	Obituary "%o was quickly executed by an officer."
	Health 30
	Speed 10
	PainChance 180
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn2:
		OFR2 N 10 A_Look
		Loop
	See:
		OFR2 AABBCCDD 2 A_Chase
		Loop
	Missile:
		OFR2 EF 2 A_FaceTarget
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 4 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-4,4))
		OFR2 F 3 A_FaceTarget
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-6,6))
		OFR2 F 3 A_FaceTarget
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-8,8))
		OFR2 F 3 A_FaceTarget
		OFR2 G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		OFR2 G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-8,8))
		OFR2 F 4
		Goto See
	Pain:
		OFR2 H 3
		OFR2 H 3 A_Pain
		Goto See
	Raise:
		OFR2 MLKJI 5
		Goto See
	}
}

ACTOR SSOfficer : Officer
{
	Obituary "%o was viciously executed by an SS officer."
	Health 40
	States
	{
	Spawn2:
		SSOF N 10 A_Look
		Loop
	See:
		SSOF AABBCCDD 2 A_Chase
		Loop
	Missile:
		SSOF EF 2 A_FaceTarget
		SSOF G 0 A_PlaySound("nazi/pistol",CHAN_WEAPON)
		SSOF G 5 BRIGHT A_CustomMissile("EnemyPistolTracer",32,0,random(-4,4))
		SSOF F 4
		Goto See
	Pain:
		SSOF H 3
		SSOF H 3 A_Pain
		Goto See
	Raise:
		SSOF MLKJI 5
		Goto See
	}
}

ACTOR DogSpawner : RandomSpawner replaces Demon
{
	DropItem Rottweiler
	DropItem GermanShepherd
}

ACTOR Rottweiler : Nazi
{
	Scale 1
	HitObituary "%o was bitten by a guard dog."
	Health 25
	PainChance 180
	Speed 16
	Height 48
	Mass 70
	SeeSound "dog/sight"
	PainSound "dog/pain"
	DeathSound "dog/death"
	ActiveSound "dog/active"
	-CANUSEWALLS
	States
	{
	Spawn2:
		DOGY AA 2 A_Wander
		DOGY A 0 A_Look
		DOGY BB 2 A_Wander
		DOGY B 0 A_Look
		DOGY CC 2 A_Wander
		DOGY C 0 A_Look
		DOGY DD 2 A_Wander
		DOGY D 0 A_Look
		Loop
	See:
		"####" AABBCCDD 2 A_Chase
		Loop
	Melee:
		"####" EF 2 Fast A_FaceTarget
		"####" G 8 Fast A_CustomMeleeAttack(random(1,8)*5,"dog/attack")
		"####" FE 4 Fast
		Goto See
	Pain:
		"####" H 2
		"####" H 2 A_Pain
		Goto See
	Death:
	Death.Fire:
		"####" H 8
		"####" I 8 A_Scream
		"####" J 6
		"####" K -1 A_NoBlocking
		Stop
	Raise:
		"####" KJIH 6
		Goto See
	}
}

ACTOR GermanShepherd : Rottweiler
{
	States
	{
	Spawn2:
		DOG2 AA 2 A_Wander
		DOG2 A 0 A_Look
		DOG2 BB 2 A_Wander
		DOG2 B 0 A_Look
		DOG2 CC 2 A_Wander
		DOG2 C 0 A_Look
		DOG2 DD 2 A_Wander
		DOG2 D 0 A_Look
		Loop
	Pain:
		"####" M 2
		"####" M 2 A_Pain
		Goto See
	}
}

ACTOR RevenantSpawner : RandomSpawner replaces Revenant
{
	DropItem RocketMan
	DropItem RocketMan
	DropItem MadDoctor
}

ACTOR RocketMan : BigNazi
{
	Obituary "%o was splattered by Otto Giftmacher."
	SeeSound "Boss2/Sighted"
	DeathSound "Boss2/Death"
	States
	{
	Spawn2:
		OTTO A 10 A_Look
		Loop
	See:
		"####" AABBCCDD 4 A_Chase
		Loop
	Missile:
		OTTO E 10 A_FaceTarget
		OTTO F 8 BRIGHT A_CustomMissile("MiniRocket",32,12)
		OTTO E 8
		Goto See
	Pain:
		OTTO A 3
		OTTO A 3 A_Pain
		Goto See
	Death:
		"####" G 5
		"####" H 5 A_Scream
		"####" I 5
		"####" J -1 A_NoBlocking
		Stop
	Raise:
		"####" JIHG 5
		Goto See
	}
}

ACTOR MiniRocket
{
	Radius 11
	Height 8
	Speed 15
	Damage 5
	Projectile
	+RANDOMIZE
	DamageType "Enemy"
	SeeSound "weapons/missilef"
	DeathSound "world/barrelboom"
	States
	{
	Spawn:
		MISL A 1 BRIGHT A_CustomMissile("RocketFlameSpawner",0,0,0,5)
		Loop
	Death:
		FRME A 1 BRIGHT A_Explode(20,40)
		FRME BCDEFGHIHJKLMNOPQRS 1 BRIGHT
		Stop
	}
}

ACTOR MadDoctor : RocketMan
{
	Obituary "%o was used as an unwitting test subject by Dr. Schabbs."
	States
	{
	Spawn2:
		SCHB A 10 A_Look
		Loop
	Missile:
		SCHB E 10 A_FaceTarget
		SCHB F 8 A_CustomMissile("FlyingNeedle",32,12)
		SCHB A 8
		Goto See
	Pain:
		SCHB G 3
		SCHB A 3 A_Pain
		Goto See
	}
}

ACTOR FlyingNeedle : MiniRocket
{
	Speed 20
	Damage (random(1,4)*8)
	Gravity 0.2
	SeeSound ""
	DeathSound ""
	States
	{
	Spawn:
		NEDL ABCD 3
		Loop
	Death:
		TNT1 A 1
		Stop
	}
}

ACTOR SuperSoldierSpawner : RandomSpawner replaces HellKnight
{
	DropItem SuperSoldier
	DropItem SuperSoldier
	DropItem SuperSoldier
	DropItem SuperSoldier
	DropItem SuperSoldier
	DropItem SuperSoldier
	DropItem SuperSoldier
	DropItem SuperSoldierGirl
}

ACTOR SuperSoldier : BigNazi
{
	Obituary "%o was terminated by an ubersoldat."
	Health 500
	Radius 24
	Height 64
	Mass 500
	PainChance 150
	SeeSound "Boss1/Sighted"
	DeathSound "Boss1/Death"
	DropItem UMG43Spawner,8
	+NOBLOOD
	States
	{
	Spawn2:
		HANS A 10 A_Look
		Loop
	See:
		"####" AABBCCDD 4 A_Chase
		Loop
	Missile:
		HANS E 20 A_FaceTarget
		HANS F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		HANS G 2 A_FaceTarget
		HANS F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		HANS G 2 A_FaceTarget
		HANS F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		HANS F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		HANS G 2 BRIGHT A_MonsterRefire(30,"See")
		HANS G 0 A_FaceTarget
 		Goto Missile+1
	Pain:
		"####" H 3
		"####" H 3 A_Pain
		Goto See
	Death:
		"####" I 5
		"####" J 5 A_Scream
		"####" K 5
		"####" L -1 A_NoBlocking
		Stop
	Raise:
		"####" LKJI 5
		Goto See
	}
}

ACTOR SuperSoldierGirl : SuperSoldier
{
	Obituary "%o was terminated by a female ubersoldat."
	SeeSound "BossGirl/Sighted"
	PainSound "bossGirl/pain"
	DeathSound "BossGirl/Death"
	DropItem ""
	+MISSILEMORE
	+MISSILEEVENMORE
	States
	{
	Spawn2:
		WBO4 A 10 A_Look
		Loop
	Missile:
		WBO4 E 20 A_FaceTarget
		WBO4 F 0 A_PlaySound("weapons/ba5fire",CHAN_WEAPON)
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",32,16,frandom(-6,6))
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",32,-16,frandom(-6,6))
		WBO4 FE 2 BRIGHT A_FaceTarget
		WBO4 F 0 A_PlaySound("weapons/ba5fire",CHAN_WEAPON)
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",32,16,frandom(-6,6))
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",32,-16,frandom(-6,6))
		WBO4 FE 2 BRIGHT A_FaceTarget
		WBO4 F 0 A_PlaySound("weapons/ba5fire",CHAN_WEAPON)
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",32,16,frandom(-6,6))
		WBO4 FFFF 0 A_CustomMissile("EnemyPistolTracer",32,-16,frandom(-6,6))
		WBO4 FE 2 BRIGHT
		Goto See
	}
}

ACTOR SuperSoldierElite : SuperSoldier replaces BaronOfHell
{
	Obituary "%o was pulverized by an elite ubersoldat."
	Health 850
	+BOSSDEATH
	States
	{
	Spawn2:
		TRAN A 10 A_Look
		Loop
	Missile:
		TRAN E 20 A_FaceTarget
		TRAN F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		TRAN G 2 A_FaceTarget
		TRAN F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		TRAN G 2 A_FaceTarget
		TRAN F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		TRAN G 2 A_FaceTarget
		TRAN F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,16,random(-11,11))
		TRAN F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-16,random(-11,11))
		TRAN G 2 BRIGHT A_MonsterRefire(30,"See")
		TRAN G 0 A_FaceTarget
		Goto Missile+1
	Death:
		TRAN I 5
		TRAN J 5 A_Scream
		TRAN K 5 A_BossDeath
		TRAN L -1 A_NoBlocking
		Stop
	}
}

ACTOR General : BigNazi replaces Archvile
{
	Obituary "%o was eliminated by a Nazi general."
	Health 800
	Mass 1000
	SeeSound "Boss3/Sighted"
	DeathSound "Boss3/Death"
	DropItem NebelwerferSpawner,64
	DropItem UMG43Spawner,8
	+MISSILEEVENMORE
	States
	{
	Spawn2:
		FETT A 10 A_Look
		Loop
	See:
		FETT AABBCCDD 4 A_Chase
		Loop
	Missile:
		FETT E 10 A_FaceTarget
		FETT E 0 A_Jump(128,"Chaingun")
		FETT F 8 BRIGHT A_CustomMissile("MiniRocket",32,15)
		FETT E 8 A_FaceTarget
		FETT F 8 BRIGHT A_CustomMissile("MiniRocket",32,15)
		FETT E 8 A_FaceTarget
		FETT F 8 BRIGHT A_CustomMissile("MiniRocket",32,15)
		FETT E 8
		Goto See
	Chaingun:
		FETT G 0 A_FaceTarget
		FETT H 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		FETT H 2 BRIGHT A_CustomMissile("EnemyChaingunTracer",40,-12,random(-11,11))
		FETT G 2 BRIGHT A_MonsterRefire(30,"See")
		Loop
	Pain:
		FETT I 3
		FETT I 3 A_Pain
		Goto See
	Death:
		FETT J 5
		FETT K 5 A_Scream
		FETT L 5
		FETT M -1 A_NoBlocking
		Stop
	Raise:
		FETT MLKJ 5
		Goto See
	}
}

ACTOR FlamerMan : BigNazi replaces Arachnotron
{
	Obituary "%o was roasted by a Nazi with a flamethrower."
	Health 500
	SeeSound "Boss1/Sighted"
	DeathSound "Boss1/Death"
	DropItem PyrolightPickup
	DropItem FlameAmmo
	States
	{
	Spawn2:
		FLAM A 10 A_Look
		Loop
	See:
		FLAM AABBCCDD 3 A_Chase
		Loop
	Missile:
		FLAM E 10 A_FaceTarget
		FLAM E 0 A_JumpIfCloser(384,"ProperMissile")
		FLAM F 4 BRIGHT A_CustomMissile("EnemyFlamebolt",32,12)
		FLAM E 6
		Goto See
	ProperMissile:
		FLAM FFFFFFFF 1 BRIGHT A_CustomMissile("EnemyFlamerShot",32,12)
		FLAM E 6 A_MonsterRefire(10,"See")
		Goto Missile
	Pain:
		FLAM A 3
		FLAM A 3 A_Pain
		Goto See
	Death:
		FLAM G 5
		FLAM H 5 A_Scream
		FLAM I 5 A_BossDeath
		FLAM J -1 A_NoBlocking
		Stop
	Raise:
		FLAM JIHG 5
		Goto See
	}
}

ACTOR EnemyFlamerShot : Flamebolt
{
	-RIPPER
	DamageType "Enemy"
}

ACTOR EnemyFlamebolt : Flameball
{
	DamageType "Enemy"
	Damage 8
	Scale .5
	States
	{
	Death:
		FBLX A 2 BRIGHT A_Explode(8,64)
		FBLX BCDEFGHIJK 2 BRIGHT
		Stop
	}
}

ACTOR HitlerGhost : BigNazi replaces Cacodemon
{
	Scale 1.2
	Obituary "%o thought %g saw Hitler."
	Health 250
	Mass 1000
	Speed 13
	PainChance 24
	DeathSound "hitlerghost/die"
	SeeSound "hitlerghost/see"
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	-FLOORCLIP
	States
	{
	Spawn2:
		HGST A 10 A_Look
		Loop
	See:
		HGST A 6 A_FastChase
		HGST BCB 6 A_Chase
		Loop
	Missile:
		HGST A 4 A_FaceTarget
		HGST D 4 BRIGHT A_CustomMissile("EnemyFlamebolt2",30)
		HGST A 4
		Goto See
	Pain:
		HGST A 4
		Goto See
	Death:
		HGST E 6 A_Scream
		HGST FGHI 6
		HGST J -1 A_NoBlocking
		Stop
	Idle:
		Stop
	}
}

ACTOR EnemyFlamebolt2 : EnemyFlamebolt
{
	Damage (random(1,4)*5)
	Scale .35
	States
	{
	Death:
		FBLX ABCDEFGHIJK 2 BRIGHT
		Stop
	}
}

ACTOR MiniShip : BigNazi replaces PainElemental
{
	Obituary "%o received death from above."
	Health 600
	Mass 2000
	PainChance 200
	PainSound "metal/pain"
	-FLOORCLIP
	+FLOAT
	+NOGRAVITY
	+NOBLOOD
	States
	{
	Spawn2:
		SHIP A 10 A_Look
		Loop
	See:
		SHIP A 4 A_Chase
		Loop
	Missile:
		SHIP A 10 A_FaceTarget
		SHIP B 8 BRIGHT A_CustomMissile("MiniRocket",20,4,random(-11,11))
		SHIP A 8 A_FaceTarget
		SHIP C 8 BRIGHT A_CustomMissile("MiniRocket",20,-4,random(-11,11))
		SHIP A 8
		Goto See
	Pain:
		SHIP A 3
		SHIP A 3 A_Pain
		Goto See
	Death:
		SHIP A 5
		SHIP A -1 A_Scream
		Wait
	Crash:
		SHIP H 0 A_SetFloorClip
		BARX A 0 A_PlaySound("world/barrelboom")
		BARX A 2 BRIGHT A_Explode(40)
		BARX BCDEFGHIJKLM 2 BRIGHT
		Stop
	}
}

ACTOR Geist : BigNazi replaces LostSoul
{
	Scale 0.8
	Obituary "%o had %p soul removed by a Geist."
	Health 100
	Radius 16
	Mass 1000
	Speed 15
	PainChance 255
	RenderStyle Translucent
	Alpha 0.5
	DeathSound "geist/die"
	PainSound "geist/pain"
	SeeSound ""
	-FLOORCLIP
	+FLOAT
	+NOGRAVITY
	+DONTFALL
	+NOBLOOD
	States
	{
	Spawn2:
		GEST A 10 A_Look
		Loop
	See:
		GEST A 0 A_PlaySound("geist/active",CHAN_VOICE,1,1)
		GEST A 0 A_SetTranslucent(0.1)
		GEST ABCD 3 A_Chase
		GEST A 0 A_SetTranslucent(0.2)
		GEST ABCD 3 A_Chase
		GEST A 0 A_SetTranslucent(0.3)
		GEST ABCD 3 A_Chase
		GEST A 0 A_SetTranslucent(0.4)
		GEST ABCD 3 A_Chase
		GEST A 0 A_SetTranslucent(0.5)
		GEST ABCD 3 A_Chase
		GEST A 0 A_SetTranslucent(0.4)
		GEST ABCD 3 A_Chase
		GEST A 0 A_SetTranslucent(0.3)
		GEST ABCD 3 A_Chase
		GEST A 0 A_SetTranslucent(0.2)
		GEST ABCD 3 A_Chase
		Loop
	Melee:
		GEST A 1 A_SetTranslucent(0.1)
		GEST AA 1 A_FadeIn(0.2)
		GEST B 3 BRIGHT A_FaceTarget
		GEST CDABCD 3 BRIGHT A_CustomMeleeAttack(4)
		GEST AAAA 1 A_Fadeout(0.1)
		Goto See
	Pain:
		GEST E 1 A_Pain
		GEST E 1 A_SetTranslucent(0.4)
		GEST EEE 1 A_Fadeout(0.1)
		Goto See
	Death:
		GEST E 5 A_SetTranslucent(0.5)
		GEST E 6 A_Scream
		GEST FG 6
		GEST H 6 A_NoBlocking
		Stop
	Idle:
		Stop
	}
}

ACTOR MGTurret : BigNazi // replaces LostSoul
{
	Scale 1
	Speed 0
	Obituary "%o was shot up by a gun turret."
	Health 100
	Radius 24
	Height 24
	Mass 10000000000000000
	PainChance 100
	PainSound "metal/pain"
	DeathSound "world/barrelboom"
	MONSTER
	-FLOORCLIP
	+MISSILEEVENMORE
	+NOGRAVITY
	+NOBLOOD
	States
	{
	Spawn2:
		GTUR A 1 A_Look
		Loop
	See:
		GTUR A 4 A_Chase
		Loop
	Pain:
		GTUR A 3
		GTUR A 3 A_Pain
		Goto See
	Missile:
		GTUR A 10 A_FaceTarget
		GTUR B 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		GTUR C 3 BRIGHT A_CustomMissile("EnemyChaingunTracer",20,0,random(-11,11))
		GTUR B 3 BRIGHT A_MonsterRefire(40,"See")
		GTUR B 0 A_FaceTarget
		Goto Missile+1
	Death:
		TNT1 A 0 A_Scream
		GTUR D 6 BRIGHT A_Explode(40)
		GTUR EFG 6 BRIGHT
		GTUR H -1 A_NoBlocking
		Stop
	Idle:
		Stop
	}
}

ACTOR DeathKnight : BigNazi replaces Cyberdemon
{
	Scale 1.5
	Obituary "%o knelt before the Death Knight."
	Health 3000
	Radius 64
	Height 96
	Mass 1000
	Speed 14
	PainChance 20
	SeeSound "deathknight/sight"
	DeathSound "deathknight/death"
	DropItem NebelwerferSpawner
	+MISSILEEVENMORE
	+BOSS
	+NORADIUSDMG
	States
	{
	Spawn2:
		DKGT A 10 A_Look
		Loop
	See:
		DKGT AABB 4 A_Chase
		DKGT A 0 A_Chase
		DKGT A 0 A_PlaySound("hitler/walk")
		DKGT CCDD 4 A_Chase
		DKGT A 0 A_Chase
		DKGT A 0 A_PlaySound("hitler/walk")
		Loop
	Missile:
		DKGT E 10 A_FaceTarget
		DKGT E 0 A_Jump(40,"AlphaStrike")
		DKGT E 0 A_Jump(128,"Chaingun")
		DKGT F 3 BRIGHT A_CustomMissile("NebRocketEnemy",80,24)
		DKGT E 3 A_MonsterRefire(40,"See")
		DKGT G 3 BRIGHT A_CustomMissile("NebRocketEnemy",80,-24)
		DKGT E 3 A_MonsterRefire(40,"See")
		DKGT F 3 BRIGHT A_CustomMissile("NebRocketEnemy",80,24)
		DKGT E 3 A_MonsterRefire(40,"See")
		DKGT G 3 BRIGHT A_CustomMissile("NebRocketEnemy",80,-24)
		DKGT E 3
		Goto See
	Chaingun:
		DKGT H 0 A_FaceTarget
		DKGT K 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		DKGT II 0 A_CustomMissile("EnemyChaingunTracer",40,24,random(-11,11))
		DKGT II 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",40,-24,random(-11,11))
		DKGT H 2 BRIGHT A_MonsterRefire(30,"See")
		Loop
	AlphaStrike:
		DKGT K 0 A_FaceTarget
		DKGT K 0 A_CustomMissile("NebRocketEnemy",80,24)
		DKGT K 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		DKGT KK 0 A_CustomMissile("EnemyChaingunTracer",40,24,random(-11,11))
		DKGT KK 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",40,-24,random(-11,11))
		DKGT L 0 A_CustomMissile("NebRocketEnemy",80,-24)
		DKGT L 2 BRIGHT A_MonsterRefire(30,"See")
		Loop
	Pain:
		DKGT E 3
		DKGT E 3 A_Pain
		Goto See
	Death:
		DKGT M 5 A_Scream
		DKGT NOP 5
		DKGT Q 5 A_NoBlocking
		DKGT R 5
		DKGT S -1 A_BossDeath
		Stop
	Idle:
		Stop
	}
}

ACTOR NebRocketEnemy : NebRocket
{
	Speed 15
	Damagetype "Enemy"
	-FORCERADIUSDMG
	SeeSound "weapons/nebelwerfer"
	States
	{
	Death:
		FRME A 1 BRIGHT A_Explode(40)
		FRME BCDEFGHIHJKLMNOPQRS 1 BRIGHT
		Stop
	}
}

ACTOR Hitler : BigNazi replaces SpiderMastermind
{
	Obituary "%o heiled Hitler."
	Health 2500
	Radius 32
	Height 64
	Mass 1000
	Speed 10
	PainChance 20
	SeeSound "hitler/sight"
	PainSound "hitler/pain"
	DeathSound "hitler/death"
	ActiveSound "hitler/active"
	DropItem UMG43Spawner,64
	+MISSILEEVENMORE
	+BOSS
	+NORADIUSDMG
	States
	{
	Spawn2:
		HITL A 10 A_Look
		Loop
	See:
		HITL AABB 4 A_Chase
		HITL A 0 A_Chase
		HITL A 0 A_PlaySound("hitler/walk")
		HITL CCDD 4 A_Chase
		HITL A 0 A_Chase
		HITL A 0 A_PlaySound("hitler/walk")
		Loop
	Missile:
		HITL E 10 A_FaceTarget
		HITL F 0 A_FaceTarget
		HITL F 0 A_PlaySound("weapons/chaingun",CHAN_WEAPON)
		HITL F 0 A_CustomMissile("EnemyChaingunTracer",32,24,random(-3,3))
		HITL F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",32,-24,random(-3,3))
		HITL F 0 A_CustomMissile("EnemyChaingunTracer",48,24,random(-3,3))
		HITL F 1 BRIGHT A_CustomMissile("EnemyChaingunTracer",48,-24,random(-3,3))
		HITL G 2 BRIGHT A_MonsterRefire(30,"See")
		Goto Missile+1
	Pain:
		HITL Q 3
		HITL Q 3 A_Pain
		Goto See
	Death:
		HITL H 10 A_Scream
		HITL I 10 A_SpawnItemEx("HitlerSuitScrap",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		HITL JK 10
		HITL L 10 A_PlaySound("hitler/gib")
		HITL M 10 A_NoBlocking
		HITL NO 10
		HITL P -1 A_BossDeath
		Stop
	Idle:
		Stop
	}
}

ACTOR HitlerSuitScrap
{
	Scale 1.1
	+FLOORCLIP
	States
	{
	Spawn:
		HIT2 AB 20
		HIT2 C -1
		Stop
	}
}

ACTOR ChairGuy : BigNazi replaces Fatso
{
	Obituary "%o was given the chair."
	//Obituary "%o was vaporized by a Nazi's double rocket penetration."
	Health 600
	Mass 1000
	SeeSound "krist/sight"
	DeathSound "krist/death"
	DropItem NebelwerferSpawner,64
	DropItem GrenadePickup,128
	+MISSILEEVENMORE
	States
	{
	Spawn2:
		KRIS A 10 A_Look
		Loop
	See:
		KRIS AABB 4 A_Chase
		Loop
	Missile:
		KRIS E 10 A_FaceTarget
		KRIS F 6 BRIGHT A_CustomMissile("MiniMiniRocket",48,15)
		KRIS E 6 A_FaceTarget
		KRIS G 6 BRIGHT A_CustomMissile("MiniMiniRocket",48,-15)
		KRIS E 6 A_FaceTarget
		KRIS F 6 BRIGHT A_CustomMissile("MiniMiniRocket",48,15)
		KRIS E 6 A_FaceTarget
		KRIS G 6 BRIGHT A_CustomMissile("MiniMiniRocket",48,-15)
		Goto See
	Pain:
		KRIS H 3
		KRIS H 3 A_Pain
		Goto See
	Death:
		KRIS H 8 A_Scream
		KRIS HHH 8 A_SetAngle(angle+45)
		KRIS HHHHHH 4 A_SetAngle(angle+45)
		KRIS HHHHHHHHHHHH 2 A_SetAngle(angle+45)
		KRIS HHHHHHHHHHHH 1 A_SetAngle(angle+45)
		BARX A 0 A_PlaySound("world/barrelboom")
		BARX A 2 BRIGHT A_Explode(40)
		BARX BCDEFGHIJKLM 2 BRIGHT
		Stop
	}
}

ACTOR MiniMiniRocket : MiniRocket
{
	Scale .8
	Damage (random(1,4)*4)
	DeathSound "weapons/nebxpl"
}

ACTOR DeadPlayer replaces DeadMarine
{
	States
	{
	Spawn:
		WEAS L -1
		Stop
	}
}
ACTOR DeadPlayer2 : DeadPlayer replaces GibbedMarine{}
ACTOR DeadPlayer3 : DeadPlayer replaces GibbedMarineExtra{}

ACTOR DeadGuard replaces DeadZombieMan
{
	Scale 1.1
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3)
		GARD M -1
		SSPG M -1
		GRD2 M -1
		Stop
	}
}

ACTOR DeadMP40Guard : DeadGuard replaces DeadShotgunGuy
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3)
		MGRD M -1
		MGR2 M -1
		SSMG M -1
		Stop
	}
}

ACTOR DeadOfficer : DeadGuard replaces DeadDoomImp
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,1,2,3)
		OFFI M -1
		OFR2 M -1
		SSOF M -1
		Stop
	}
}

ACTOR DeadDog replaces DeadDemon
{
	States
	{
	Spawn:
		DOGY K -1 NODELAY A_Jump(128,1)
		DOG2 K -1
		Stop
	}
}

ACTOR DeadHitlerGhost replaces DeadCacodemon
{
	Scale 1.2
	States
	{
	Spawn:
		HGST J -1
		Stop
	}
}

ACTOR EasyMiniRocket : MiniRocket
{
	DamageType "EasyEnemy"
}
ACTOR EasyFlyingNeedle : FlyingNeedle
{
	DamageType "EasyEnemy"
}
ACTOR EasyEnemyFlamerShot : EnemyFlamerShot
{
	DamageType "EasyEnemy"
}
ACTOR EasyEnemyFlamebolt : EnemyFlamebolt
{
	DamageType "EasyEnemy"
}
ACTOR EasyEnemyFlamebolt2 : EnemyFlamebolt2
{
	DamageType "EasyEnemy"
}
ACTOR EasyNebRocketEnemy : NebRocketEnemy
{
	DamageType "EasyEnemy"
}
ACTOR EasyMiniMiniRocket : MiniMiniRocket
{
	DamageType "EasyEnemy"
}

ACTOR HardMiniRocket : MiniRocket
{
	+THRUGHOST
}
ACTOR HardFlyingNeedle : FlyingNeedle
{
	+THRUGHOST
}
ACTOR HardEnemyFlamerShot : EnemyFlamerShot
{
	+THRUGHOST
}
ACTOR HardEnemyFlamebolt : EnemyFlamebolt
{
	+THRUGHOST
}
ACTOR HardEnemyFlamebolt2 : EnemyFlamebolt2
{
	+THRUGHOST
}
ACTOR HardNebRocketEnemy : NebRocketEnemy
{
	+THRUGHOST
}
ACTOR HardMiniMiniRocket : MiniMiniRocket
{
	+THRUGHOST
}