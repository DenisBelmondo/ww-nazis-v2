//Rate of fire: 900-1000+ rounds/min - Wikipedia
ACTOR PPSH41 : NaziWeapon
{
	Weapon.SlotNumber 3
	Scale .8
	Weapon.SelectionOrder 600
	Weapon.AmmoType "PPSH41Loaded"
	Weapon.AmmoUse 1
	weapon.ammotype2 "TokarevAmmo"
	weapon.ammouse2 1
	Tag "PPSh-41"
	Inventory.PickupMessage "You got the PPSh-41! (slot 4)"
	States
	{
	Ready:
		PPSG A 0 A_JumpIfInventory("PPSH41Loaded",0,2)
		PPSG A 0 A_JumpIfInventory("TokarevAmmo",1,2)
		PPSG A 1 A_WeaponReady
		Loop
		PPSG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		PPSG A 0 A_Lower
		PPSG A 1 A_Lower
		Loop
	Select:
		PPSG A 0 A_Raise
		PPSG A 1 A_Raise
		Loop
	Fire:
		PPSG A 0 A_JumpIfInventory("PPSH41Loaded",1,1)
		Goto Dryfire
		PPSG A 0 A_GunFlash
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt",0,1)
		PPSG A 0 A_PlaySound("ppsh/fire",CHAN_WEAPON)
		PPSG A 0 A_SpawnItemEx("TokarevCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		PPSG A 0 A_AlertMonsters
		PPSG A 1 A_FireCustomMissile("PPSHTracer",frandom(-2.5,2.5),1,0,0,0,frandom(-1.0,1.0))
		PPSG A 1 Offset(0,40) A_SetPitch(pitch-(0.6*CallACS("RecoilAmount")))
		PPSG A 1 Offset(0,43) A_Refire
		PPSG A 1 Offset(0,42) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,43) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,40) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,36) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Hold:
		PPSG A 0 A_JumpIfInventory("PPSH41Loaded",1,1)
		Goto Dryfire
		PPSG A 0 A_GunFlash
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt",0,1)
		PPSG A 0 A_PlaySound("ppsh/fire",CHAN_WEAPON)
		PPSG A 0 A_SpawnItemEx("TokarevCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		PPSG A 0 A_AlertMonsters
		PPSG A 1 A_FireCustomMissile("PPSHTracer",frandom(-2.5,2.5),1,0,0,0,frandom(-1.0,1.0))
		PPSG A 1 Offset(0,52) A_SetPitch(pitch-(0.8*CallACS("RecoilAmount")))
		PPSG A 1 Offset(0,53) A_Refire
		PPSG A 1 Offset(0,52) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,54) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,53) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,50) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,46) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,41) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Reload:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt",0,2)
		PPSG A 1 Offset(0,35) A_PlaySound("ppsh/reload",5)
		PPSG A 1 Offset(0,38)
		PPSG A 1 Offset(0,44)
		PPSG B 1 Offset(0,52)
		PPSG B 1 Offset(-1,54)
		PPSG B 1 Offset(-2,56)
		PPSG B 1 Offset(-3,58)
		PPSG B 1 Offset(-4,58)
		PPSG B 1 Offset(-4,57)
		PPSG B 1 Offset(-3,54)
		PPSG B 1 Offset(-3,51)
		PPSG B 1 Offset(-3,53)
		PPSG B 1 Offset(-3,55)
		PPSG B 1 Offset(-3,56)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("TokarevAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("PPSH41Loaded")
		TNT1 A 0 A_JumpIfInventory("PPSH41Loaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("TokarevAmmo",1,"ReloadLoop")
	ReloadFinish:
		PPSG B 1 Offset(-3,57)
		PPSG B 1 Offset(-3,59)
		PPSG B 1 Offset(-3,63)
		PPSG B 1 Offset(-3,67)
		PPSG A 1 Offset(-3,65)
		PPSG A 1 Offset(-3,62)
		PPSG A 1 Offset(-3,58)
		PPSG A 1 Offset(-3,55)
		PPSG A 1 Offset(-2,53)
		PPSG A 1 Offset(-2,49)
		PPSG A 1 Offset(-2,46)
		PPSG A 1 Offset(-2,45)
		PPSG A 1 Offset(-1,44)
		PPSG A 1 Offset(-1,46)
		PPSG A 1 Offset(-1,47)
		PPSG A 1 Offset(0,49) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,38) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		PPSG A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Flash:
		PPSF A 1 BRIGHT A_Light2
		PPSF B 1 BRIGHT
		PPSF C 1 A_Light1
		TNT1 A 1
		Goto LightDone
	Spawn:
		PPSH A -1
		Stop
	}
}

ACTOR PPSH41Loaded : Ammo
{
	Tag "7.62x25mm"
	+IGNORESKILL
	Inventory.MaxAmount 71
	Inventory.Icon PPSHA0
}

ACTOR PPSH41Spawner
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("PPSH41Pickup",0,0,0,velx,vely,velz)
		TNT1 AAA 0 A_SpawnItemEx("TokarevAmmo",frandom(-16,16),frandom(-16,16),0,velx,vely,velz,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR PPSH41Pickup : NaziWeaponPickup
{
	Scale .8
	States
	{
	Spawn:
		PPSH A -1
		Stop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("WWNaziWeaponCollect",11)
		Stop
	}
}