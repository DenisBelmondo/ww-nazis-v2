ACTOR NewShell : Ammo
{
	Tag "12 Gauge"
	Inventory.PickupMessage "Picked up some shotgun shells."
	Inventory.Amount 4
	Inventory.MaxAmount 32
	Ammo.BackpackAmount 4
	Ammo.BackpackMaxAmount 64
	ConversationID 185
	States
	{
	Spawn:
		SHEL A -1
		Stop
	}
}

ACTOR NewShellBox : NewShell
{
	Inventory.Amount 16
	Inventory.PickupMessage "Picked up a box of shells."
	States
	{
	Spawn:
		SBOX A -1
		Stop
	}
}

ACTOR Browning5Loaded : Ammo
{
	Tag "12 Gauge"
	Inventory.MaxAmount 4
	+IGNORESKILL
	Inventory.Icon BA5PA0
}

ACTOR Browning5 : NaziWeapon
{
	Weapon.SelectionOrder 400
	Weapon.AmmoUse1 1
	Weapon.AmmoType1 "Browning5Loaded"
	Weapon.AmmoType2 "NewShell"
	Weapon.AmmoUse2 1
	Scale .5
	Inventory.PickupMessage "You got the Browning Auto-5! (slot 3)"
	Obituary "%o was peppered by %k's auto shotgun."
	Tag "Browning Auto-5"
	+NOAUTOFIRE
	States
	{
	Spawn:
		BA5P A -1
		Stop
	Select:
		BA5G A 0 A_Raise
		BA5G A 1 A_Raise
		Loop
	Deselect:
		BA5G A 0 A_TakeInventory("ShotgunReloading")
		BA5G A 0 A_Lower
		BA5G A 1 A_Lower
		Loop
	Ready:
		BA5G A 0 A_JumpIfInventory("Browning5Loaded",0,2)
		BA5G A 0 A_JumpIfInventory("NewShell",1,2)
		BA5G A 1 A_WeaponReady
		Loop
		BA5G A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		BA5G A 0 A_JumpIfInventory("ShotgunReloading",1,"ReloadFinish")
		BA5G A 0 A_JumpIfInventory("Browning5Loaded",1,1)
		Goto Dryfire
		BA5G A 0 A_PlaySound("weapons/ba5fire",CHAN_WEAPON)
		BA5G A 0 A_SpawnItemEx("ShotgunCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		BA5G AAAAAAAAAA 0 A_FireCustomMissile("ShotgunTracer",frandom(-4.0,4.0),0,0,0,0,frandom(-3,3))
		BA5G A 0 A_TakeInventory("Browning5Loaded",1)
		BA5G A 1 A_GunFlash
		BA5G A 1 Offset(0,42) A_SetPitch(pitch-(4.0*CallACS("RecoilAmount")))
		BA5G A 1 Offset(0,47) A_SetPitch(pitch-(2.0*CallACS("RecoilAmount")))
		BA5G A 1 Offset(0,36) A_SetPitch(pitch-(1.0*CallACS("RecoilAmount")))
		BA5G A 1 Offset(0,34) A_SetPitch(pitch+(1.0*CallACS("RecoilAmount")))
		BA5G A 2 Offset(0,33) A_SetPitch(pitch+(0.5*CallACS("RecoilAmount")))
		BA5G A 2 A_AlertMonsters
		BA5G A 1 ACS_NamedExecute("WWNaziTaunt",0,1)
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		BA5G E 0 ACS_NamedExecute("WWNaziTaunt",0,2)
		BA5G E 1 Offset(-1,33) A_GiveInventory("ShotgunReloading")
		BA5G E 1 Offset(-4,41)
		BA5G E 1 Offset(-6,46)
		BA5G E 1 Offset(-7,55)
	ReloadWork:
		BA5G A 0 A_TakeInventory("NewShell",1,TIF_NOTAKEINFINITE)
		BA5G A 0 A_GiveInventory("Browning5Loaded")
		BA5G E 1 Offset(-8,58) A_PlaySound("weapons/ba5Load",5)
		BA5G E 1 Offset(-9,64)
		BA5G E 1 Offset(-10,70)
		BA5G E 1 Offset(-10,68)
		BA5G E 1 Offset(-9,66)
		BA5G E 1 Offset(-9,64)
		BA5G E 1 Offset(-9,62)
		BA5G E 1 Offset(-8,61) A_WeaponReady(WRF_NOBOB)
		BA5G E 2 Offset(-8,60) A_WeaponReady(WRF_NOBOB)
		BA5G E 3 Offset(-8,59) A_WeaponReady(WRF_NOBOB)
		BA5G E 4 Offset(-8,58) A_WeaponReady(WRF_NOBOB)
		BA5G A 0 A_JumpIfInventory("Browning5Loaded",4,"ReloadFinish")
		BA5G A 0 A_JumpIfInventory("NewShell",1,"ReloadWork")
	ReloadFinish:
		BA5G E 1 Offset(-8,58) A_PlaySound("weapons/ba5cock",5)
		BA5G E 1 Offset(-8,60)
		BA5G E 1 Offset(-8,63)
		BA5G E 1 Offset(-9,67)
		BA5G E 1 Offset(-9,72)
		BA5G E 1 Offset(-10,75)
		BA5G E 2 Offset(-10,77)
		BA5G E 3 Offset(-11,78)
		BA5G E 1 Offset(-10,76)
		BA5G E 1 Offset(-8,68)
		BA5G E 1 Offset(-6,58)
		BA5G E 1 Offset(-4,46)
		BA5G E 1 Offset(-2,33)
		BA5G E 1 Offset(-1,32) A_TakeInventory("ShotgunReloading")
		Goto Ready
	Flash:
		BA5G B 1 BRIGHT A_Light2
		BA5G C 1 BRIGHT
		BA5G D 1 A_Light1
		TNT1 A 1
		Goto LightDone
	}
}

ACTOR Browning5Spawner replaces SuperShotgun
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("Browning5Pickup",0,0,0,velx,vely,velz)
		TNT1 AA 0 A_SpawnItemEx("NewShell",frandom(-16,16),frandom(-16,16),0,velx,vely,velz,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR Browning5Pickup : NaziWeaponPickup
{
	Scale .5
	States
	{
	Spawn:
		BA5P A -1
		Stop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("WWNaziWeaponCollect",7)
		Stop
	}
}