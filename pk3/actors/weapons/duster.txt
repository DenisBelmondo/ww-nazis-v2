ACTOR KnuckleDusters : NaziWeapon
{
	Scale .1
	Tag "Knuckle Dusters"
	-WEAPON.AMMO_CHECKBOTH
	Weapon.SelectionOrder 10000
	Inventory.PickupMessage "You got the knuckle dusters! (slot 1)"
	Weapon.Kickback 40
	States
	{
	Ready:
		PUNG A 1 A_WeaponReady
		Loop
	Deselect:
		PUNG A 1 A_Lower
		Loop
	Select:
		PUNG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_Jump(128,"Fire2")
		PUNG B 1 ACS_NamedExecute("WWNaziTaunt",0,1)
		PUNG C 1
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,3)
		PUNG D 4 A_CustomPunch(3,0,0,"HitPuff")
		TNT1 A 0 A_Jump(256,2)
		PUNG D 4 A_CustomPunch(3*10,0,0,"HitPuff")
		PUNG CB 2
		TNT1 A 5
		Goto Ready
	Fire2:
		PUNG FG 1
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt",0,1)
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,3)
		PUNG H 4 A_CustomPunch(4,0,0,"HitPuff")
		TNT1 A 0 A_Jump(256,2)
		PUNG H 4 A_CustomPunch(4*10,0,0,"HitPuff")
		PUNG GF 2
		TNT1 A 5
		Goto Ready
	Spawn:
		PUNG Z -1
		Stop
	}
}

ACTOR HitPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	+NOEXTREMEDEATH
	activesound "melee/miss"
	attacksound "melee/wall"
	seesound "melee/hit"
	Obituary "%o was dusted by %k."
	states
	{
	Spawn:
		TNT1 A 1
		Stop
	}
}

ACTOR KnuckleDustersPickup : NaziWeaponPickup
{
	Scale .1
	States
	{
	Spawn:
		PUNG Z -1
		Stop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("WWNaziWeaponCollect",1)
		Stop
	}
}