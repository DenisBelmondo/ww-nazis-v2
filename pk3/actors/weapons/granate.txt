ACTOR GrenadePickup : CustomInventory replaces RocketAmmo
{
	inventory.pickupmessage "Picked up a grenade."
	Tag "Frag Grenade"
	inventory.maxamount 9
	inventory.icon TNT1A0
	inventory.usesound "grenade/throw"
	inventory.pickupsound "grenade/pickup"
	+Inventory.invbar
	+inventory.keepdepleted
	Inventory.InterHubamount 9
	ConversationID 181
	states
	{
	Spawn:
		THRP A -1
		stop
	Use:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt",0,3)
		TNT1 A 0 A_FireCustomMissile("HandGrenade",0,0)
		stop
	}
}

ACTOR HandGrenade
{
	Radius 4
	Height 3
	Speed 40
	Damage (1)
	DamageType "Frag"
	Projectile
	-NOGRAVITY
	-NOTELEPORT
	+CANBOUNCEWATER
	Obituary "%o failed to field %k's fragmenting fastball."
	BounceType Doom
	BounceFactor 0.2
	WallBounceFactor 0.6
	bouncesound "grenade/bounce"
	states
	{
	Spawn:
		THRW A 2 A_Countdown
		THRW BCDEFGH 2
		loop
	Death:
		THRW B 35
		FRME A 0 A_AlertMonsters
		FRME A 0 A_SetScale(1.75,1.75)
		FRME A 0 A_SetTranslucent(0.75,1)
		FRME A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FRME A 1 bright A_Explode(128,256,1,1,128,15,2)
		FRME B 1 BRIGHT Radius_Quake(10,10,0,120,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}