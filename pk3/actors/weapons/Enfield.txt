// Enfield bolt-action rifle.
// Sprite from Day of Defeat model, sounds from MOHAA Breakthrough and NOLF.
// Pickup sprite by WildWeasel.

ACTOR EnfieldAmmo : Ammo
{
	Tag ".303 British"
	Inventory.Amount 20
	Inventory.MaxAmount 40
	Ammo.BackpackAmount 5
	Ammo.BackpackMaxAmount 100
	Inventory.PickupMessage "Picked up a box of .303 British ammo."
	States
	{
	Spawn:
		M1GA A -1	//Placeholder sprite
		Stop
	}
}

ACTOR EnfieldAmmoSmall : EnfieldAmmo
{
	Inventory.Amount 5
	Inventory.PickupMessage "Picked up a .303 charger clip."
	Scale 0.3
	States
	{
	Spawn:
		M1GA B -1	//Placeholder sprite
		Stop
	}
}

ACTOR EnfieldLoaded : Ammo
{
	//Tag ".303 British"
	Tag "7.92x57mm"
	+IGNORESKILL
	Inventory.MaxAmount 10
	Inventory.Icon ENFPA0
}

ACTOR Enfield : NaziWeapon
{
	//Weapon.SelectionOrder 0
	Weapon.AmmoType1 "EnfieldLoaded"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "MauserAmmo"
	Weapon.AmmoUse2 1
	Tag "Lee-Enfield SMLE"
	Inventory.PickupMessage "You got the Lee-Enfield SMLE! (slot 5)"
	//Obituary "%o was killed by %k."
	+NOAUTOFIRE
	States
	{
	Spawn:
		ENFP A -1
		Stop
	Select:
		ENFG A 0 A_Raise
		ENFG A 1 A_Raise
		Loop
	Deselect:
		ENFG A 0 A_Lower
		ENFG A 1 A_Lower
		Loop
	Ready:
		ENFG A 0 A_JumpIfInventory("EnfieldLoaded",0,2)
		ENFG A 0 A_JumpIfInventory("MauserAmmo",1,2)
		ENFG A 1 A_WeaponReady
		Loop
		ENFG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		ENFG A 0 A_JumpIfInventory("EnfieldLoaded",1,1)
		Goto Reload
		ENFF A 0 A_PlaySound("Enfield/Fire",CHAN_WEAPON)
		ENFF A 1 Bright Offset(0,40) A_FireCustomMissile("EnfieldTracer")
		ENFF B 1 Bright Offset(0,50) A_GunFlash
		ENFG A 0 A_AlertMonsters
		ENFG A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1)
		ENFG A 1 Offset(0,45) A_SetPitch(pitch - 4.0)
		ENFG A 1 Offset(0,40) A_SetPitch(pitch - 2.0)
		ENFG A 1 Offset(0,36) A_SetPitch(pitch + 1.0)
		ENFG A 1 Offset(0,34) A_SetPitch(pitch + 1.0)
		ENFG A 1 Offset(0,33) A_SetPitch(pitch + 0.5)
		ENFG A 1 Offset(-2,33) A_PlaySound("Enfield/boltback",5)
		ENFG A 1 Offset(-4,34)
		ENFG A 1 Offset(-6,35)
		ENFG A 2 Offset(-8,36)
		ENFG A 2 Offset(-4,42)
		ENFG A 1 Offset(0,51) A_SpawnItemEx("MauserRifleCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		ENFG A 1 Offset(4,60)
		ENFG A 2 Offset(5,74)
		ENFG A 3 Offset(6,76)
		ENFG A 2 Offset(5,74) A_PlaySound("Enfield/boltfwd",5)
		ENFG A 1 Offset(4,60)
		ENFG A 1 Offset(0,51)
		ENFG A 1 Offset(-4,42)
		ENFG A 2 Offset(-8,36)
		ENFG A 1 Offset(-6,35)
		ENFG A 1 Offset(-4,34)
		ENFG A 1 Offset(-2,33)
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		ENFG A 0 ACS_NamedExecute("WWNaziTaunt", 0, 2)
		ENFG A 1 Offset(-2,33) A_PlaySound("Enfield/boltback",5)
		ENFG A 1 Offset(-4,34)
		ENFG A 1 Offset(-6,35)
		ENFG A 2 Offset(-8,36)
		ENFG A 2 Offset(-4,42)
		ENFG A 1 Offset(0,51)
		ENFG A 1 Offset(4,60)
		ENFG A 2 Offset(5,74)
		ENFG A 3 Offset(6,76)
		ENFG A 2 Offset(5,74)
		ENFG A 3 Offset(6,76)
		ENFG A 5
		ENFG A 0 A_JumpIfInventory("EnfieldLoaded",5,"ReloadWork2")
		ENFG A 0 A_JumpIfInventory("MauserAmmo",5,1)
		Goto ReloadWork2
		TNT1 A 0 A_TakeInventory("MauserAmmo",5)
		TNT1 A 0 A_GiveInventory("EnfieldLoaded",5)
		ENFG A 1 Offset(6,80) A_PlaySound("Enfield/load",5)
		ENFG A 1 Offset(6,84)
		ENFG A 1 Offset(6,87)
		ENFG A 1 Offset(7,90)
		ENFG A 1 Offset(8,92)
		ENFG A 1 Offset(9,88)
		ENFG A 1 Offset(8,82)
		ENFG A 1 Offset(7,77)
		ENFG A 0 A_JumpIfInventory("EnfieldLoaded",10,"ReloadOnlyOneClip")
		TNT1 A 0 A_JumpIfInventory("MauserAmmo",1,1)
		Goto ReloadOnlyOneClip
		ENFG A 5 Offset(6,75)
	ReloadWork2:
		TNT1 A 0 A_TakeInventory("MauserAmmo",1)
		TNT1 A 0 A_GiveInventory("EnfieldLoaded")
		TNT1 A 0 A_JumpIfInventory("EnfieldLoaded",10,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("MauserAmmo",1,"ReloadWork2")
	ReloadFinish:
		ENFG A 1 Offset(6,80) A_PlaySound("Enfield/load",5)
		ENFG A 1 Offset(6,84)
		ENFG A 1 Offset(6,87)
		ENFG A 1 Offset(7,90)
		ENFG A 1 Offset(8,92)
		ENFG A 1 Offset(9,88)
		ENFG A 1 Offset(8,82)
		ENFG A 1 Offset(7,77)
	ReloadOnlyOneClip:
		ENFG A 2 Offset(5,74) A_PlaySound("Enfield/boltfwd",5)
		ENFG A 1 Offset(4,60)
		ENFG A 1 Offset(0,51)
		ENFG A 1 Offset(-4,42)
		ENFG A 2 Offset(-8,36)
		ENFG A 1 Offset(-6,35)
		ENFG A 1 Offset(-4,34)
		ENFG A 1 Offset(-2,33)
		Goto Ready
	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	}
}

ACTOR EnfieldSpawner replaces Chainsaw
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("EnfieldPickup",0,0,0,velx,vely,velz)
		TNT1 A 0 A_SpawnItemEx("MauserAmmoSmall",frandom(-16,16),frandom(-16,16),0,velx,vely,velz,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR EnfieldPickup : CustomInventory
{
	States
	{
	Spawn:
		ENFP A -1
		Stop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("WWNaziWeaponCollect",14)
		Stop
	}
}