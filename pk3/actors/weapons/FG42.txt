//Rate of fire: ~750-900 rounds/min - Wikipedia
ACTOR FG42 : NaziWeapon
{
	Weapon.SelectionOrder 700
	Inventory.PickupMessage "You got the Fallschirmjagergewehr 42!"
	Weapon.AmmoType "FG42Loaded"
	Weapon.AmmoUse 1
	weapon.ammotype2 "MauserAmmo"
	weapon.ammouse2 1
	Scale .38
	Tag "FG 42"
	Weapon.UpSound "fg42/select"
	States
	{
	Ready:
		FG42 A 0 A_JumpIfInventory("FG42Loaded",0,2)
		FG42 A 0 A_JumpIfInventory("MauserAmmo",1,2)
		FG42 A 1 A_WeaponReady
		Loop
		FG42 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		FG42 A 0 A_Lower
		FG42 A 1 A_Lower
		Loop
	Select:
		FG42 A 0 A_Raise
		FG42 A 1 A_Raise
		Loop
	Fire:
		FG42 A 0 A_JumpIfInventory("FG42Loaded",1,1)
		Goto Dryfire
		FG42 A 0 A_GunFlash
		FG42 A 0 A_PlaySound("fg42/fire",CHAN_WEAPON)
		FG42 A 0 ACS_NamedExecute("WWNaziTaunt",0,1)
		FG42 A 0 A_SpawnItemEx("MauserRifleCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		FG42 A 0 A_AlertMonsters
		FG42 A 1 A_FireCustomMissile("FGTracer",frandom(-2.5,2.5),1,0,0,0,frandom(-1.0,1.0))
		FG42 A 1 Offset(0,42) A_SetPitch(pitch-(1.8*CallACS("RecoilAmount")))
		FG42 A 1 A_Refire
		FG42 A 1 A_WeaponReady(WRF_NOBOB)
		FG42 A 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		FG42 A 1 Offset(0,43) A_WeaponReady(WRF_NOBOB)
		FG42 A 1 Offset(0,40) A_WeaponReady(WRF_NOBOB)
		FG42 A 1 Offset(0,36) A_WeaponReady(WRF_NOBOB)
		FG42 A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Hold:
		FG42 A 0 A_JumpIfInventory("FG42Loaded",1,1)
		Goto Dryfire
		FG42 A 0 A_GunFlash
		FG42 A 0 A_PlaySound("fg42/fire",CHAN_WEAPON)
		FG42 A 0 ACS_NamedExecute("WWNaziTaunt",0,1)
		FG42 A 0 A_SpawnItemEx("MauserRifleCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		FG42 A 0 A_AlertMonsters
		FG42 A 1 Offset(0,58) A_FireCustomMissile("FGTracer",frandom(-2.5,2.5),1,0,0,0,frandom(-1.0,1.0))
		FG42 A 1 Offset(0,54) A_SetPitch(pitch-(1.8*CallACS("RecoilAmount")))
		FG42 A 1 A_Refire
		FG42 A 1 Offset(0,52) A_WeaponReady(WRF_NOBOB)
		FG42 A 1 Offset(0,54) A_WeaponReady(WRF_NOBOB)
		FG42 A 1 Offset(0,53) A_WeaponReady(WRF_NOBOB)
		FG42 A 1 Offset(0,50) A_WeaponReady(WRF_NOBOB)
		FG42 A 1 Offset(0,46) A_WeaponReady(WRF_NOBOB)
		FG42 A 1 Offset(0,41) A_WeaponReady(WRF_NOBOB)
		FG42 A 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		FG42 A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Reload:
		FG42 A 1 Offset(0,35) A_PlaySound("fg42/reload",5)
		FG42 A 1 Offset(0,38) ACS_NamedExecute("WWNaziTaunt",0,2)
		FG42 A 1 Offset(0,44)
		FG42 A 1 Offset(-2,46)
		FG42 A 1 Offset(-4,52)
		FG42 A 1 Offset(-7,56)
		FG42 A 1 Offset(-10,57)
		FG42 A 8 Offset(-11,58)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("MauserAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("FG42Loaded")
		TNT1 A 0 A_JumpIfInventory("FG42Loaded",0,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("MauserAmmo",1,"ReloadLoop")
	ReloadFinish:
		FG42 A 1 Offset(-10,58)
		FG42 A 1 Offset(-10,61)
		FG42 A 1 Offset(-10,65)
		FG42 A 1 Offset(-10,71)
		FG42 A 1 Offset(-10,65)
		FG42 A 1 Offset(-10,60)
		FG42 A 1 Offset(-10,55)
		FG42 A 1 Offset(-10,53)
		FG42 A 1 Offset(-10,51)
		FG42 A 1 Offset(-9,50)
		FG42 A 1 Offset(-8,46)
		FG42 A 1 Offset(-7,43)
		FG42 A 1 Offset(-5,40)
		FG42 A 1 Offset(-3,37)
		FG42 A 1 Offset(-1,34)
		FG42 A 1 Offset(0,32)
		Goto Ready
	Flash:
		FG42 B 1 BRIGHT A_Light2
		FG42 C 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		FG42 P -1
		Stop
	}
}

ACTOR FG42Loaded : Ammo
{
	Tag "7.92x57mm"
	Inventory.MaxAmount 20
	+IGNORESKILL
	Inventory.Icon FG42P0
}

ACTOR FG42Spawner
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("FG42Pickup",0,0,0,velx,vely,velz)
		TNT1 AA 0 A_SpawnItemEx("MauserAmmo",frandom(-16,16),frandom(-16,16),0,velx,vely,velz,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR FG42Pickup : NaziWeaponPickup
{
	Scale .38
	States
	{
	Spawn:
		FG42 P 0 NODELAY A_SetUserVar("user_weapon",19)
		Goto Idle
	}
}