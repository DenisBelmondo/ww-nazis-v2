ACTOR M1911 : NaziWeapon
{
	Weapon.AmmoType "M1911Loaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "45ACPAmmo"
	Weapon.AmmoUse2 1
	Weapon.SelectionOrder 9990
	Inventory.Pickupmessage "You got the M1911! (slot 2)"
	+WEAPON.NOAUTOFIRE
	Tag "Colt M1911"
	States
	{
	Ready:
		1911 A 0 A_JumpIfInventory("M1911Loaded",0,2)
		1911 A 0 A_JumpIfInventory("45ACPAmmo",1,2)
		1911 A 1 A_WeaponReady
		Loop
		1911 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		1911 A 0 A_Lower
		1911 A 1 A_Lower
		Loop
	Select:
		1911 A 0 A_Raise
		1911 A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("M1911Loaded",1,1)
		Goto Dryfire
		1911 B 0 A_GunFlash
		1911 B 0 A_PlaySound("m1911/fire",CHAN_WEAPON)
		1911 B 0 A_SetPitch(pitch+(0.2*CallACS("RecoilAmount")))
		1911 B 0 A_AlertMonsters
		1911 B 0 A_SpawnItemEx("45ACPCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt",0,1)
		1911 B 2 A_FireCustomMissile("1911Tracer",frandom(-1.5,1.5),1,0,0,0,frandom(-0.8,0.8))
		1911 C 1 A_SetPitch(pitch-(0.2*CallACS("RecoilAmount")))
		TNT1 A 0 A_CheckReload
		1911 DEED 1 A_WeaponReady
		1911 B 1 offset(0,34)
		1911 B 1 offset(0,33)
		Goto Ready
	Flash:
		GNFL A 1 BRIGHT A_Light2
		GNFL B 1 BRIGHT
		TNT1 A 2 A_Light1
		Goto LightDone
	Reload:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt",0,2)
		1911 B 4 A_PlaySound("m1911/slide",5)
		1911 C 2
		HGRL ABCDE 2
		HGRL F 4
	ReloadLoop:
		TNT1 A 0 A_GiveInventory("M1911Loaded")
		TNT1 A 0 A_TakeInventory("45ACPAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("M1911Loaded",0,"ReloadEnd")
		TNT1 A 0 A_JumpIfInventory("45ACPAmmo",1,"ReloadLoop")
	ReloadEnd:
		HGRL G 2 A_PlaySound("m1911/reload",5)
		HGRL HI 2
		HGRL JK 3
		HGRL L 4 A_PlaySound("m1911/snap",5)
		HGRL M 3
		HGRL NNOOP 1
		Goto Ready
	Spawn:
		COLP A -1	//Really not a fan of this sprite...
		Stop
	}
}

ACTOR M1911Loaded : Ammo
{
	Tag ".45 ACP"
	+IGNORESKILL
	Inventory.MaxAmount 7
	Inventory.Icon COLPA0
}

ACTOR M1911Spawner
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("M1911Pickup",0,0,0,velx,vely,velz)
		TNT1 AA 0 A_SpawnItemEx("45ACPAmmo",frandom(-16,16),frandom(-16,16),0,velx,vely,velz,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR M1911Pickup : NaziWeaponPickup
{
	States
	{
	Spawn:
		COLP A -1
		Stop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("WWNaziWeaponCollect",6)
		Stop
	}
}