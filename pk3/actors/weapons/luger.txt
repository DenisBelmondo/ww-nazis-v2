ACTOR Luger9mm : NaziWeapon
{
	Weapon.SelectionOrder 9998
	weapon.kickback 60
	Weapon.AmmoType "Luger9mmLoaded"
	Weapon.AmmoUse1 1
	weapon.ammotype2 "MPClip"
	weapon.ammouse2 1
	Tag "Luger P08"
	Inventory.PickupMessage "You got the Luger P08! (slot 2)"
	//Obituary "%o was killed by %k."
	+Weapon.NoAutoFire
	States
	{
	Ready:
		LUGG A 0 A_JumpIfInventory("Luger9mmLoaded",0,2)
		LUGG A 0 A_JumpIfInventory("MPClip",1,2)
		LUGG A 1 A_WeaponReady
		Loop
		LUGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		LUGG A 0 A_Lower
		LUGG A 1 A_Lower
		Loop
	Select:
		LUGG A 0 A_Raise
		LUGG A 1 A_Raise
		Loop
	Fire:
		LUGG A 0 A_JumpIfInventory("Luger9mmLoaded",1,1)
		Goto Reload
		LUGF A 0 A_GunFlash
		LUGF A 0 A_SetPitch(pitch - 0.2)
		LUGF A 0 A_PlaySound("weapons/Luger",CHAN_WEAPON)
		LUGF A 0 A_SpawnItemEx("9MMCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		LUGF A 1 BRIGHT A_FireCustomMissile("LugerTracer",frandom(-1,1))
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt",0,1)
		LUGG B 1 A_AlertMonsters
		LUGG B 1 Offset(0,36) A_SetPitch(pitch - 0.2)
		LUGG B 1 Offset(0,41) A_CheckReload
		LUGG C 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		LUGG C 1 Offset(0,33) A_WeaponReady(WRF_NOBOB)
		LUGG C 1 Offset(0,31) A_WeaponReady(WRF_NOBOB)
		LUGG A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Reload:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt",0,2)
		LUGG A 1 Offset(0,35) A_PlaySound("reloads/luger",5)
		LUGG A 1 Offset(0,38)
		LUGG A 1 Offset(0,44)
		LUGG A 1 Offset(0,52)
		LUGG C 1 Offset(0,62)
		LUGG C 1 Offset(0,72)
		LUGG C 1 Offset(0,82)
		TNT1 A 11
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("MPClip",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("Luger9mmLoaded")
		TNT1 A 0 A_JumpIfInventory("Luger9mmLoaded",8,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("MPClip",1,"ReloadLoop")
	ReloadFinish:
		LUGG B 1 Offset(0,82)
		LUGG B 1 Offset(0,72)
		LUGG B 1 Offset(0,62)
		LUGG C 1 Offset(0,52)
		LUGG C 1 Offset(0,44)
		LUGG C 1 Offset(0,38)
		LUGG A 1 Offset(0,35)
		LUGG A 1 Offset(0,32)
		Goto Ready
	Flash:
		TNT1 A 3 A_Light2
		TNT1 A 3 A_Light1
		Goto LightDone
	Spawn:
		LUGP A -1
		Stop
	}
}

ACTOR Luger9mmLoaded : Ammo
{
	Tag "9x19mm"
	+IGNORESKILL
	Inventory.MaxAmount 8
	Inventory.Icon LUGPA0
}

ACTOR Luger9mmSpawner
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("Luger9mmPickup",0,0,0,velx,vely,velz)
		TNT1 A 0 A_SpawnItemEx("MPMagazine",frandom(-16,16),frandom(-16,16),0,velx,vely,velz,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR Luger9mmPickup : NaziWeaponPickup
{
	States
	{
	Spawn:
		LUGP A -1
		Stop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("WWNaziWeaponCollect",2)
		Stop
	}
}