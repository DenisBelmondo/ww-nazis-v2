ACTOR MP40Seq : Inventory{}

ACTOR MP40 : NaziWeapon
{
	Scale 0.5
	Weapon.SelectionOrder 600
	weapon.kickback 60
	Weapon.AmmoType "MP40Loaded"
	Weapon.AmmoUse1 1
	weapon.ammotype2 "MPClip"
	weapon.ammouse2 1
	Tag "MP 40"
	Inventory.PickupMessage "You got the MP 40! (slot 4)"
	//Obituary "%o was killed by %k."
	ConversationID 188
	States
	{
	Ready:
		RIFG A 0 A_JumpIfInventory("MP40Loaded",0,2)
		RIFG A 0 A_JumpIfInventory("MPClip",1,2)
		RIFG A 1 A_WeaponReady
		Loop
		RIFG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		RIFG A 0 A_Lower
		RIFG A 1 A_Lower
		Loop
	Select:
		RIFG A 0 A_Raise
		RIFG A 1 A_Raise
		Loop
	Fire:
		RIFF A 0 A_JumpIfInventory("MP40Loaded",1,1)
		Goto Reload
		RIFF A 0 A_GunFlash
		RIFF A 0 A_SetPitch(pitch - 0.5)
		RIFF A 0 A_PlaySound("weapons/mp40",CHAN_WEAPON)
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1)
		RIFF A 0 A_SpawnItemEx("9MMCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		RIFF A 1 BRIGHT A_FireCustomMissile("SMGTracer",frandom(-1.4,1.4))
		RIFF B 1 Offset(0,38) A_AlertMonsters
		RIFF B 1 Offset(0,44)
		RIFF B 1 Offset(0,40)
		RIFF A 0 A_JumpIfInventory("MP40Seq",1,"Fire2")
		TNT1 A 0 A_GiveInventory("MP40Seq")
		TNT1 A 0 A_CheckReload
		Goto Ready
	Fire2:
		RIFG A 1 Offset(0,36) A_TakeInventory("MP40Seq",1)
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 2)
		RIFG A 1 Offset(0,35) A_PlaySound("reloads/mp40",5)
		RIFG A 1 Offset(0,38)
		RIFG A 1 Offset(0,44)
		RIFF B 1 Offset(0,52)
		RIFF B 1 Offset(-1,54)
		RIFF B 1 Offset(-2,56)
		RIFF B 1 Offset(-3,58)
		RIFF B 1 Offset(-4,58)
		RIFF B 1 Offset(-4,57)
		RIFF B 1 Offset(-3,54)
		RIFF B 1 Offset(-3,51)
		RIFF B 1 Offset(-3,53)
		RIFF B 1 Offset(-3,55)
		RIFF B 1 Offset(-3,56)
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("MPClip",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("MP40Loaded")
		TNT1 A 0 A_JumpIfInventory("MP40Loaded",32,"ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("MPClip",1,"ReloadLoop")
	ReloadFinish:
		RIFF B 1 Offset(-3,57)
		RIFF B 1 Offset(-3,59)
		RIFF B 1 Offset(-3,63)
		RIFF B 1 Offset(-3,67)
		RIFF B 1 Offset(-3,65)
		RIFF B 1 Offset(-3,62)
		RIFF B 1 Offset(-3,58)
		RIFF B 1 Offset(-3,55)
		RIFF B 1 Offset(-2,53)
		RIFF B 1 Offset(-2,49)
		RIFF B 1 Offset(-2,46)
		RIFF B 1 Offset(-2,45)
		RIFF B 1 Offset(-1,44)
		RIFF B 1 Offset(-1,46)
		RIFF B 1 Offset(-1,47)
		RIFF B 1 Offset(0,49) A_WeaponReady(WRF_NOBOB)
		RIFG A 1 Offset(0,44) A_WeaponReady(WRF_NOBOB)
		RIFG A 1 Offset(0,38) A_WeaponReady(WRF_NOBOB)
		RIFG A 1 Offset(0,35) A_WeaponReady(WRF_NOBOB)
		RIFG A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB)
		Goto Ready
	Flash:
		TNT1 A 3 A_Light2
		TNT1 A 3 A_Light1
		Goto LightDone
	Spawn:
		MP40 A -1
		Stop
	}
}

ACTOR MPClip : Ammo replaces ClipBox
{
	Tag "9x19mm"
	Inventory.Amount 64
	Inventory.MaxAmount 256
	Ammo.BackpackAmount 64
	Ammo.BackpackMaxAmount 480
	Inventory.PickupMessage "Picked up a box of 9mm ammo."
	States
	{
	Spawn:
		MCLP A -1
		Stop
	}
}

ACTOR MPMagazine : MPClip
{
	Inventory.Amount 8
	Inventory.PickupMessage "Picked up a 9mm magazine."
	States
	{
	Spawn:
		CLIP A -1 //Placeholder sprite
		Stop
	}
}

ACTOR MP40Loaded : Ammo
{
	Tag "9x19mm"
	+IGNORESKILL
	Inventory.MaxAmount 32
	Inventory.Icon MP40A0
}

ACTOR MP40Spawner
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("MP40Pickup",0,0,0,velx,vely,velz)
		TNT1 AA 0 A_SpawnItemEx("MPMagazine",frandom(-16,16),frandom(-16,16),0,velx,vely,velz,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR MP40Pickup : CustomInventory
{
	Scale .5
	States
	{
	Spawn:
		MP40 A -1
		Stop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("WWNaziWeaponCollect",8)
		Stop
	}
}