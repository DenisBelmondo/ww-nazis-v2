ACTOR Garand : NaziWeapon
{
	Weapon.AmmoType "GarandLoaded"
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "3006Ammo"
	Weapon.AmmoUse2 1
	Inventory.PickupMessage "You got the M1 Garand! (slot 5)"
	Weapon.SelectionOrder 750
	+WEAPON.NOAUTOFIRE
	Tag "M1 Garand"
	States
	{
	Ready:
		M1A1 A 0 A_JumpIfInventory("GarandLoaded",0,2)
		M1A1 A 0 A_JumpIfInventory("3006Ammo",1,2)
		M1A1 A 1 A_WeaponReady
		Loop
		M1A1 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Select:
		M1A1 A 0 A_Raise
		M1A1 A 1 A_Raise
		Loop
	Deselect:
		M1A1 A 0 A_Lower
		M1A1 A 1 A_Lower
		Loop
	Fire:
		M1A1 A 0 A_JumpIfInventory("GarandLoaded",1,1)
		Goto Dryfire
		M1A1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("garand/fire",CHAN_WEAPON)
		M1A1 A 0 A_SpawnItemEx("GarandRifleCasing",12,-20,32,8,random(-2,2),random(0,4),random(-55,-80),SXF_NOCHECKPOSITION)
		M1A1 A 0 A_AlertMonsters
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt",0,1)
		M1A1 A 1 A_FireCustomMissile("GarandTracer")
		M1A1 C 1 A_SetPitch(pitch-(3.6*CallACS("RecoilAmount")))
		M1A1 A 1 A_JumpIfInventory("GarandLoaded",1,3)
		M1A1 C 0 A_PlaySound("garand/ping",5)
		M1A1 C 0 A_GiveInventory("M1Pinged")
	FireFinish:
		M1A1 FEDCBAGA 1
		TNT1 A 0 A_CheckReload
		Goto Ready
	Reload:
		M1A1 A 0 ACS_NamedExecute("WWNaziTaunt",0,2)
		M1A1 A 5 A_JumpIfInventory("M1Pinged",1,"AfterUnload")
		M1A1 A 1 Offset(1,34)
		M1A1 A 1 Offset(2,36)
		M1A1 A 1 Offset(4,38)
		M1A1 A 1 Offset(6,42)
		M1A1 A 1 Offset(9,46)
		M1A1 A 1 Offset(12,50)
		M1A1 A 1 Offset(15,54)
		M1A1 A 1 Offset(19,59)
		M1A1 A 1 Offset(23,65) A_PlaySound("garand/unload",6)
		M1A1 B 1 Offset(28,72)
		M1A1 B 1 Offset(30,84)
		M1A1 C 1 Offset(31,89)
		M1A1 C 1 Offset(31,92)
		M1A1 D 1 Offset(31,94)
		M1A1 D 1 Offset(31,93)
		M1A1 E 1 Offset(31,91)
		M1A1 E 1 Offset(31,88)
		M1A1 F 1 Offset(31,84)
		M1A1 F 1 Offset(31,80)
		M1A1 F 1 Offset(33,72)
		M1A1 F 1 Offset(34,68)
		M1A1 F 1 Offset(33,62)
		M1A1 E 1 Offset(31,56)
		M1A1 E 1 Offset(29,55)
		M1A1 D 1 Offset(27,53)
		M1A1 D 1 Offset(24,51)
		M1A1 C 1 Offset(21,49)
		M1A1 C 1 Offset(18,46)
		M1A1 B 1 Offset(16,43)
		M1A1 B 1 Offset(14,41)
		M1A1 B 1 Offset(12,39)
		M1A1 B 1 Offset(10,38)
		M1A1 A 1 Offset(8,37)
		M1A1 A 1 Offset(7,36)
		M1A1 G 1 Offset(6,35)
		M1A1 G 1 Offset(5,34)
		M1A1 A 1 Offset(4,33)
		M1AR A 1 Offset(3,32)
		M1AR B 1 Offset(2,32)
	AfterUnload:
		M1AR BBBBBBBBBBB 1
		M1AR C 1 A_PlaySound("garand/load",5)
		M1AR CDDEFGHH 1
		M1AR IJK 2
	ReloadLoop:
		M1AR A 0 A_TakeInventory("3006Ammo",1,TIF_NOTAKEINFINITE)
		M1AR A 0 A_GiveInventory("GarandLoaded")
		M1AR A 0 A_JumpIfInventory("GarandLoaded",0,"ReloadFinish")
		M1AR A 0 A_JumpIfInventory("3006Ammo",1,"ReloadLoop")
	ReloadFinish:
		M1AR LMNOOPPBBBBBA 1
		M1AR A 1 A_TakeInventory("M1Pinged")
		M1AR A 1 A_Refire
		Goto Ready
	Flash:
		M1AF A 1 BRIGHT A_Light2
		TNT1 A 1
		TNT1 A 2 A_Light1
		Goto LightDone
	Spawn:
		M1AP A -1
		Stop
	}
}

ACTOR GarandLoaded : Ammo
{
	Tag ".30-06"
	+IGNORESKILL
	Inventory.MaxAmount 8
	Inventory.Icon M1APA0
}

ACTOR GarandSpawner
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("GarandPickup",0,0,0,velx,vely,velz)
		TNT1 A 0 A_SpawnItemEx("3006AmmoBox",frandom(-16,16),frandom(-16,16),0,velx,vely,velz,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR GarandPickup : NaziWeaponPickup
{
	States
	{
	Spawn:
		M1AP A -1
		Stop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("WWNaziWeaponCollect",16)
		Stop
	}
}

ACTOR M1Pinged : Inventory{} // Tracks state of weapon emptiness.