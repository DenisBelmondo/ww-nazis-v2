class TrenchShotgun : NaziWeapon
{
	default
	{
		Scale .5;
		Tag "M1897 Trenchgun";
		Inventory.PickupMessage "You got the Winchester Trenchgun!";
		Weapon.SelectionOrder 400;
		Weapon.AmmoType "TrenchShotgunLoaded";
		Weapon.AmmoUse 1;
		Weapon.AmmoType2 "TwelveGaugeAmmo";
		Weapon.AmmoUse2 1;
		Weapon.UpSound "shotgun/select";

		+WEAPON.NOAUTOFIRE;
	}

	states
	{
	Select:
		SHTG A 0 A_Raise();
		SHTG A 1 A_Raise();
		loop;
	Deselect:
		SHTG A 0 A_JumpIfInventory("ShotgunReloading", 1, 3);
		SHTG A 0 A_Lower();
		SHTG A 1 A_Lower();
		loop;
		SHTG C 1 Offset(3, 35) A_StartSound("shotgun/pump");
		SHTG C 1 Offset(-1, 38);
		SHTG D 1 Offset(-4, 41);
		SHTG D 1 Offset(-1, 33);
		SHTG E 1 Offset(-2, 37);
		SHTG E 1 Offset(-3, 43);
		SHTG D 1 Offset(-2, 40);
		SHTG D 1 Offset(-4, 41);
		SHTG C 1 Offset(-1, 38);
		SHTG C 1 Offset(3, 35);
		SHTG B 1 Offset(-1, 38);
		SHTG B 1 Offset(2, 36);
		SHTG B 1 Offset(5, 34) A_TakeInventory("ShotgunReloading", 1);
		loop;
	Ready:
		SHTG A 0 A_JumpIfInventory("TrenchShotgunLoaded", 0, 2);
		SHTG A 0 A_JumpIfInventory("TwelveGaugeAmmo", 1, 2);
		SHTG A 1 A_WeaponReady();
		loop;
		SHTG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		loop;
	Fire:
		SHTG A 0 A_JumpIfInventory("ShotgunReloading", 1, "ReloadFinish");
		SHTG A 0 A_JumpIfInventory("TrenchShotgunLoaded", 1, 1);
		goto Dryfire;
		SHTG A 0 A_TakeInventory("TrenchShotgunLoaded", 1);
		SHTG A 0 A_StartSound("shotgun/fire", CHAN_WEAPON);
		SHTG A 0 A_AlertMonsters();
		SHTG AAAAAAAAAA 0 A_FireProjectile("ShotgunTracer", frandom(-2.0, 2.0), 0, 0, 0, 0, frandom(-1.5, 1.5));
		SHTG F 1 A_GunFlash();
		SHTG F 1 A_SetPitch(pitch - (4.0 * CallACS("RecoilAmount")));
		SHTG F 1 Offset(0, 40) A_SetPitch(pitch - (2.0 * CallACS("RecoilAmount")));
		SHTG F 1 Offset(0, 36) A_SetPitch(pitch - (1.0 * CallACS("RecoilAmount")));
		SHTG F 1 Offset(0, 34) A_SetPitch(pitch + (1.0 * CallACS("RecoilAmount")));
		SHTG F 1 Offset(0, 33) A_SetPitch(pitch + (0.5 * CallACS("RecoilAmount")));
		SHTG F 7 A_WeaponReady(WRF_NOFIRE);
		SHTG F 1 Offset(-1, 33);
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1);
		SHTG B 1 Offset(5, 34) A_StartSound("shotgun/pump", 5);
		SHTG B 1 Offset(2, 36);
		SHTG B 1 Offset(-1, 38);
		SHTG C 1 Offset(3, 35);
		SHTG C 1 Offset(-1, 38);
		SHTG D 1 Offset(-4, 41);
		SHTG D 1 Offset(-4, 43) A_SpawnItemEx("ShotgunCasing", 12, -20, 32, 8, random(-2, 2), random(0, 4), random(-55, -80), SXF_NOCHECKPOSITION);
		SHTG E 1 Offset(-4, 45);
		SHTG E 1 Offset(-3, 44);
		SHTG D 1 Offset(-2, 40);
		SHTG D 1 Offset(-4, 41);
		SHTG C 1 Offset(-1, 38);
		SHTG C 1 Offset(-1, 37);
		SHTG B 1 Offset(1, 36);
		SHTG B 1 Offset(2, 36);
		SHTG BA 1 Offset(5, 34);
		TNT1 A 0 A_CheckReload();
		goto Ready;
	Reload:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 2);
		SHTG A 1 Offset(-1, 33) A_GiveInventory("ShotgunReloading");
		SHTG B 1 Offset(5, 34);
		SHTG B 1 Offset(2, 36);
		SHTG B 1 Offset(-1, 38);
		SHTG C 1 Offset(3, 35);
		SHTG C 1 Offset(-1, 38);
		SHTG C 1 Offset(-4, 41);
		SHTG C 1 Offset(-6, 46);
		SHTG C 1 Offset(-7, 55);
	ReloadLoop:
		SHTG A 0 A_TakeInventory("TwelveGaugeAmmo", 1, TIF_NOTAKEINFINITE);
		SHTG A 0 A_GiveInventory("TrenchShotgunLoaded");
		SHTG C 1 Offset(-8, 58) A_StartSound("shotgun/load", CHAN_WEAPON);
		SHTG C 1 Offset(-9, 64);
		SHTG C 1 Offset(-10, 70);
		SHTG C 1 Offset(-10, 68);
		SHTG C 1 Offset(-9, 66);
		SHTG C 1 Offset(-9, 64);
		SHTG C 1 Offset(-9, 62);
		SHTG C 1 Offset(-8, 61) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY);
		SHTG C 1 Offset(-8, 60) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY);
		SHTG C 2 Offset(-8, 59) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY);
		SHTG C 3 Offset(-8, 58) A_WeaponReady(WRF_NOBOB | WRF_NOSECONDARY);
		SHTG A 0 A_JumpIfInventory("TrenchShotgunLoaded", 0, "ReloadFinish");
		TNT1 A 0 A_JumpIfInventory("TwelveGaugeAmmo", 1, "ReloadLoop");
	ReloadFinish:
		SHTG C 1 Offset(3, 35) A_StartSound("shotgun/pump");
		SHTG C 1 Offset(-1, 38);
		SHTG D 1 Offset(-4, 41);
		SHTG D 1 Offset(-1, 33);
		SHTG E 1 Offset(-2, 37);
		SHTG E 1 Offset(-3, 43);
		SHTG D 1 Offset(-2, 40);
		SHTG D 1 Offset(-4, 41);
		SHTG C 1 Offset(-1, 38);
		SHTG C 1 Offset(3, 35);
		SHTG B 1 Offset(-1, 38);
		SHTG B 1 Offset(2, 36);
		SHTG B 1 Offset(5, 34) A_TakeInventory("ShotgunReloading", 1);
		goto Ready;
	Flash:
		SHTF A 1 Bright A_Light(2);
		TNT1 A 1;
		TNT1 A 2 A_Light(1);
		goto LightDone;
	Spawn:
		SHOT A -1;
		stop;
	}
}

class TrenchShotgunLoaded : Ammo
{
	default
	{
		Tag "12 Gauge";
		Inventory.MaxAmount 8;
		Inventory.Icon "SHOTA0";

		+INVENTORY.IGNORESKILL;
	}
}

class TrenchShotgunSpawner : Actor
{
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("TrenchShotgunPickup", 0, 0, 0, vel.x, vel.y, vel.z);
		TNT1 AA 0 A_SpawnItemEx("TwelveGaugeAmmo", frandom(-16, 16), frandom(-16, 16), 0, vel.x, vel.y, vel.z, 0, SXF_NOCHECKPOSITION);
		stop;
	}
}

class TrenchShotgunPickup : NaziWeaponPickup
{
	default
	{
		Scale .5;
	}

	states
	{
	Spawn:
		SHOT A 0 NoDelay A_SetUserVar("user_weapon", 8);
		goto Idle;
	}
}

class ShotgunReloading : Inventory {}
