class Luger9mm : NaziWeapon
{
	default
	{
		Tag "Luger P08";
		Weapon.SelectionOrder 9998;
		Weapon.AmmoType "Luger9mmLoaded";
		Weapon.AmmoUse 1;
		Weapon.AmmoType2 "ParabellumAmmo";
		Weapon.AmmoUse2 1;
		Inventory.PickupMessage "You got the Luger P08!";
		Weapon.UpSound "luger/select";

		+WEAPON.NOAUTOFIRE;
	}

	states
	{
	Ready:
		LUGG A 0 A_JumpIfInventory("Luger9mmLoaded", 0, 2);
		LUGG A 0 A_JumpIfInventory("ParabellumAmmo", 1, 2);
		LUGG A 1 A_WeaponReady();
		loop;
		LUGG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		loop;
	Deselect:
		LUGG A 0 A_Lower();
		LUGG A 1 A_Lower();
		loop;
	Select:
		LUGG A 0 A_Raise();
		LUGG A 1 A_Raise();
		loop;
	Fire:
		LUGG A 0 A_JumpIfInventory("Luger9mmLoaded", 1, 1);
		goto Dryfire;
		LUGG A 0 A_GunFlash();
		LUGG A 0 A_SetPitch(pitch - (0.2 * CallACS("RecoilAmount")));
		LUGG A 0 A_StartSound("luger/fire", CHAN_WEAPON);
		LUGG A 0 A_SpawnItemEx("ParabellumCasing", 12, -20, 32, 8, random(-2, 2), random(0, 4), random(-55, -80), SXF_NOCHECKPOSITION);
		LUGG A 0 A_AlertMonsters();
		LUGG D 1 A_FireProjectile("LugerTracer", frandom(-1, 1));
		LUGG B 1 ACS_NamedExecute("WWNaziTaunt", 0, 1);
		LUGG B 1 Offset(0, 36) A_SetPitch(pitch - (0.2 * CallACS("RecoilAmount")));
		LUGG B 1 Offset(0, 41) A_CheckReload();
		LUGG C 1 Offset(0, 35) A_WeaponReady(WRF_NOBOB);
		LUGG C 1 Offset(0, 33) A_WeaponReady(WRF_NOBOB);
		LUGG C 1 Offset(0, 31) A_WeaponReady(WRF_NOBOB);
		LUGG A 1 Offset(0, 32) A_WeaponReady(WRF_NOBOB);
		goto Ready;
	Reload:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 2);
		LUGG A 1 Offset(0, 35) A_StartSound("luger/reload", 5);
		LUGG A 1 Offset(0, 38);
		LUGG A 1 Offset(0, 44);
		LUGG A 1 Offset(0, 52);
		LUGG C 1 Offset(0, 62);
		LUGG C 1 Offset(0, 72);
		LUGG C 1 Offset(0, 82);
		TNT1 A 11;
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("ParabellumAmmo", 1, TIF_NOTAKEINFINITE);
		TNT1 A 0 A_GiveInventory("Luger9mmLoaded");
		TNT1 A 0 A_JumpIfInventory("Luger9mmLoaded", 0, "ReloadFinish");
		TNT1 A 0 A_JumpIfInventory("ParabellumAmmo", 1, "ReloadLoop");
	ReloadFinish:
		LUGG B 1 Offset(0, 82);
		LUGG B 1 Offset(0, 72);
		LUGG B 1 Offset(0, 62);
		LUGG C 1 Offset(0, 52);
		LUGG C 1 Offset(0, 44);
		LUGG C 1 Offset(0, 38);
		LUGG A 1 Offset(0, 35);
		LUGG A 1 Offset(0, 32);
		goto Ready;
	Flash:
		LUGF A 1 Bright A_Light(2);
		TNT1 A 1;
		TNT1 A 2 A_Light(1);
		goto LightDone;
	Spawn:
		LUGP A -1;
		stop;
	}
}

class Luger9mmLoaded : Ammo
{
	default
	{
		Tag "9x19mm";
		Inventory.MaxAmount 8;
		Inventory.Icon "LUGPA0";

		+INVENTORY.IGNORESKILL;
	}
}

class Luger9mmSpawner : Actor
{
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("Luger9mmPickup", 0, 0, 0, vel.x, vel.y, vel.z);
		TNT1 A 0 A_SpawnItemEx("ParabellumAmmo", frandom(-16, 16), frandom(-16, 16), 0, vel.x, vel.y, vel.z, 0, SXF_NOCHECKPOSITION);
		stop;
	}
}

class Luger9mmPickup : NaziWeaponPickup
{
	states
	{
	Spawn:
		LUGP A 0 NoDelay A_SetUserVar("user_weapon", 4);
		goto Idle;
	}
}
