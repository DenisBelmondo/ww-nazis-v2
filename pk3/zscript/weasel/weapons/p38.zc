class Walther9mm : NaziWeapon
{
	default
	{
		Tag "Walther P38";
		Inventory.PickupMessage "You got the Walther P38!";
		Weapon.SelectionOrder 9997;
		Weapon.AmmoType "Walther9mmLoaded";
		Weapon.AmmoUse 1;
		Weapon.ammotype2 "ParabellumAmmo";
		Weapon.ammouse2 1;
		Weapon.UpSound "walther/select";
	}

	states
	{
	Ready:
		WALG A 0 A_JumpIfInventory("Walther9mmLoaded", 0, 2);
		WALG A 0 A_JumpIfInventory("ParabellumAmmo", 1, 2);
		WALG A 1 A_WeaponReady();
		loop;
		WALG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		loop;
	Deselect:
		WALG B 0 A_Lower();
		WALG B 1 A_Lower();
		loop;
	Select:
		WALG A 0 A_Raise();
		WALG A 1 A_Raise();
		loop;
	Fire:
		WALG A 0 A_JumpIfInventory("Walther9mmLoaded", 1, 1);
		goto Dryfire;
		WALG A 0 A_GunFlash();
		WALG A 0 A_SetPitch(pitch - (0.2 * CallACS("RecoilAmount")));
		WALG A 0 A_StartSound("walther/fire", CHAN_WEAPON);
		WALG A 0 A_SpawnItemEx("ParabellumCasing", 12, -20, 32, 8, random(-2, 2), random(0, 4), random(-55, -80), SXF_NOCHECKPOSITION);
		WALG A 0 A_AlertMonsters();
		WALG A 1 A_FireProjectile("WaltherTracer", frandom(-1,1));
		WALG B 2;
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1);
		WALG A 1 A_SetPitch(pitch - (0.2 * CallACS("RecoilAmount")));
		WALG A 1 Offset(0, 36);
		WALG A 1 Offset(0, 41);
		WALG A 1 Offset(0, 35);
		TNT1 A 0 A_CheckReload();
		WALG A 1 Offset(0, 32) A_Jump(128, "Ready");
		goto Ready;
	Reload:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 2);
		WALG A 1 Offset(0, 35) A_StartSound("walther/reload", 5);
		WALG A 1 Offset(0, 38);
		WALG A 1 Offset(0, 44);
		WALG A 1 Offset(0, 52);
		WALG B 1 Offset(0, 62);
		WALG B 1 Offset(0, 72);
		WALG B 1 Offset(0, 82);
		TNT1 A 8;
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("ParabellumAmmo", 1, TIF_NOTAKEINFINITE);
		TNT1 A 0 A_GiveInventory("Walther9mmLoaded");
		TNT1 A 0 A_JumpIfInventory("Walther9mmLoaded", 0, "ReloadFinish");
		TNT1 A 0 A_JumpIfInventory("ParabellumAmmo", 1, "ReloadLoop");
	ReloadFinish:
		WALG B 1 Offset(0, 82);
		WALG B 1 Offset(0, 72);
		WALG B 1 Offset(0, 62);
		WALG B 1 Offset(0, 52);
		WALG A 1 Offset(0, 44);
		WALG A 1 Offset(0, 38);
		WALG A 1 Offset(0, 35);
		WALG A 1 Offset(0, 32);
		goto Ready;
	Flash:
		WALF A 1 Bright A_Light(2);
		TNT1 A 1;
		TNT1 A 2 A_Light(1);
		goto LightDone;
	Spawn:
		WALP A -1;
		stop;
	}
}

class Walther9mmLoaded : Ammo
{
	default
	{
		Tag "9x19mm";
		Inventory.MaxAmount 8;
		Inventory.Icon "WALPA0";

		+INVENTORY.IGNORESKILL;
	}
}

class Walther9mmSpawner : Actor
{
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("Walther9mmPickup", 0, 0, 0, vel.x, vel.y, vel.z);
		TNT1 A 0 A_SpawnItemEx("ParabellumAmmo", frandom(-16, 16), frandom(-16, 16), 0, vel.x, vel.y, vel.z, 0, SXF_NOCHECKPOSITION);
		stop;
	}
}

class Walther9mmPickup : NaziWeaponPickup
{
	states
	{
	Spawn:
		WALP A 0 NoDelay A_SetUserVar("user_weapon", 5);
		goto Idle;
	}
}
