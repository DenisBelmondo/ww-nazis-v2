class KnuckleDusters : NaziWeapon
{
	default
	{
		Scale .1;
		Tag "Knuckle Dusters";
		Inventory.PickupMessage "You got the knuckle dusters!";
		Weapon.SelectionOrder 10000;

		-WEAPON.AMMO_CHECKBOTH;
	}

	states
	{
	Ready:
		PUNG A 1 A_WeaponReady();
		loop;
	Deselect:
		PUNG A 0 A_Lower();
		PUNG A 1 A_Lower();
		loop;
	Select:
		PUNG A 0 A_Raise();
		PUNG A 1 A_Raise();
		loop;
	Fire:
		TNT1 A 0 A_Jump(128, "Fire2");
		PUNG B 1 ACS_NamedExecute("WWNaziTaunt", 0, 1);
		PUNG C 1;
		TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, 3);
		PUNG D 4 A_CustomPunch(3, 0, 0, "HitPuff");
		TNT1 A 0 A_Jump(256, 2);
		PUNG D 4 A_CustomPunch(3 * 10, 0, 0, "HitPuff");
		PUNG CB 2;
		TNT1 A 5;
		goto Ready;
	Fire2:
		PUNG FG 1;
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1);
		TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, 3);
		PUNG H 4 A_CustomPunch(4, 0, 0, "HitPuff");
		TNT1 A 0 A_Jump(256, 2);
		PUNG H 4 A_CustomPunch(4 * 10, 0, 0, "HitPuff");
		PUNG GF 2;
		TNT1 A 5;
		goto Ready;
	Spawn:
		PUNG Z -1;
		stop;
	}
}

class KnuckleDustersPickup : NaziWeaponPickup
{
	default
	{
		Scale 0.1;
	}

	states
	{
	Spawn:
		PUNG Z 0 NoDelay A_SetUserVar("user_weapon", 2);
		goto Idle;
	}
}

class HitPuff : Actor
{
	default
	{
		ActiveSound "melee/miss";
		AttackSound "melee/wall";
		SeeSound "melee/hit";
		Obituary "%o was dusted by %k.";
		ProjectileKickback 40;

		+NOBLOCKMAP;
		+NOGRAVITY;
		+PUFFONACTORS;
		+NOEXTREMEDEATH;
	}

	states
	{
	Spawn:
		TNT1 A 1;
		stop;
	}
}
