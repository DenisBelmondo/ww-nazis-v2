class M1911 : NaziWeapon
{
	default
	{
		Tag "Colt M1911";
		Inventory.Pickupmessage "You got the M1911!";
		Weapon.AmmoType "M1911Loaded";
		Weapon.AmmoUse 1;
		Weapon.AmmoType2 "Forty5ACPAmmo";
		Weapon.AmmoUse2 1;
		Weapon.SelectionOrder 9990;
		Weapon.UpSound "m1911/select";

		+WEAPON.NOAUTOFIRE;
	}

	states
	{
	Ready:
		1911 A 0 A_JumpIfInventory("M1911Loaded", 0, 2);
		1911 A 0 A_JumpIfInventory("Forty5ACPAmmo", 1, 2);
		1911 A 1 A_WeaponReady();
		loop;
		1911 A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		loop;
	Deselect:
		1911 A 0 A_Lower();
		1911 A 1 A_Lower();
		loop;
	Select:
		1911 A 0 A_Raise();
		1911 A 1 A_Raise();
		loop;
	Fire:
		1911 A 0 A_JumpIfInventory("M1911Loaded", 1, 1);
		goto Dryfire;
		1911 B 0 A_GunFlash();
		1911 B 0 A_StartSound("m1911/fire", CHAN_WEAPON);
		1911 B 0 A_SetPitch(pitch+(0.2 * CallACS("RecoilAmount")));
		1911 B 0 A_AlertMonsters();
		1911 B 0 A_SpawnItemEx("Forty5ACPCasing", 12, -20, 32, 8, random(-2, 2), random(0, 4), random(-55, -80), SXF_NOCHECKPOSITION);
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1);
		1911 B 2 A_FireProjectile("M1911Tracer", frandom(-1.5, 1.5), 1, 0, 0, 0, frandom(-0.8, 0.8));
		1911 C 1 A_SetPitch(pitch - (0.2 * CallACS("RecoilAmount")));
		TNT1 A 0 A_CheckReload();
		1911 DEED 1 A_WeaponReady();
		1911 B 1 offset(0, 34);
		1911 B 1 offset(0, 33);
		goto Ready;
	Flash:
		GNFL A 1 Bright A_Light(2);
		GNFL B 1 Bright;
		TNT1 A 2 A_Light(1);
		goto LightDone;
	Reload:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 2);
		1911 B 4 A_StartSound("m1911/slide", 5);
		1911 C 2;
		HGRL ABCDE 2;
		HGRL F 4;
	ReloadLoop:
		TNT1 A 0 A_GiveInventory("M1911Loaded");
		TNT1 A 0 A_TakeInventory("Forty5ACPAmmo", 1, TIF_NOTAKEINFINITE);
		TNT1 A 0 A_JumpIfInventory("M1911Loaded", 0, "ReloadEnd");
		TNT1 A 0 A_JumpIfInventory("Forty5ACPAmmo", 1, "ReloadLoop");
	ReloadEnd:
		HGRL G 2 A_StartSound("m1911/reload", 5);
		HGRL HI 2;
		HGRL JK 3;
		HGRL L 4 A_StartSound("m1911/snap", 5);
		HGRL M 3;
		HGRL NNOOP 1;
		goto Ready;
	Spawn:
		1911 Z -1;	// Really not a fan of this sprite...
		stop;
	}
}

class M1911Loaded : Ammo
{
	default
	{
		Tag ".45 ACP";
		Inventory.MaxAmount 7;
		Inventory.Icon "1911Z0";

		+INVENTORY.IGNORESKILL;
	}
}

class M1911Spawner : Actor
{
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("M1911Pickup", 0, 0, 0, vel.x, vel.y, vel.z);
		TNT1 AA 0 A_SpawnItemEx("Forty5ACPAmmo", frandom(-16, 16), frandom(-16, 16), 0, vel.x, vel.y, vel.z, 0, SXF_NOCHECKPOSITION);
		stop;
	}
}

class M1911Pickup : NaziWeaponPickup
{
	states
	{
	Spawn:
		1911 Z 0 NoDelay A_SetUserVar("user_weapon", 7);
		goto Idle;
	}
}
