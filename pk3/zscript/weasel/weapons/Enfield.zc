// Enfield bolt-action rifle.
// Sprite from Day of Defeat model, sounds from MOHAA Breakthrough and NOLF.
// Pickup sprite by WildWeasel.

class Enfield : NaziWeapon
{
	default
	{
		Tag "Lee-Enfield SMLE";
		Inventory.PickupMessage "You got the Lee-Enfield SMLE!";
		Weapon.SelectionOrder 750;
		Weapon.AmmoType "EnfieldLoaded";
		Weapon.AmmoUse 1;
		Weapon.AmmoType2 "Three03Ammo";
		Weapon.AmmoUse2 1;
		Weapon.UpSound "enfield/select";

		+WEAPON.NOAUTOFIRE;
	}

	states
	{
	Select:
		ENFG A 0 A_Raise();
		ENFG A 1 A_Raise();
		loop;
	Deselect:
		ENFG A 0 A_Lower();
		ENFG A 1 A_Lower();
		loop;
	Ready:
		ENFG A 0 A_JumpIfInventory("EnfieldLoaded", 0, 2);
		ENFG A 0 A_JumpIfInventory("Three03Ammo", 1, 2);
		ENFG A 1 A_WeaponReady();
		loop;
		ENFG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		loop;
	Fire:
		ENFG A 0 A_JumpIfInventory("EnfieldLoaded", 1, 1);
		goto Dryfire;
		ENFG A 0 A_GunFlash();
		ENFG A 0 A_StartSound("enfield/fire", CHAN_WEAPON);
		ENFG A 0 A_AlertMonsters();
		ENFG A 1 A_FireProjectile("EnfieldTracer");
		ENFG A 1 Offset(0,52) ACS_NamedExecute("WWNaziTaunt", 0, 1);
		ENFG A 1 Offset(0,45) A_SetPitch(pitch - (4.0 * CallACS("RecoilAmount")));
		ENFG A 1 Offset(0,40) A_SetPitch(pitch - (2.0 * CallACS("RecoilAmount")));
		ENFG A 1 Offset(0,36) A_SetPitch(pitch + (1.0 * CallACS("RecoilAmount")));
		ENFG A 1 Offset(0,34) A_SetPitch(pitch + (1.0 * CallACS("RecoilAmount")));
		ENFG A 1 Offset(0,33) A_SetPitch(pitch + (0.5 * CallACS("RecoilAmount")));
		ENFG A 1 Offset(-2,33) A_StartSound("enfield/boltback", 5);
		ENFG A 1 Offset(-4,34);
		ENFG A 1 Offset(-6,35);
		ENFG A 2 Offset(-8,36);
		ENFG A 2 Offset(-4,42);
		ENFG A 1 Offset(0,51) A_SpawnItemEx("EnfieldRifleCasing", 12, -20, 32, 8, random(-2, 2), random(0, 4), random(-55, -80), SXF_NOCHECKPOSITION);
		ENFG A 1 Offset(4,60);
		ENFG A 2 Offset(5,74);
		ENFG A 3 Offset(6,76);
		ENFG A 2 Offset(5,74) A_StartSound("enfield/boltfwd", 5);
		ENFG A 1 Offset(4,60);
		ENFG A 1 Offset(0,51);
		ENFG A 1 Offset(-4,42);
		ENFG A 2 Offset(-8,36);
		ENFG A 1 Offset(-6,35);
		ENFG A 1 Offset(-4,34);
		ENFG A 1 Offset(-2,33);
		TNT1 A 0 A_CheckReload();
		goto Ready;
	Reload:
		ENFG A 0 ACS_NamedExecute("WWNaziTaunt", 0, 2);
		ENFG A 1 Offset(-2,33) A_StartSound("enfield/boltback", 5);
		ENFG A 1 Offset(-4,34);
		ENFG A 1 Offset(-6,35);
		ENFG A 2 Offset(-8,36);
		ENFG A 2 Offset(-4,42);
		ENFG A 1 Offset(0,51);
		ENFG A 1 Offset(4,60);
		ENFG A 2 Offset(5,74);
		ENFG A 3 Offset(6,76);
		ENFG A 2 Offset(5,74);
		ENFG A 8 Offset(6,76);
		ENFG A 0 A_JumpIfInventory("EnfieldLoaded", 5, "ReloadLoop2");
		ENFG A 0 A_JumpIfInventory("Three03Ammo", 5, 1);
		goto ReloadLoop2;
		TNT1 A 0 A_TakeInventory("Three03Ammo", 5);
		TNT1 A 0 A_GiveInventory("EnfieldLoaded", 5);
		ENFG A 1 Offset(6,80) A_StartSound("enfield/load", 5);
		ENFG A 1 Offset(6,84);
		ENFG A 1 Offset(6,87);
		ENFG A 1 Offset(7,90);
		ENFG A 1 Offset(8,92);
		ENFG A 1 Offset(9,88);
		ENFG A 1 Offset(8,82);
		ENFG A 1 Offset(7,77);
		ENFG A 0 A_JumpIfInventory("EnfieldLoaded", 0, "ReloadOnlyOneClip");
		TNT1 A 0 A_JumpIfInventory("Three03Ammo", 1, 1);
		goto ReloadOnlyOneClip;
		ENFG A 5 Offset(6, 75);
	ReloadLoop2:
		TNT1 A 0 A_TakeInventory("Three03Ammo", 1);
		TNT1 A 0 A_GiveInventory("EnfieldLoaded");
		TNT1 A 0 A_JumpIfInventory("EnfieldLoaded", 0, "ReloadFinish");
		TNT1 A 0 A_JumpIfInventory("Three03Ammo", 1, "ReloadLoop2");
	ReloadFinish:
		ENFG A 1 Offset(6,80) A_StartSound("enfield/load", 5);
		ENFG A 1 Offset(6,84);
		ENFG A 1 Offset(6,87);
		ENFG A 1 Offset(7,90);
		ENFG A 1 Offset(8,92);
		ENFG A 1 Offset(9,88);
		ENFG A 1 Offset(8,82);
		ENFG A 1 Offset(7,77);
	ReloadOnlyOneClip:
		ENFG A 2 Offset(5,74) A_StartSound("enfield/boltfwd", 5);
		ENFG A 1 Offset(4,60);
		ENFG A 1 Offset(0,51);
		ENFG A 1 Offset(-4,42);
		ENFG A 2 Offset(-8,36);
		ENFG A 1 Offset(-6,35);
		ENFG A 1 Offset(-4,34);
		ENFG A 1 Offset(-2,33);
		goto Ready;
	Flash:
		ENFF A 1 Bright A_Light2();
		ENFF B 1 Bright;
		TNT1 A 2 A_Light1();
		goto LightDone;
	Spawn:
		ENFP A -1;
		stop;
	}
}

class EnfieldLoaded : Ammo
{
	default
	{
		Tag ".303 British";
		Inventory.MaxAmount 10;
		Inventory.Icon "ENFPA0";

		+INVENTORY.IGNORESKILL;
	}
}

class EnfieldSpawner : Actor
{
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("EnfieldPickup", 0, 0, 0, vel.x, vel.y, vel.z);
		TNT1 A 0 A_SpawnItemEx("Three03AmmoBox", frandom(-16, 16), frandom(-16, 16), 0, vel.x, vel.y, vel.z, 0, SXF_NOCHECKPOSITION);
		stop;
	}
}

class EnfieldPickup : NaziWeaponPickup
{
	states
	{
	Spawn:
		ENFP A 0 NoDelay A_SetUserVar("user_weapon", 16);
		goto Idle;
	}
}
