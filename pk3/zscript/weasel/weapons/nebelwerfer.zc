class Nebelwerfer : NaziWeapon
{
	default
	{
		Tag "Klein Nebelwerfer";
		Obituary "%o rode %k's rocket.";
		Inventory.PickupMessage "You got the Klein Nebelwerfer!";
		Weapon.SelectionOrder 2500;
		Weapon.AmmoUse 1;
		Weapon.AmmoType "NebAmmo";
		Weapon.UpSound "nebelwerfer/select";

		-WEAPON.NOALERT;
		-WEAPON.AMMO_CHECKBOTH;
	}

	states
	{
	Ready:
		NEBG A 1 A_WeaponReady();
		loop;
	Deselect:
		NEBG A 0 A_Lower();
		NEBG A 1 A_Lower();
		loop;
	Select:
		NEBG A 0 A_Raise();
		NEBG A 1 A_Raise();
		loop;
	Fire:
		NEBG A 0 A_GunFlash();
		NEBG A 0 A_StartSound("nebelwerfer/fire",CHAN_WEAPON);
		NEBG A 2 A_FireProjectile("NebRocket");
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt",0,1);
		NEBG BCDEB 1 A_SetPitch(pitch-(0.5*CallACS("RecoilAmount")));
		NEBG A 2 A_ReFire();
		goto Ready;
	Flash:
		NEBF A 2 Bright A_Light(2);
		TNT1 A 2 A_Light(1);
		goto LightDone;
	Spawn:
		NEBL A -1;
		stop;
	}
}

class NebelwerferSpawner : Actor
{
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("NebelwerferPickup", 0, 0, 0, vel.x, vel.y, vel.z);
		TNT1 A 0 A_SpawnItemEx("NebAmmo", frandom(-16, 16), frandom(-16, 16), 0, vel.x, vel.y, vel.z, 0, SXF_NOCHECKPOSITION);
		stop;
	}
}

class NebelwerferPickup : NaziWeaponPickup
{
	states
	{
	Spawn:
		NEBL A 0 NoDelay A_SetUserVar("user_weapon", 21);
		goto Idle;
	}
}

class NebRocket : Actor
{
	default
	{
		Scale .5;
		Radius 11;
		Height 8;
		Speed 35;
		Damage 8;
		Projectile;
		DeathSound "nebelwerfer/xplode";
		Decal "Scorch";
		ProjectileKickback 100;

		+RANDOMIZE;
		+FORCERADIUSDMG;
	}

	states
	{
	Spawn:
		MNSS A 1 Bright A_SpawnItemEx("RocketFlame", random(-1, 1), 0, random(-1, 1));
		loop;
	Death:
		FRME A 1 Bright A_Explode(64, 56);
		FRME BCDEFGHIHJKLMNOPQRS 1 Bright;
		stop;
	}
}

class RocketFlame : Actor
{
	default
	{
		Scale 0.15;
		Alpha 0.5;
		Renderstyle "Add";
		Projectile;

		+NOCLIP;
	}

	states
	{
	Spawn:
		TNT1 A 1;
		XPLO AB 2 Bright;
		XPLO C 2 Bright A_SpawnItemEx("RPG8RocketTrail", random(-1, 1), 0, 1);
		stop;
	}
}

class RPG8RocketTrail : RocketFlame
{
	default
	{
		Scale 0.18;
		Alpha 0.6;
	}

	states
	{
	Spawn:
		SMOK ABCDEFGHIJKLMNOPQ 1;
		stop;
	}
}
