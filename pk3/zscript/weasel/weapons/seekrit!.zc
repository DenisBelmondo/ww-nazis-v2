class Firebrand : NaziWeapon
{
	default
	{
		Tag "Firebrand";
		Inventory.PickupMessage "Mad Jack Churchill now considers you properly dressed!";
		Inventory.PickupSound "sword/draw";
		Weapon.SelectionOrder 9999;
		Weapon.UpSound "sword/draw";

		+WEAPON.NOAUTOFIRE;
		+WEAPON.NOALERT;
		-WEAPON.AMMO_CHECKBOTH;
	}

	states
	{
	Select:
		SRDG A 0 A_Raise();
		SRDG A 1 A_Raise();
		loop;
	Deselect:
		SRDG A 0 A_Lower();
		SRDG A 1 A_Lower();
		loop;
	Ready:
		TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2", 1, 2);
		SRDG A 1 A_WeaponReady();
		loop;
		SRDF A 3 A_WeaponReady();
		SRDI BC 3 A_WeaponReady();
		loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2", 1, "SuperFire");
		SRDG B 0 A_JumpIfInventory("SwordSeq", 2, "Fire3");
		SRDG B 1 A_JumpIfInventory("SwordSeq", 1, "Fire2");
		SRDG C 1 A_GiveInventory("SwordSeq");
		TNT1 A 4;
		SRDG DE 1;
		SRDG F 1 A_StartSound("sword/swing");
		SRDG GHI 1;
		SRDG J 1 A_CustomPunch(35,1,0,"SwordPuff",96);
		SRDG KLM 1;
		TNT1 A 8 A_WeaponReady();
		SRDG C 1;
		SRDG B 1 A_TakeInventory("SwordSeq");
		goto ready;
	Fire2:
		TNT1 A 5;
		SRDG N 1 A_GiveInventory("SwordSeq");
		SRDG O 1 A_StartSound("sword/swing");
		SRDG PQR 1;
		SRDG S 1 A_CustomPunch(45,1,0,"SwordPuff",96);
		SRDG TUV 1;
		TNT1 A 8 A_WeaponReady();
		TNT1 A 0 A_TakeInventory("SwordSeq");
		goto Ready;
	Fire3:
		TNT1 A 4;
		SRDG DE 1;
		SRDG F 1 A_StartSound("sword/swing");
		SRDG GHI 1;
		SRDG J 1 A_CustomPunch(35,1,0,"SwordPuff",96);
		SRDG KL 1;
		SRDG M 1 A_TakeInventory("SwordSeq",1);
		TNT1 A 8 A_WeaponReady();
		SRDG C 1;
		SRDG B 1 A_TakeInventory("SwordSeq");
		goto Ready;
	SuperFire:
		SRDF B 0 A_JumpIfInventory("SwordSeq",2,"SuperFire3");
		SRDF B 1 A_JumpIfInventory("SwordSeq",1,"SuperFire2");
		SRDF C 1 A_GiveInventory("SwordSeq");
		TNT1 A 4;
		SRDG DE 1;
		SRDF F 1 A_StartSound("flamesword/swing");
		SRDF GHI 1;
		SRDF J 1 A_CustomPunch(80,1,0,"FireSwordPuff",96);
		SRDF KLM 1;
		TNT1 A 8 A_WeaponReady();
		SRDF C 1;
		SRDF B 1 A_TakeInventory("SwordSeq");
		goto ready;
	SuperFire2:
		TNT1 A 5;
		SRDF N 1 A_GiveInventory("SwordSeq");
		SRDF O 1 A_StartSound("flamesword/swing");
		SRDF PQR 1;
		SRDF S 1 A_CustomPunch(100,1,0,"FireSwordPuff",96);
		SRDF TUV 1;
		TNT1 A 8 A_WeaponReady();
		TNT1 A 0 A_TakeInventory("SwordSeq");
		goto Ready;
	SuperFire3:
		TNT1 A 4;
		SRDG DE 1;
		SRDF F 1 A_StartSound("flamesword/swing");
		SRDF GHI 1;
		SRDF J 1 A_CustomPunch(80,1,0,"FireSwordPuff",96);
		SRDF KL 1;
		SRDF M 1 A_TakeInventory("SwordSeq",1);
		TNT1 A 8 A_WeaponReady();
		SRDF C 1;
		SRDF B 1 A_TakeInventory("SwordSeq");
		goto Ready;
	AltFire:
		TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"Altfire2");
		SRDG A 1 ACS_NamedExecute("WWNaziTaunt",0,1);
		SRDG BC 1;
		TNT1 A 7 Thing_Damage(0,25);
		SRDG C 1 A_GiveInventory("PowerWeaponLevel2");
		SRDG BA 1;
		goto Ready;
	Altfire2:
		SRDF BC 1;
		TNT1 A 4;
		SRDG DE 1;
		SRDF F 1 A_StartSound("flamesword/swing");
		SRDF GH 1;
		SRDF I 1 Thing_Damage(0,2);
		SRDF J 1 A_FireProjectile("FirebrandFireball");
		SRDF KLM 1;
		TNT1 A 20 A_WeaponReady();
		SRDF C 1;
		SRDF B 1 A_TakeInventory("SwordSeq");
		goto Ready;
	Spawn:
		SRDP A -1 Bright;
		loop;
	}
}

class FirebrandPickup : NaziWeaponPickup
{
	default
	{
		Scale .8;
	}

	states
	{
	Spawn:
		SRDP A 0 NoDelay A_SetUserVar("user_weapon", 3);
		goto Idle;
	}
}

class FirebrandFireball : Actor
{
	default
	{
		Projectile;
		Radius 12;
		Height 12;
		Speed 40;
		DamageFunction (40);
		RenderStyle "Add";
		DeathSound "world/barrelboom";
		DamageType "Fire";
		//Obituary "%o was killed by %k.";
		ProjectileKickback 30;
	}

	states
	{
	Spawn:
		CFFX N 1 Bright A_SpawnItemEx("FirebrandFireballTrail");
		loop;
	Death:
		CFCF Q 2 Bright A_Scream();
		CFCF R 2 Bright A_Explode(16, 128, 0);
		CFCF STUVWXYZ 2 Bright;
		stop;
	}
}

class FirebrandFireballTrail : Actor
{
	default
	{
		RenderStyle "Add";
	}

	states
	{
	Spawn:
		CFFX NOP 4 Bright;
		stop;
	}
}

class SwordPuff : Actor
{
	default
	{
		AttackSound "sword/wall";
		SeeSound "sword/hit";
		//Obituary "%o was killed by %k.";
		ProjectileKickback 10;

		+NOBLOCKMAP;
		+NOGRAVITY;
		+PUFFONACTORS;
	}

	states
	{
	Spawn:
	Melee:
		TNT1 A 1;
		stop;
	Crash:
		TNT1 A 0 A_SpawnDebris("SwordPuffSpark");
		TNT1 A 1 A_AlertMonsters();
		stop;
	}
}

class FireSwordPuff : SwordPuff
{
	default
	{
		DamageType "Fire";
		ProjectileKickback 30;
		SeeSound "flamesword/hit";
	}
}

class SwordPuffSpark : Actor
{
	default
	{
		Health 4;
		radius 3;
		height 6;
		speed .1;
		RenderStyle "Add";
		Scale .4;
		Mass 0;
		BounceType "Doom";

		+MISSILE;
		+FLOORCLIP;
		+DONTSPLASH;
	}

	states
	{
	Spawn:
		PUFF A 1;
		PUFF A 5 Bright A_SetTranslucent(0.8, 1);
		PUFF A 6 Bright A_SetTranslucent(0.6, 1);
		PUFF A 8 Bright A_SetTranslucent(0.4, 1);
		PUFF A 10 Bright A_SetTranslucent(0.2, 1);
		stop;
	}
}

class SwordSeq : Inventory
{
	default
	{
		Inventory.MaxAmount 2;
	}
}

class WWKeen : CommanderKeen replaces CommanderKeen
{
	default
	{
		DropItem "KeenSpawner";
	}
}

class KeenSpawner : RandomSpawner
{
	default
	{
		DropItem "FirebrandPickup";
	}
}
