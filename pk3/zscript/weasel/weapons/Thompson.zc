//Rate of fire: 700 rounds/min - Wikipedia
class Thompson : NaziWeapon
{
	default
	{
		Scale 0.5;
		Tag "Thompson M1A1";
		Inventory.PickupMessage "You got the Thompson M1A1!";
		Weapon.SelectionOrder 600;
		Weapon.AmmoType "ThompsonLoaded";
		Weapon.AmmoUse 1;
		Weapon.AmmoType2 "45ACPAmmo";
		Weapon.AmmoUse2 1;
		Weapon.UpSound "thompson/select";
	}

	states
	{
	Ready:
		THOM A 0 A_JumpIfInventory("ThompsonLoaded", 0, 2);
		THOM A 0 A_JumpIfInventory("45ACPAmmo", 1, 2);
		THOM A 1 A_WeaponReady();
		loop;
		THOM A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		loop;
	Deselect:
		THOM A 0 A_Lower();
		THOM A 1 A_Lower();
		loop;
	Select:
		THOM A 0 A_Raise();
		THOM A 1 A_Raise();
		loop;
	Fire:
		THOM A 0 A_JumpIfInventory("ThompsonLoaded", 1, 1);
		goto Dryfire;
		THOM A 0 A_GunFlash();
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1);
		THOM A 0 A_StartSound("thompson/fire", CHAN_WEAPON);
		THOM A 0 A_SpawnItemEx("45ACPCasing", 12, -20, 32, 8, random(-2, 2), random(0, 4), random(-55, -80), SXF_NOCHECKPOSITION);
		THOM A 0 A_AlertMonsters();
		THOM A 1 A_FireProjectile("ThompsonTracer", frandom(-1.5, 1.5), 1, 0, 0, 0, frandom(-1.0, 1.0));
		THOM A 1 Offset(0, 40) A_SetPitch(pitch - (0.5 * CallACS("RecoilAmount")));
		THOM A 1 Offset(0, 34);
		TNT1 A 0 A_Refire();
		goto Ready;
	Hold:
		THOM A 0 A_JumpIfInventory("ThompsonLoaded", 1, 1);
		goto Dryfire;
		THOM A 0 A_GunFlash();
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1);
		THOM A 0 A_StartSound("thompson/fire", CHAN_WEAPON);
		THOM A 0 A_SpawnItemEx("45ACPCasing", 12, -20, 32, 8, random(-2, 2), random(0, 4), random(-55, -80), SXF_NOCHECKPOSITION);
		THOM A 0 A_AlertMonsters();
		THOM A 1 Offset(0, 36) A_FireProjectile("ThompsonTracer", frandom(-1.5, 1.5), 1, 0, 0, 0, frandom(-1.0, 1.0));
		THOM A 1 Offset(0, 40) A_SetPitch(pitch - (0.5 * CallACS("RecoilAmount")));
		THOM A 1 Offset(0, 34);
		TNT1 A 0 A_Refire();
		goto Ready;
	Reload:
		THOM A 1 Offset(0, 35) A_StartSound("thompson/reload", 5);
		THOM A 1 Offset(0, 38) ACS_NamedExecute("WWNaziTaunt", 0, 2);
		THOM A 1 Offset(0, 44);
		THOM A 1 Offset(-2, 46);
		THOM A 1 Offset(-4, 52);
		THOM A 1 Offset(-7, 56);
		THOM A 1 Offset(-10, 57);
		THOM A 8 Offset(-11, 58);
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("45ACPAmmo", 1, TIF_NOTAKEINFINITE);
		TNT1 A 0 A_GiveInventory("ThompsonLoaded");
		TNT1 A 0 A_JumpIfInventory("ThompsonLoaded", 0, "ReloadFinish");
		TNT1 A 0 A_JumpIfInventory("45ACPAmmo", 1, "ReloadLoop");
	ReloadFinish:
		THOM A 1 Offset(-10, 58);
		THOM A 1 Offset(-10, 61);
		THOM A 1 Offset(-10, 65);
		THOM A 1 Offset(-10, 71);
		THOM A 1 Offset(-10, 65);
		THOM A 1 Offset(-10, 60);
		THOM A 1 Offset(-10, 55);
		THOM A 1 Offset(-10, 53);
		THOM A 1 Offset(-10, 51);
		THOM A 1 Offset(-9, 50);
		THOM A 1 Offset(-8, 46);
		THOM A 1 Offset(-7, 43);
		THOM A 1 Offset(-5, 40);
		THOM A 1 Offset(-3, 37);
		THOM A 1 Offset(-1, 34);
		THOM A 1 Offset(0, 32);
		goto Ready;
	Flash:
		THOM B 1 Bright A_Light(2);
		THOM C 1 Bright;
		TNT1 A 2 A_Light(1);
		goto LightDone;
	Spawn:
		THOM P -1;
		stop;
	}
}

class ThompsonLoaded : Ammo
{
	default
	{
		Tag ".45 ACP";
		Inventory.MaxAmount 20;
		Inventory.Icon "THOMP0";

		+INVENTORY.IGNORESKILL;
	}
}

class ThompsonSpawner : Actor
{
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("ThompsonPickup", 0, 0, 0, vel.x, vel.y, vel.z);
		TNT1 AAAAAAAA 0 A_SpawnItemEx("45ACPAmmoBox", frandom(-16, 16), frandom(-16, 16), 0, vel.x, vel.y, vel.z, 0, SXF_NOCHECKPOSITION);
		stop;
	}
}

class ThompsonPickup : NaziWeaponPickup
{
	default
	{
		Scale .5;
	}

	states
	{
	Spawn:
		THOM P 0 NoDelay A_SetUserVar("user_weapon", 13);
		goto Idle;
	}
}
