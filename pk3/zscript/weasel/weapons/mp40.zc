//Rate of fire: 500-550 rounds/min - Wikipedia
class MP40 : NaziWeapon
{
	default
	{
		Scale .5;
		Tag "MP 40";
		Inventory.PickupMessage "You got the MP 40!";
		Weapon.SelectionOrder 600;
		Weapon.kickback 60;
		Weapon.AmmoType "MP40Loaded";
		Weapon.AmmoUse 1;
		Weapon.ammotype2 "ParabellumAmmo";
		Weapon.ammouse2 1;
		Weapon.UpSound "mp40/select";
	}

	states
	{
	Ready:
		RIFG A 0 A_JumpIfInventory("MP40Loaded", 0, 2);
		RIFG A 0 A_JumpIfInventory("ParabellumAmmo", 1, 2);
		RIFG A 1 A_WeaponReady();
		loop;
		RIFG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		loop;
	Deselect:
		RIFG A 0 A_Lower();
		RIFG A 1 A_Lower();
		loop;
	Select:
		RIFG A 0 A_Raise();
		RIFG A 1 A_Raise();
		loop;
	Fire:
		RIFG A 0 A_JumpIfInventory("MP40Loaded", 1, 1);
		goto Dryfire;
		RIFG A 0 A_GunFlash();
		RIFG A 0 A_SetPitch(pitch - (0.5 * CallACS("RecoilAmount")));
		RIFG A 0 A_StartSound("mp40/fire", CHAN_WEAPON);
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1);
		RIFG A 0 A_SpawnItemEx("ParabellumCasing", 12, -20, 32, 8, random(-2,2), random(0,4), random(-55, -80), SXF_NOCHECKPOSITION);
		RIFG A 0 A_AlertMonsters();
		RIFG A 1 A_FireProjectile("MP40Tracer", frandom(-1.4, 1.4));
		RIFG B 1 Offset(0, 38);
		RIFG B 1 Offset(0, 44);
		RIFG B 1 Offset(0, 40);
		RIFG A 0 A_JumpIfInventory("MP40Seq", 1, "Fire2");
		TNT1 A 0 A_GiveInventory("MP40Seq");
		TNT1 A 0 A_CheckReload();
		goto Ready;
	Fire2:
		RIFG A 1 Offset(0, 36) A_TakeInventory("MP40Seq");
		TNT1 A 0 A_CheckReload();
		goto Ready;
	Reload:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 2);
		RIFG A 1 Offset(0, 35) A_StartSound("mp40/reload", 5);
		RIFG A 1 Offset(0, 38);
		RIFG A 1 Offset(0, 44);
		RIFG B 1 Offset(0, 52);
		RIFG B 1 Offset(-1, 54);
		RIFG B 1 Offset(-2, 56);
		RIFG B 1 Offset(-3, 58);
		RIFG B 1 Offset(-4, 58);
		RIFG B 1 Offset(-4, 57);
		RIFG B 1 Offset(-3, 54);
		RIFG B 1 Offset(-3, 51);
		RIFG B 1 Offset(-3, 53);
		RIFG B 1 Offset(-3, 55);
		RIFG B 1 Offset(-3, 56);
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("ParabellumAmmo", 1, TIF_NOTAKEINFINITE);
		TNT1 A 0 A_GiveInventory("MP40Loaded");
		TNT1 A 0 A_JumpIfInventory("MP40Loaded", 0, "ReloadFinish");
		TNT1 A 0 A_JumpIfInventory("ParabellumAmmo", 1, "ReloadLoop");
	ReloadFinish:
		RIFG B 1 Offset(-3, 57);
		RIFG B 1 Offset(-3, 59);
		RIFG B 1 Offset(-3, 63);
		RIFG B 1 Offset(-3, 67);
		RIFG B 1 Offset(-3, 65);
		RIFG B 1 Offset(-3, 62);
		RIFG B 1 Offset(-3, 58);
		RIFG B 1 Offset(-3, 55);
		RIFG B 1 Offset(-2, 53);
		RIFG B 1 Offset(-2, 49);
		RIFG B 1 Offset(-2, 46);
		RIFG B 1 Offset(-2, 45);
		RIFG B 1 Offset(-1, 44);
		RIFG B 1 Offset(-1, 46);
		RIFG B 1 Offset(-1, 47);
		RIFG B 1 Offset(0, 49) A_WeaponReady(WRF_NOBOB);
		RIFG A 1 Offset(0, 44) A_WeaponReady(WRF_NOBOB);
		RIFG A 1 Offset(0, 38) A_WeaponReady(WRF_NOBOB);
		RIFG A 1 Offset(0, 35) A_WeaponReady(WRF_NOBOB);
		RIFG A 1 Offset(0, 32) A_WeaponReady(WRF_NOBOB);
		goto Ready;
	Flash:
		RIFF A 1 Bright A_Light(2);
		TNT1 A 1;
		TNT1 A 2 A_Light(1);
		goto LightDone;
	Spawn:
		MP40 A -1;
		stop;
	}
}

class MP40Loaded : Ammo
{
	default
	{
		Tag "9x19mm";
		Inventory.MaxAmount 32;
		Inventory.Icon "MP40A0";

		+INVENTORY.IGNORESKILL;
	}
}

class MP40Spawner : Actor
{
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("MP40Pickup", 0, 0, 0, vel.x, vel.y, vel.z);
		TNT1 AA 0 A_SpawnItemEx("ParabellumAmmo", frandom(-16, 16), frandom(-16, 16), 0, vel.x, vel.y, vel.z, 0, SXF_NOCHECKPOSITION);
		stop;
	}
}

class MP40Pickup : NaziWeaponPickup
{
	default
	{
		Scale .5;
	}

	states
	{
	Spawn:
		MP40 A 0 NoDelay A_SetUserVar("user_weapon", 10);
		goto Idle;
	}
}

class MP40Seq : Inventory {}
