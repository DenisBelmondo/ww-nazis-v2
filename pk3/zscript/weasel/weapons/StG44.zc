//Rate of fire: 550-600 rounds/min - Wikipedia
class STG44 : NaziWeapon
{
	default
	{
		Scale .5;
		Tag "Sturmgewehr 44";
		Inventory.pickupmessage "You got the Sturmgewehr 44!";
		Weapon.SelectionOrder 700;
		Weapon.AmmoType "STG44Loaded";
		Weapon.AmmoUse 1;
		Weapon.ammotype2 "792KurzAmmo";
		Weapon.ammouse2 1;
		Weapon.UpSound "stg44/select";
	}

	states
	{
	Ready:
		STGG A 0 A_JumpIfInventory("STG44Loaded", 0, 2);
		STGG A 0 A_JumpIfInventory("792KurzAmmo", 1, 2);
		STGG A 1 A_WeaponReady();
		loop;
		STGG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		loop;
	Deselect:
		STGG A 0 A_Lower();
		STGG A 1 A_Lower();
		loop;
	Select:
		STGG A 0 A_Raise();
		STGG A 1 A_Raise();
		loop;
	Fire:
		STGG A 0 A_JumpIfInventory("STG44Loaded", 1, 1);
		goto Dryfire;
		STGG A 0 A_GunFlash();
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1);
		STGG A 0 A_StartSound("stg44/fire", CHAN_WEAPON);
		STGG A 0 A_SpawnItemEx("STGCasing", 12, -20, 32, 8, random(-2, 2), random(0, 4), random(-55, -80), SXF_NOCHECKPOSITION);
		STGG A 0 A_AlertMonsters();
		STGG A 1 A_FireProjectile("STGTracer", frandom(-2.5, 2.5), 1, 0, 0, 0, frandom(-1.0, 1.0));
		STGG A 1 Offset(0, 38) A_SetPitch(pitch - (1.0 * CallACS("RecoilAmount")));
		STGG A 1 Offset(0, 37);
		STGG A 1 Offset(0, 35);
		STGG A 1 Offset(0, 33);
		TNT1 A 0 A_CheckReload();
		goto Ready;
	Reload:
		STGG A 1 Offset(0, 35) A_StartSound("stg44/reload", 5);
		STGG A 1 Offset(0, 38) ACS_NamedExecute("WWNaziTaunt", 0, 2);
		STGG A 1 Offset(0, 44);
		STGG A 1 Offset(-2, 46);
		STGG A 1 Offset(-4, 52);
		STGG A 1 Offset(-7, 56);
		STGG A 1 Offset(-10, 57);
		STGG A 8 Offset(-11, 58);
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("792KurzAmmo", 1, TIF_NOTAKEINFINITE);
		TNT1 A 0 A_GiveInventory("STG44Loaded");
		TNT1 A 0 A_JumpIfInventory("STG44Loaded", 0, "ReloadFinish");
		TNT1 A 0 A_JumpIfInventory("792KurzAmmo", 1, "ReloadLoop");
	ReloadFinish:
		STGG A 1 Offset(-10, 58);
		STGG A 1 Offset(-10, 61);
		STGG A 1 Offset(-10, 65);
		STGG A 1 Offset(-10, 71);
		STGG A 1 Offset(-10, 65);
		STGG A 1 Offset(-10, 60);
		STGG A 1 Offset(-10, 55);
		STGG A 1 Offset(-10, 53);
		STGG A 1 Offset(-10, 51);
		STGG A 1 Offset(-9, 50);
		STGG A 1 Offset(-8, 46);
		STGG A 1 Offset(-7, 43);
		STGG A 1 Offset(-5, 40);
		STGG A 1 Offset(-3, 37);
		STGG A 1 Offset(-1, 34);
		STGG A 1 Offset(0, 32);
		goto Ready;
	Flash:
		STGF A 1 Bright A_Light(2);
		STGF B 1 Bright;
		TNT1 A 2 A_Light(1);
		goto LightDone;
	Spawn:
		STGI A -1;
		stop;
	}
}

class STG44Loaded : Ammo
{
	default
	{
		Tag "7.92x33mm";
		Inventory.MaxAmount 30;
		Inventory.Icon "STGIA0";

		+INVENTORY.IGNORESKILL;
	}
}

class STG44Spawner : Actor
{
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("STG44Pickup", 0, 0, 0, vel.x, vel.y, vel.z);
		TNT1 A 0 A_SpawnItemEx("792KurzAmmo", frandom(-16, 16), frandom(-16, 16), 0, vel.x, vel.y, vel.z, 0, SXF_NOCHECKPOSITION);
		stop;
	}
}

class STG44Pickup : NaziWeaponPickup
{
	default
	{
		Scale .5;
	}

	states
	{
	Spawn:
		STGI A 0 NoDelay A_SetUserVar("user_weapon", 18);
		goto Idle;
	}
}
