class GrenadePickup : CustomInventory
{
	default
	{
		Inventory.PickupMessage "Picked up a grenade.";
		Tag "Frag Grenade";
		Inventory.MaxAmount 9;
		Inventory.Icon "TNT1A0";
		Inventory.UseSound "grenade/throw";
		Inventory.PickupSound "grenade/pickup";
		Inventory.InterHubamount 9;

		+INVENTORY.INVBAR;
		+INVENTORY.KEEPDEPLETED;
	}

	states
	{
	Spawn:
		THRP A -1;
		stop;
	Use:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 3);
		TNT1 A 0 A_FireProjectile("HandGrenade", 0, 0, 0, 0, FPF_NOAUTOAIM);
		stop;
	}
}

class HandGrenade : Actor
{
	default
	{
		Radius 4;
		Height 3;
		Speed 40;
		DamageFunction (1);
		DamageType "Frag";
		Projectile;
		Obituary "%o failed to field %k's fragmenting fastball.";
		BounceType "Doom";
		BounceFactor 0.2;
		WallBounceFactor 0.6;
		BounceSound "grenade/bounce";

		-NOGRAVITY;
		-NOTELEPORT;
		+CANBOUNCEWATER;
	}

	states
	{
	Spawn:
		THRW A 2 A_Countdown();
		THRW BCDEFGH 2;
		loop;
	Death:
		THRW B 35;
		FRME A 0 A_AlertMonsters();
		FRME A 0 A_SetScale(1.75, 1.75);
		FRME A 0 A_SetTranslucent(0.75, 1);
		FRME A 0 A_StartSound("grenade/explode", 0, 1.0, 0, 0.1);
		FRME A 1 Bright A_Explode(128, 256, 1, 1, 128, 15, 2);
		FRME B 1 Bright Radius_Quake(10, 10, 0, 120, 0);
		FRME CDEFGHIHJKLMNOPQRS 1 bright;
		stop;
	}
}
