class G43 : NaziWeapon
{
	default
	{
		Tag "Gewehr 43";
		Inventory.PickupMessage "You got the Gewehr 43!";
		Weapon.SelectionOrder 750;
		Weapon.AmmoType "G43Loaded";
		Weapon.AmmoUse 1;
		Weapon.AmmoType2 "MauserAmmo";
		Weapon.AmmoUse2 1;
		Weapon.UpSound "g43/select";

		+WEAPON.NOAUTOFIRE;
	}

	states
	{
	Select:
		M1GG A 0 A_Raise();
		M1GG A 1 A_Raise();
		loop;
	Deselect:
		M1GG A 0 A_Lower();
		M1GG A 1 A_Lower();
		loop;
	Ready:
		M1GG A 0 A_JumpIfInventory("G43Loaded", 0, 2);
		M1GG A 0 A_JumpIfInventory("MauserAmmo", 1, 2);
		M1GG A 1 A_WeaponReady();
		loop;
		M1GG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		loop;
	Fire:
		M1GG A 0 A_JumpIfInventory("G43Loaded", 1, 1);
		goto Dryfire;
		M1GG A 0 A_StartSound("g43/fire", CHAN_WEAPON);
		M1GG A 0 A_GunFlash();
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1);
		M1GG A 0 A_SpawnItemEx("MauserRifleCasing", 12, -20, 32, 8, random(-2, 2), random(0, 4), random(-55, -80), SXF_NOCHECKPOSITION);
		M1GG A 0 A_AlertMonsters();
		M1GG A 1 A_FireProjectile("G43Tracer");
		M1GG A 1 A_SetPitch(pitch - (1.4 * CallACS("RecoilAmount")));
		M1GG CDEDC 1;
		TNT1 A 0 A_CheckReload();
		M1GG AAFFFA 1 A_WeaponReady(WRF_NOBOB);
		goto Ready;
	Reload:
		TNT1 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 2);
		M1GG A 1 Offset(0, 35) A_StartSound("g43/reload", 5);
		M1GG A 1 Offset(-2, 38);
		M1GG A 1 Offset(-4, 44);
		M1GG A 1 Offset(-6, 52);
		M1GG A 1 Offset(-7, 57);
		M1GG C 1 Offset(-8, 62);
		M1GG C 1 Offset(-9, 67);
		M1GG C 1 Offset(-10, 72);
		M1GG C 1 Offset(-11, 77);
		M1GG C 1 Offset(-11, 80);
		M1GG D 1 Offset(-12, 82);
		M1GG D 1 Offset(-12, 84);
		M1GG D 1 Offset(-13, 86);
		M1GG E 1 Offset(-13, 87);
		M1GG E 2 Offset(-14, 88);
		M1GG E 3 Offset(-14, 89);
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("MauserAmmo", 1, TIF_NOTAKEINFINITE);
		TNT1 A 0 A_GiveInventory("G43Loaded");
		TNT1 A 0 A_JumpIfInventory("G43Loaded", 0, "ReloadFinish");
		TNT1 A 0 A_JumpIfInventory("MauserAmmo", 1, "ReloadLoop");
	ReloadFinish:
		M1GG D 2 Offset(-10, 96);
		M1GG D 1 Offset(-6, 100);
		M1GG C 1 Offset(-7, 90);
		M1GG C 1 Offset(-6, 80);
		M1GG C 1 Offset(-4, 70);
		M1GG A 1 Offset(-2, 60);
		M1GG A 1 Offset(0, 50);
		M1GG A 1 Offset(0, 40);
		M1GG A 1 Offset(0, 32);
		goto Ready;
	Flash:
		M1GF A 1 Bright A_Light(2);
		M1GF B 1 Bright;
		TNT1 A 2 A_Light(1);
		goto LightDone;
	Spawn:
		M1GP A -1;
		stop;
	}
}

class G43Loaded : Ammo
{
	default
	{
		Tag "7.92x57mm";
		Inventory.MaxAmount 10;
		Inventory.Icon "M1GPA0";

		+INVENTORY.IGNORESKILL;
	}
}

class G43Spawner : Actor
{
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("G43Pickup", 0, 0, 0, vel.x, vel.y, vel.z);
		TNT1 AA 0 A_SpawnItemEx("MauserAmmo", frandom(-16, 16), frandom(-16, 16), 0, vel.x, vel.y, vel.z, 0, SXF_NOCHECKPOSITION);
		stop;
	}
}

class G43Pickup : NaziWeaponPickup
{
	states
	{
	Spawn:
		M1GP A 0 NoDelay A_SetUserVar("user_weapon", 15);
		goto Idle;
	}
}
