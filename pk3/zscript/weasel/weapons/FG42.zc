//Rate of fire: ~750-900 rounds/min - Wikipedia
class FG42 : NaziWeapon
{
	default
	{
		Scale .38;
		Tag "FG 42";
		Inventory.PickupMessage "You got the Fallschirmjagergewehr 42!";
		Weapon.SelectionOrder 700;
		Weapon.AmmoType "FG42Loaded";
		Weapon.AmmoUse 1;
		Weapon.AmmoType2 "MauserAmmo";
		Weapon.AmmoUse2 1;
		Weapon.UpSound "fg42/select";
	}

	states
	{
	Ready:
		FG42 A 0 A_JumpIfInventory("FG42Loaded", 0, 2);
		FG42 A 0 A_JumpIfInventory("MauserAmmo", 1, 2);
		FG42 A 1 A_WeaponReady();
		loop;
		FG42 A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		loop;
	Deselect:
		FG42 A 0 A_Lower();
		FG42 A 1 A_Lower();
		loop;
	Select:
		FG42 A 0 A_Raise();
		FG42 A 1 A_Raise();
		loop;
	Fire:
		FG42 A 0 A_JumpIfInventory("FG42Loaded", 1, 1);
		goto Dryfire;
		FG42 A 0 A_GunFlash();
		FG42 A 0 A_StartSound("fg42/fire", CHAN_WEAPON);
		FG42 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1);
		FG42 A 0 A_SpawnItemEx("MauserRifleCasing", 12, -20, 32, 8, random(-2,2), random(0,4), random(-55,-80), SXF_NOCHECKPOSITION);
		FG42 A 0 A_AlertMonsters();
		FG42 A 1 A_FireProjectile("FGTracer", frandom(-2.5, 2.5), 1, 0, 0, 0, frandom(-1.0, 1.0));
		FG42 A 1 Offset(0, 42) A_SetPitch(pitch - (1.8 * CallACS("RecoilAmount")));
		FG42 A 1 A_Refire();
		FG42 A 1 A_WeaponReady(WRF_NOBOB);
		FG42 A 1 Offset(0,44) A_WeaponReady(WRF_NOBOB);
		FG42 A 1 Offset(0,43) A_WeaponReady(WRF_NOBOB);
		FG42 A 1 Offset(0,40) A_WeaponReady(WRF_NOBOB);
		FG42 A 1 Offset(0,36) A_WeaponReady(WRF_NOBOB);
		FG42 A 1 Offset(0,32) A_WeaponReady(WRF_NOBOB);
		goto Ready;
	Hold:
		FG42 A 0 A_JumpIfInventory("FG42Loaded", 1, 1);
		goto Dryfire;
		FG42 A 0 A_GunFlash();
		FG42 A 0 A_StartSound("fg42/fire", CHAN_WEAPON);
		FG42 A 0 ACS_NamedExecute("WWNaziTaunt", 0, 1);
		FG42 A 0 A_SpawnItemEx("MauserRifleCasing", 12, -20, 32, 8, random(-2, 2), random(0, 4), random(-55, -80), SXF_NOCHECKPOSITION);
		FG42 A 0 A_AlertMonsters();
		FG42 A 1 Offset(0,58) A_FireProjectile("FGTracer", frandom(-2.5, 2.5), 1, 0, 0, 0, frandom(-1.0, 1.0));
		FG42 A 1 Offset(0,54) A_SetPitch(pitch - (1.8 * CallACS("RecoilAmount")));
		FG42 A 1 A_Refire();
		FG42 A 1 Offset(0, 52) A_WeaponReady(WRF_NOBOB);
		FG42 A 1 Offset(0, 54) A_WeaponReady(WRF_NOBOB);
		FG42 A 1 Offset(0, 53) A_WeaponReady(WRF_NOBOB);
		FG42 A 1 Offset(0, 50) A_WeaponReady(WRF_NOBOB);
		FG42 A 1 Offset(0, 46) A_WeaponReady(WRF_NOBOB);
		FG42 A 1 Offset(0, 41) A_WeaponReady(WRF_NOBOB);
		FG42 A 1 Offset(0, 35) A_WeaponReady(WRF_NOBOB);
		FG42 A 1 Offset(0, 32) A_WeaponReady(WRF_NOBOB);
		goto Ready;
	Reload:
		FG42 A 1 Offset(0, 35) A_StartSound("fg42/reload", 5);
		FG42 A 1 Offset(0, 38) ACS_NamedExecute("WWNaziTaunt", 0, 2);
		FG42 A 1 Offset(0, 44);
		FG42 A 1 Offset(-2, 46);
		FG42 A 1 Offset(-4, 52);
		FG42 A 1 Offset(-7, 56);
		FG42 A 1 Offset(-10, 57);
		FG42 A 8 Offset(-11, 58);
	ReloadLoop:
		TNT1 A 0 A_TakeInventory("MauserAmmo", 1, TIF_NOTAKEINFINITE);
		TNT1 A 0 A_GiveInventory("FG42Loaded");
		TNT1 A 0 A_JumpIfInventory("FG42Loaded", 0, "ReloadFinish");
		TNT1 A 0 A_JumpIfInventory("MauserAmmo", 1, "ReloadLoop");
	ReloadFinish:
		FG42 A 1 Offset(-10, 58);
		FG42 A 1 Offset(-10, 61);
		FG42 A 1 Offset(-10, 65);
		FG42 A 1 Offset(-10, 71);
		FG42 A 1 Offset(-10, 65);
		FG42 A 1 Offset(-10, 60);
		FG42 A 1 Offset(-10, 55);
		FG42 A 1 Offset(-10, 53);
		FG42 A 1 Offset(-10, 51);
		FG42 A 1 Offset(-9, 50);
		FG42 A 1 Offset(-8, 46);
		FG42 A 1 Offset(-7, 43);
		FG42 A 1 Offset(-5, 40);
		FG42 A 1 Offset(-3, 37);
		FG42 A 1 Offset(-1, 34);
		FG42 A 1 Offset(0, 32);
		goto Ready;
	Flash:
		FG42 B 1 Bright A_Light2();
		FG42 C 1 Bright;
		TNT1 A 2 A_Light1();
		goto LightDone;
	Spawn:
		FG42 P -1;
		stop;
	}
}

class FG42Loaded : Ammo
{
	default
	{
		Tag "7.92x57mm";
		Inventory.MaxAmount 20;
		Inventory.Icon "FG42P0";

		+INVENTORY.IGNORESKILL;
	}
}

class FG42Spawner : Actor
{
	states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("FG42Pickup", 0, 0, 0, vel.x, vel.y, vel.z);
		TNT1 AA 0 A_SpawnItemEx("MauserAmmo", frandom(-16, 16), frandom(-16, 16), 0, vel.x, vel.y, vel.z, 0, SXF_NOCHECKPOSITION);
		stop;
	}
}

class FG42Pickup : NaziWeaponPickup
{
	default
	{
		Scale .38;
	}

	states
	{
	Spawn:
		FG42 P 0 NoDelay A_SetUserVar("user_weapon",19);
		goto Idle;
	}
}
